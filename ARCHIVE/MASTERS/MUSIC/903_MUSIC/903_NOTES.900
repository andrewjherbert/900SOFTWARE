notes;
"begin"

"comment" Program to analyze music tapes in the form of pairs of integers,
	  the first being a "frequency", the second being a "duration". The
	  list should be terminated by a -1 sentinel. Produces a histogram
	  of the "frequencies" used in the music tape;

    "integer" "array" name [1:20], note [1:40], count [1: 40];
    "integer" lo, hi, mid, i, freq, dur, next;
    "real" r;

    "comment" Output to teleprinter;
    punch(3);

    "comment" Set next to first free element in note / count;
    next := 1;

    "comment" Read in data;
    i := 1; instring (name, i);
    i := 1; outstring(name, i); "print" {{l3}};

Read:

    "read" freq; "if" freq < 0 "then" "goto" Results;
    "read" dur;

    "comment" See if we have seen this note already;
    	lo := 1; hi := next-1;

    "comment" Binary search;
Search:  

    "if" lo > hi "then" "begin"
	mid := lo; "goto" Insert
    "end";
 
    mid := (lo+hi)"div"2;

    "if" note[mid] = freq "then" "begin"
	count[mid] := count[mid]+1; "goto" Print 
    "end";

    "if" note[mid] < freq "then" 
        lo := mid+1
    "else"
	hi := mid-1;

    "goto" Search;


    "comment" Need to insert new entry in list;
Insert:

    "for" i := next "step" -1 "until" mid+1 "do" "begin"
	note[i] := note[i-1]; count[i] := count[i-1]
    "end";
    note[mid] := freq; count[mid] := 1;
    next := next + 1; 

    "comment" Calculate duration of note;
Print:

    r := (note[mid]*3.0+96.0)*dur;  "comment" usec;
    r := r / 140000 + 0.5;
    i := r;

    "comment" "print" sameline, digits(4), freq, i, {{l}};
    "goto" Read;

    "comment" print out results;
Results:

    "print" {{l2}   Note  Count{l}};

    "for" i := 1 "step" 1 "until" next-1 "do"
	"print" note[i], sameline, count[i];

"end"
<! halt !>



    

