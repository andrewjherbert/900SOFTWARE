<,]?!>[

HGPM TESTS
==========

Examples from Thimbleby Thesis
------------------------------

Layout
------

[DEF,A,!ALL ON !
ONE LINE]!
[A] SB ALL ON ONE LINE

NOTE Macro
----------

[NOTE,Hello World] SB MONITOR:  (Hello World)

LEG Macro
---------

[DEF,TEST,<[LEG,?1,L,E,G]>][TEST,-1][TEST,0][TEST,+1] SB LEG
Show all args are evaluated!
[LEG,0,[NOTE,L],[NOTE,E],[NOTE,G]]
Use DEF and quoting to make selective!
[DEF,COND,[LEG,0,<[NOTE,L]>,<[NOTE,E]>,<[NOTE,G]>]][COND]

DIV Macro
---------

[DEF,DIV,< 14 8176
 13 ?1
 14 8191
>]
<[DIV,12]: >!
[DIV,12] SB 13 12

Differentiation
---------------

[DEF,X,<X,1>]!
[DEF,Y,<Y,DY/DX>]!
[DEF,D,<D/DX?1 = ?2>]!
[DEF,+,<(?1+?3),(?2+?4)>]!
[DEF,SIN,<SIN(?1),?2*COS(?1)>]!
[D,[+,[X],[Y]]] SB (1+DY/DX)

IF Macro
--------

[DEF,IF,<[DEF,?1,?4][DEF,?2,?3][A]>]!
[IF,A,B,TRUE,FALSE] SB FALSE
[IF,A,A,TRUE,FALSE] SB TRUE

FOR macro
---------

[DEF,FOR,<[DEF,?1,XXXXXXX][UPDATE,?1,1]!
[DEF,SUBFOR,?3<[DEF,[?1],<[UPDATE,?1,[BAR,+,[?1],1]][SUBFOR,?1,?2,?3]>]!
[DEF,?2,<>][[?1],?1,?2,?3]>][SUBFOR,?1,?2,?3]>]

[FOR,J,4,< 4 X[J]
 5 P+[J]
>]

Delayed SIR Code
----------------

[DEF,POOLT,A VERY LONG STRING A VERY LONG STRING A VERY LONG STRING !
A VERY LONG STRING A VERY LONG STRING A VERY LONG STRING A VERY LONG STRING !
A VERY LONG STRING A VERY LONG STRING A VERY LONG STRING A VERY LONG STRING !
A VERY LONG STRING A VERY LONG STRING A VERY LONG STRING]!
[UPDATE,POOLT,<>]!
[DEF,POOL,<[POOLT][UPDATE,POOLT,<>]>]!
[DEF,NAME,131071][UPDATE,NAME,1]!
[DEF,LOC,<L[NAME]!
[UPDATE,POOLT,L[NAME] ?1
[POOLT]]!
[UPDATE,NAME,[BAR,+,1,[NAME]]]>]!

	 0	+0
	 4	WORD
LOOP 	 9	[LOC,!
	10	1
	 8	SHIFT]
	 7	[LOC,!
	 4	1
	 0	LINK
	/8	1]
SHIFT   14	1
	 8	LOOP
[POOL]   

Functional Tests
================

Text processing
---------------

Isolated apply is just copied ].

Isolated argument is just copied ?.

<Nested <quotes < nested < again > > > .>

End of text tests.

Test DEF and UPDATE.

<[DEF,abc,bluebell][abc] => [DEF,abc,bluebell][abc].

<[DEF,x,<result is ?1>][x,3] => [DEF,x,<result is ?1>][x,3].

<[DEF,y,<result is ?1 + ?2>][y,5,6] => [DEF,y,<result is ?1 + ?2>][y,5,6].

<[DEF,z,<?12>][z,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15] = >!
[DEF,z,<?12>][z,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15].

<[DEF,abc,<pqrstuv>][UPDATE,abc,<xyz>][abc] = >!
[DEF,abc,<pqrstuv>][UPDATE,abc,<xyz>][abc].

End of DEF and UPDATE tests. 

Test arithmetic.

[DEF,+,<[BAR,+,?1,?2]>]!
[DEF,-,<[BAR,-,?1,?2]>]!
[DEF,*,<[BAR,*,?1,?2]>]!
[DEF,/,<[BAR,/,?1,?2]>]!
[DEF,%,<[BAR,%,?1,?2]>]!

7+3=[+,7,3]
7-3=[-,7,3]
7*3=[*,7,3]
7/3=[/,7,3]
<7%3=[%,7,3]>

End of arithmetic tests.

Demonstrate how to construction conditional macros.

Conditional
<[DEF,IF,<[?1,[DEF,?1,?4][DEF,?2,?3]]>]>[DEF,IF,<[?1,[DEF,?1,?4][DEF,?2,?3]]>]
IF X=Y = [IF,X,Y,T,F]
IF X=X = [IF,X,X,T,F]

This demonstrates a subtle bug in Strachey's GPM which is
not present in BGPM.

<[DEF,args,<arg0=?0 arg1=?1 arg2=?2 arg3=?3>]>
[DEF,args,<arg0=?0 arg1=?1 arg2=?2 arg3=?3>]

<[args,aaa[DEF,junk,junkbody]xxx,bbbyyy,ccczzz]> =
[args,aaa[DEF,junk,junkbody]xxx,bbbyyy,ccczzz]

<[args,aaaxxx,bbbyyy,ccczzz[DEF,junk,junkbody]]> =
[args,aaaxxx,bbbyyy,ccczzz[DEF,junk,junkbody]]

<[args,aaaxxx,bbbyyy,ccczzz,[DEF,junk,junkbody]]> =
[args,aaaxxx,bbbyyy,ccczzz,[DEF,junk,junkbody]]

This is to demonstrate that GPM macros can leave definitions
behind.

[DEF,defs,<!
Define ?1a[DEF,?1a,<Macro ?0 called with argument ?1>]
Define ?1b[DEF,?1b,<Macro ?0 called with argument ?1>]
Define ?1c[DEF,?1c,<Macro ?0 called with argument ?1>]
>]!

<[defs,A]> = [defs,A]
<[defs,B]> = [defs,B]
<[defs,C]> = [defs,C]

<[Aa,xxx]> = [Aa,xxx]
<[Bb,yyy]> = [Bb,yyy]
<[Cc,zzz]> = [Cc,zzz]

<Calling [DEF,a,<[DEF,b,[c]xxx[d]]>]>  [DEF,a,<[DEF,b,[c]xxx[d]]>]

<Calling [DEF,c,old c][DEF,d,old d]> [DEF,c,old c][DEF,d,old d]

<Calling [a,[DEF,c,ccc][DEF,d,ddd]]>  [a,[DEF,c,ccc][DEF,d,ddd]]
<Calling [b]> = [b]
<Calling [c]> = [c]
<Calling [d]> = [d]

Examples from Strachey's Computer Journal Paper
-----------------------------------------------

General Purpose Macrogenerator
by C. Strachey
CJ Vol.8 No.3, Oct 1965

Examples of macro expansion.

<[DEF,A,<A?1A>]>[DEF,A,<A?1A>]
<[DEF,B,<B[A,X?1X]B>]>[DEF,B,<B[A,X?1X]B>]
<[DEF,APA,<P?1?1P>]>[DEF,APA,<P?1?1P>]

<[A,C]>       = [A,C]
<[A,ACA]>     = [A,ACA]
<[A,[A,C]]>   = [A,[A,C]]
<[A,XDX]>     = [A,XDX]
<[B,D]>       = [B,D]
<[A,P]>       = [A,P]
<[APA,Y]>     = [APA,Y]
<[[A,P],Y]>   = [[A,P],Y]

<Q<[A,C]>R>   = Q<[A,C]>R
<[A,<[A,X]>]> = [A,<[A,X]>]
<[B,<[A,X]>]> = [B,<[A,X]>]
<Q<[>R<]>>    = Q<[>R<]>
<Q<<[A,C]>>R> = Q<<[A,C]>>R

<[A,X,U,[DEF,A,<?1?2?1>]]> = [A,X,U,[DEF,A,<?1?2?1>]]

Functional product example.

<[DEF,Log,<log(?1)>] [DEF,Sin,<sin(?1)>]>!
[DEF,Log,<log(?1)>] [DEF,Sin,<sin(?1)>]
<[Log,x] [Sin,x] = >[Log,x] [Sin,x] 

<[DEF,FNPROD,<[DEF,?1?2,<[>?1<,[>?2<,?1]]>]>] = >!
[DEF,FNPROD,<[DEF,?1?2,<[>?1<,[>?2<,?1]]>]>]
<[FNPROD,Log,Sin]> = [FNPROD,Log,Sin]
<[LogSin,x]> = [LogSin,x]

Modified version of Sum, correcting error in original.

This version corrects the bug in Successor as defined
in Strachey's paper.

[DEF,Suc,<[1,2,3,4,5,6,7,8,9,10,[DEF,1,<?>?1]]>]!

<[Suc,0]> = [Suc,0]
<[Suc,1]> = [Suc,1]
<[Suc,2]> = [Suc,2]
<[Suc,9]> = [Suc,9]

[DEF,Successor,<[?2,[DEF,?2,?1<<,>[Suc,>?2<]>]!
[DEF,9,<[Suc,>?1<]<,>0>]]>]!

<[Successor,3,4]> = [Successor,3,4]
<[Successor,3,9]> = [Successor,3,9]

[DEF,Sum,<[S,?1,?2,0,!
[DEF,S,<[?3,[DEF,?3,<[S,>[Successor,?1,?2]<,>[Suc,?3]<]>]!
[DEF,>?3<,?1<,>?2]]>]]>]!

<[Sum,3,4,2] = > [Sum,3,4,2]
<[Sum,3,8,3] = > [Sum,3,8,3]

End of examples from CJ paper.

Martin Richards Examples
------------------------

Towers of Hanoi.

[DEF,H,<[?1,?2,?3,?4,
[DEF,?1,<!
[H,[-,?0,1],?1,?3,?2]!
Move disc ?0 from tower ?1 to tower ?2
[H,[-,?0,1],?3,?2,?1]>][DEF,0,]]>]

<[H,1,A,B,C] = >
[H,1,A,B,C]

<[H,2,A,B,C] = >
[H,2,A,B,C]

<[H,3,A,B,C] = >
[H,3,A,B,C]

<[H,4,A,B,C] = >
[H,4,A,B,C]

End of Hanoi.

END OF HGPM TESTS.
<! halt !><!!>

c change warning chars
j 34

c run functional tests
j 32
st 1112

c Collatz sequences
at ptr file ../../scripts/gpm/tgpm.bin
j 8181
st 917

select out tty

at ptr inline

Collatz conjecture: Every Collatz sequence end with a one.

I.e. can it ever diverge, or end in a loop not containing 1?

[DEF,DEC,<?1>]!
[DEF,BIN,<?1>]!
[DEF,+,<[DEC,[BAR,+,[BIN,?1],[BIN,?2]]]>]!
[DEF,-,<[DEC,[BAR,-,[BIN,?1],[BIN,?2]]]>]!
[DEF,*,<[DEC,[BAR,*,[BIN,?1],[BIN,?2]]]>]!
[DEF,/,<[DEC,[BAR,/,[BIN,?1],[BIN,?2]]]>]!
[DEF,%,<[DEC,[BAR,R,[BIN,?1],[BIN,?2]]]>]!

[DEF,collatz,<[C,
[DEF,n,0000000000]
[UPDATE,n,[+,?1,0]]
[DEF,R0,<[UPDATE,n,[/,[n],2]]>]
[DEF,R1,<[UPDATE,n,[+,[*,3,[n]],1]]>]
[DEF,next,<[R[%,[n],2]]>]
[DEF,C,<
      [n]!
[[n],[DEF,[n],<[next][C]>][DEF,1,]]>]]>]

<[collatz,7] = >
[collatz,7]

<[collatz,767] = >
[collatz,767]

End of Collatz.

<! halt !><!!>
j 32
st 1112

c Primes
at ptr file ../../scripts/gpm/tgpm.bin
j 8181
st 917

select out tty

at ptr  inline

Primes.

<[prime,n] = > the nth prime number.

[DEF,+,<[BAR,+,?1,?2]>]!
[DEF,%,<[BAR,R,?1,?2]>]!

[DEF,isprime,<[p%?1=[%,?2,?1],!
[DEF,p%?1=[%,?2,?1],<[isprime,[+,>?1<,1],>?2<]>]!
[DEF,p%?1=0,no]!
[DEF,p%?2=0,yes]!
]>]!

[DEF,nextprime,<[UPDATE,p,[+,[p],1]]!
[test=[isprime,2,[p]],[DEF,test=yes,][DEF,test=no,<[nextprime]>]]>]!

[DEF,iter,<[res[n],!
[DEF,res[n],<[UPDATE,i,[+,[i],1]][nextprime][iter]>]!
[DEF,res[i],<[p]>]]>]!

[DEF,prime,<[iter,!
[DEF,n,[+,?1,0]]!
[DEF,i,1padding][UPDATE,i,1]!
[DEF,p,2padding][UPDATE,p,2]!
]>]!

<[isprime,2,2]>   = [isprime,2,2]
<[isprime,2,3]>   = [isprime,2,3]
<[isprime,2,4]>   = [isprime,2,4]
<[isprime,2,5]>   = [isprime,2,5]
<[isprime,2,6]>   = [isprime,2,6]
<[isprime,2,9]>   = [isprime,2,9]
<[isprime,2,136]> = [isprime,2,136]

<[prime,1]   = > [prime,1]
<[prime,2]   = > [prime,2]
<[prime,3]   = > [prime,3]
<[prime,8]   = > [prime,8,Biggest prime we can do]

End of primes.

<! halt !><!!>
j 32

c Permutations
at ptr file ../../scripts/thimbleby/tgpm.bin
j 8181
st 917

select out tty

at ptr inline

Permutations.

[DEF,inc,<[BAR,+,?1,1]>]!
[DEF,dec,<[BAR,-,?1,1]>]!

[DEF,perm,<[perms,1,?1,     ,?2,
[DEF,arg,<[list,
[DEF,S,<?>?1<>]]>]
[DEF,ax,<[?1,?1,[DEF,?1,<<,>[arg,?1]>][DEF,?2,]]>]
[DEF,allbut,<[ax,?1,?2][?1,?1,?2,?3,[DEF,?1,<[allbut,[inc,?1],?2,?3]>]
[DEF,?3,]]>]
[DEF,perms,<[?2,?1,?2,?3,?4,[DEF,list,<[>S?4<]>]
[DEF,?2,<[perms,1,[dec,?2],?3[arg,?1],[allbut,1,?1,?2]][?1,?1,?2,?3,?4,
[DEF,?1,<[perms,[inc,?1],?2,?3,?4]>][DEF,?2,][debug]]>][DEF,0,?3
]]>]]>]!

[DEF,debug,]

Permuting A B C

<[perm,3,<,A,B,C>] = >
[perm,3,<,A,B,C>]

<! halt !>
