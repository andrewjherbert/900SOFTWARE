

*+22
 (EDITOR 9 VERSION [C] 3/11/78 C.H.M. )

[ HEADER TTSP QSTOP PRINT QCHOP QDOUT HELPF PROFLG LOA  LENGTH
 AS BFMAX ROL RC REPF ]

HEADER 8 NLS   (RESTART)
 8 ;+2       (NORMAL ENTRY)
 8 LDHLP     (LOAD  HELP)
 4 +3
 5 QDOUT
 4 LNL
 5 QSTOP
 11 PRINT
 8 PRINT+1
+3
&010000
\EDI
\TOR
\ 9C
&010000
&030000
NLS 4 +3
 5 QDOUT
 4 +1
 5 RC+7    (SET COMM.STRING EMPTY)
 4 +1
 11 QCHOP
 8 QCHOP+1
 8 TTSP2

LDHLP 11 LOA
 8 LOA+1
 4 +0
 5 PROFLG
 5 HELPF
 8 ;+0


TTSP1 11 PRINT
 8 PRINT+1
 +3
 &764253   (^BK)
 &010000
 &030000
 11 ROL
 8 ROL+1

TTSP2 4 LNL-1    (DESCEND TO LEVEL 4)
  5 2
 5 4
 4 LNL
 5 6
 15 7168
 8 TTSP1
LNL 8 NL



NL 4 +3  (ENTRY AFTER ERRORS)
 5 QDOUT
 4 +1          (SET LAST CHAR TO <NL>)
 5 RC+7
 11 QCHOP
 8 QCHOP+1
 4 +0
 5 REPF
 8 TTSP


[ TTSP QDIN QDOUT LL QCHOP CS RC PRINT INSS QSTOP ENDFLG PROFLG 
 SRC4 SRC5 SFG ]

TTSP 4 +3          (TELETYPE SUPERVISORY PROGRAM)
 5 QDIN
 5 QDOUT
 4 +0
 5 ENDFLG
 4 SFG
 7 ;+2
 8 REST
 4 RC+7                (LAST CHAR)
 8 CHK
RCR1 11 RC
 8 RC+1
CHK 1 -1
 7 NCL
 1 -26
 7 RCR2
 8 RCR1
RCR2 11 RC
 8 RC+1
 1 -1
 7 NCL
 1 +1
 11 AZ
 8 AZ+1
 7 RCR2
 0 +0
 14 12
 5 INS
 11 RC
 8 RC+1
 11 AZ
 8 AZ+1
 7 DONEC
 0 +0
 14 6
 1 INS
 5 INS
 11 RC
 8 RC+1
 11 AZ
 8 AZ+1
 7 DONEC
 1 INS
 5 INS
DONEC 4 DS
 5 CD
 4 +0
 5 TOOFAR
AGAIN 0 CD
 4 INS
/2 0
 7 FOUND
 10 CD
 10 CD
/4 0
 1 +2
 7 PROGS
/4 0
 1 +1
 7 ;+2
 8 AGAIN
 11 PRINT
 8 PRINT+1
+3
\INV
\ALI
\D  
\INS
\TRU
\CTI
\ON 
&030000
 8 QSTOP
NCL 4 SFG
 7 ;+2
 8 REST
 4 CS
 5 LL+2
 4 +40
 5 LL+4
 4 &12
 11 QCHOP
8 QCHOP+1
 11 LL
8 LL+1
 11 RC
 8 RC+2
 8 RCR2

REST 4 +0
 5 SFG
 4 SRC4
 5 RC+4
 4 SRC5
 5 RC+5
 4 CS
 5 RC+8
 8 RCR1

AZ +0
 5 CD
 1 -33
 9 NAZ
 1 -26
 9 ;+2
 8 NAZ
 4 CD
AZX 0 AZ
 /8 1
NAZ 4 +0
 8 AZX

INS +0
CD +0
DS 0 INSS

FOUND 4 PROFLG
 7 ;+2
 8 FO
 4 TOOFAR
 7 ;+2
 8 NOPRO
FO /0 1
/11 0
/8 1
 8 TTSP

PROGS 10 TOOFAR
 8 AGAIN
NOPRO 11 PRINT
 8 PRINT+1
 +3
 \NO 
 \PRO
 \GRA
 \M  
 &030000
 8 QSTOP

PROFLG +0  (=1 IF PROG IN STORE)
TOOFAR +0  (=1 IF INS ONLY WITH PROG IN STORE)



[ SID SOD DOSAVE DISAVE PRINT QSTOP NIFCS ]

SID +0
 11 CIN
 8 CIN+1
 5 DISAVE
 0 SID
/8 1

SOD +0
 11 CIN
 8 CIN+1
 5 DOSAVE
 0 SOD
/8 1

CIN +0  (READ DEV. NO. FROM COMM. LINE)
 11 NIFCS
 8 NIFCS+1
 8 NOD  (NEG)
 7 SPEC (=0)
 2 +4   (4-X)
 9 NOD
 2 +4
 0 CIN
/8 1
SPEC 4 -1
 8 ;-3

NOD 11 PRINT
 8 PRINT+1
 +3
\NO 
\DEV
\ICE
&030000
 8 QSTOP

[ NIFCS RC ]

NIFCS +0   (NO. I/P FROM COMM. STRING)

(EXIT POINTS:)
(- SIGNED)
(UNSIGNED,ZERO,NO ARGUMENT)
(+ SIGNED)
 4 +0
 5 VALUE
 5 INC
 4 RC+7
 8 ;+3
R1 11 RC
 8 RC+1
 1 -1
 7 EX  ((X=1,NL)
 1 -10
 7 INC1  (X=11,+)
 1 -2
 7 INC2  (X=13,-)
 1 -14
 7 EX     (;)
 1 +1
 9 ;+2   (X<26)
 8 R1    (IGNORE LEADING CHARS)
 1 +10
 9 R1    (IGNORE LEADING PUNCTUATION)
 8 NUMBS (NUMBERS ONLY)
INC1 4 +1
 8 ;+2
INC2 4 -1
 5 INC
 8 R1
NUMBS 5 VALUE
R2 11 RC
 8 RC+1
 1 -26
 9 ;+2
 8 EX   (A-Z WILL TERMINATE) (X>25)
 1 +10  (X-16)
 9 EX
 5 CHAR
 4 VALUE
 12 +10
 14 17
 1 CHAR
 5 VALUE
 8 R2
EX 4 INC
 9 NEG
 8 EX1
NEG 4 VALUE
 2 +0
 5 VALUE
EX1 4 INC
 1 =/8 2
 5 JUMP
 4 VALUE
 0 NIFCS
JUMP /8 1
CHAR +0
INC +0
VALUE +0

[ SSC RSC PSC PRO  SCE BC TRAIL DOSAVE QDOUT TL2B ]


PSC +0
 4 DOSAVE
 5 QDOUT
 11 TRAIL
 8 TRAIL+1
 0 PSC
 /8 1


SSC +0
4 +0
 5 SCE
 0 SSC
/8 1

RSC +0
 4 +1
 5 SCE
 0 RSC
/8 1

SCE +0    (SET TO +1 TO ENABLE STOPCODE)
          (+1 IMPLIES END OF TAPE REACHED)

PRO +0   (PUNCH RUN-OUT)
 4 -150
 5 COUNT
AGAIN 4 +0
 15 6144
 10 COUNT
 4 COUNT
 0 PRO
/7 1
 8 AGAIN


COUNT +0


[ CLP SCE LTX PNE ]

CLP +0      (CONTINUOUS LOAD & PUNCH)
 11 LTX
 8 LTX+1
 11 PNE
 8 PNE+1
 4 SCE
 7 CLP+1
 0 CLP
 /8 1


[ LOA BLC LTX PN MNL ]

LOA +0       (LOAD 1ST TAPE)
 4 +0
 5 BLC
 5 PN
 11 LTX
 8 LTX+1
 11 MNL
 8 MNL+1
 0 LOA
/8 1

[ CON BLC ELC LTX RST SCE LTX1 AS AF BFMAX DISAVE EOT MNL ]

CON +0
 4 DISAVE
 9 DEFAULT
 2 +1
 9 ;+2
 15 2048  (IGNORE LF)
 11 EOT
 8 EOT+1
 4 SCE
 7 MORE
 11 RST
 8 RST+1
 4 AS       (LOAD NEW TAPE AFTER OLD)
 2 AF
 1 AF
 2 BFMAX
 9 MORE  (NEW TAPE CANNOT GO AFTER OLD - NOT ROOM)
 11 LTX
 8 LTX1   (SPECIAL ENTRY POINT)
EXIT 11 MNL
 8 MNL+1
 0 CON
/8 1

DEFAULT 2 +0
 8 CON+3

MORE 4 ELC
 5 BLC
 11 LTX
 8 LTX+1
 8 EXIT

[ ITA ITB DISAVE QDIN IBA IBB ]

ITA +0
 4 DISAVE
 5 QDIN
 11 IBA
 8 IBA+1
 4 +3
 5 QDIN
 0 ITA
/8 1

ITB +0      (INSERT BLOCK FROM TAPE BEFORE CURRENT LINE)
 4 DISAVE
 5 QDIN
 11 IBB
 8 IBB+1
 4 +3
 5 QDIN
 0 ITB
/8 1


[ LTX CLR SCE AS AF LL DISAVE QDIN ROT LENGTH LTX1 PROFLG RST NIFCS 
 BFMAX MNL ]

LTX +0      (LOAD TEXT TAPE)
 11 CLR
 8 CLR+1
 10 PROFLG
 4 +1
 5 SCE
 4 AS
 8 NOTSPEC
LTX1 11 RST  (SPECIAL ENTRY FROM CONTINUE)
 8 RST+1
 4 AF
NOTSPEC 5 LL+2
 11 NIFCS
 8 NIFCS+1
 8 EX+2
 7 DEFAULT
WRP 5 PERCENT
 2 +120
 9 EX+2
 4 AS
 2 BFMAX
 14 8184
 12 PERCENT
 14 17
 2 +0
 5 LENGTH
 4 DISAVE
 5 QDIN
AGAIN 11 LL
 8 LL+1
 4 LL+5
 7 ;+2
 8 EXST    (EXIT IF STOPCODE)
 4 LL+3
 5 LL+2
 5 AF      (NEXT FREE WORD)
 11 ROT
 8 ROT+1
 4 AF
 2 AS
 2 LENGTH    (CHECK NOT TOO LONG)
 9 AGAIN
 4 +0
 5 SCE
EX 11 ROT
 8 ROT+1
 0 LTX
/8 1
EXST 4 LL+3
 1 -1
 5 AF   (AF IS NOW ADDRESS OF STOPCODE)
 0 AF
 4 +0
/5 0    (DELETE STOPCODE)
 8 EX
DEFAULT 4 +100
 8 WRP
LENGTH -2160  (THIS VALUE IS CALCULATED BY LTX TO NEW MAX.)
PERCENT +100


[ ROT AS AC AF BS BC L1S L1C L1F L2S L2C L2F NLC BLC BFMAX PRINT ]

ROT +0   (RESET TO ORIGINAL TEXT)
 4 AS
 5 AC
 4 AF
 1 +1
 5 BS
 5 BC
 4 AF
 2 AS
 2 BS
 2 BFMAX
 1 -20

 9 STFL
STFLR 4 L1S
 5 L1C
 5 L1F
 4 L2S
 5 L2C
 5 L2F
 4 BLC
 5 NLC
 0 ROT
/8 1
STFL 11 PRINT
 8 PRINT+1
 +3
 \STO
 \RE
 \FUL
 \L
 &010000
 &030000
 8 STFLR


[ EOT TL2B AC AF BFMAX BC BOF NLC ELC ]

EOT +0    (MOVE TO END OF TEXT)
 11 TL2B
 8 TL2B+1
RTN 4 AC
 2 AF
 7 END
 0 AC
/4 0
 0 BC
/5 0
 5 STORE
 2 &010000
 7 ;+2
 8 ;+2
 10 NLC
 4 STORE
 6 &7700
 2 &0100
 7 ;+2
 8 ;+2
 10 NLC
 4 STORE
 6 &77
 2 &01
 7 ;+2
 8 ;+2
 10 NLC
 4 BFMAX
 2 BC
 9 ;+3
 10 BC
 8 BOF
 10 AC
 10 BC
 8 RTN
END 4 NLC
 5 ELC
 0 EOT
/8 1

STORE +0



[ PNE EOT DOSAVE QDOUT BS PRINT BC SCE TRAIL ]

PNE +0     (PUNCH NEW EDITION)
 11 EOT
 8 EOT+1
 4 DOSAVE
 5 QDOUT
 4 BS
 5 PRINT+3
 4 BC
 5 PRINT+4
 11 PRINT
 8 PRINT+2
 4 SCE
 0 PNE
/7 1
 11 TRAIL
 8 TRAIL+1
 0 PNE
 /8 1



[ DET TL2B AF AC NLC ELC ]

DET +0     (DELETE TO END OF TEXT, INC. CUR. LINE)
 4 AF
 5 AC
 4 NLC
 5 ELC
 0 DET
/8 1



[  CLR AS AC AF ROT BFMAX HELPF PROFLG ]


CLR +0   (CLEAR ALL BUFFERS)
 10 HELPF
 4 +0
 5 PROFLG
 4 AS
 5 AC
 5 AF
AGAIN 4 +0
 0 AC
/5 0
 10 AC
 4 AC
 2 BFMAX
 7 ;+2
 8 AGAIN
 11 ROT
 8 ROT+1
 0 CLR
/8 1


[ RST EOT AS AC BS BC T ROT AF ]

RST +0    (RESET TO START OF NEW EDITION OF TEXT)
 11 EOT
 8 EOT+1
 4 AS
 5 AC
 4 BS
 5 X
 2 BC
 7 EX
TRANS 11 T
 8 T+1
 0 X
 0 AC
 4 X
 2 BC
 7 ;+2
 8 TRANS
 4 AC
 5 AF   (NEXT FREE WORD)
 11 ROT
 8 ROT+1

EX 0 RST
/8 1

X +0


[ ROL TL1L2D ]

ROL +0   (RETURN TO ORIGINAL LINE TEXT)
 11 TL1L2D
 8 TL1L2D+1
 0 ROL
/8 1


[ OCL NLC QOUT1 QCHOP L2S L2C L2F PRINT VERIFY ENDFLG BLC PN NP 
 MNCPL SIZE ]

OCL +0
 4 VERIFY
 7 SKIP
 9 SKIP
 4 ENDFLG
 7 ON
 4 NLC
 2 BLC
 7 FIRST
 4 SIZE
 2 PRINT+8
 9 ON
 7 NP1
 4 +10
 11 NP
 8 NP+3    (O/P 1 LF LESS)
 8 ON
NP1 11 NP
 8 NP+1
 8 ON
FIRST 11 NP
 8 NP+1
 8 SKIP
ON 4 NLC
 11 QOUT1
 8 QOUT1+1
 +0
 4 MNCPL
 1 -6
 5 MNCPL
 4 +160
 15 6148
 4 L2S
 5 PRINT+3
 4 L2F
5 PRINT+4
 11 PRINT
 8 PRINT+2
 4 MNCPL
 1 +6
 5 MNCPL
SKIP 4 L2S
 5 L2C
 0 OCL
/8 1


SIZE +60     (THIS SETS THE SIZE OF A PAGE)
PN +0     (   THIS IS PAGE NUMBER)
COUNT +0

[ RPN PN NIFCS ]

RPN +0  (RESET PAGE NO.)
 11 NIFCS
 8 NIFCS+1
 2 +0
 8 ;+3
 10 PN
 1 PN
 7 ;+2
 8 ;+2
 4 +1
 1 -1
 5 PN
 0 RPN
/8 1


[ DPC ENPAR ]

DPC +0
 4 +0
 5 ENPAR     (PARITY CHECKING DISABLED IF ENPAR=0)
 0 DPC
/8 1

[ EPC ENPAR ]

EPC +0
 10 ENPAR    (PARITY CHECKING ENABLED IF ENPAR"NE"0)
 0 EPC
/8 1


[ RLW MNCPL NIFCS ]
RLW +0
 11 NIFCS
 8 NIFCS+1
 2 +0
 7 EX
 5 MNCPL
EX 0 RLW
 /8 1

[ DSC DSFG SFG PRINT QSTOP LL QCHOP S ]

DSC +0
 4 SFG
 7 DOK
 11 PRINT
 8 PRINT+1
 +3
 \DSC
 \ IN
 \ SP
 \EC.
 &030000
 8 QSTOP

DOK 4 SS
 5 LL+2
 4 +40
 5 LL+4
 4 &41
 15 6148
 11 LL
 8 LL+1
 4 +0
 5 DSFG
 0 DSC
/8 1

DSFG +1
SS 0 S

[ SPS SIZE NIFCS ]
SPS +0    (SET PAGE SIZE)
 11 NIFCS
 8 NIFCS+1
 2 +0
 7 EX
 5 SIZE
EX 0 SPS
 /8 1

[ VDU PFLG TTY ]
VDU +0   ( DONT PRINT PAGE NOS, PAUSE AFTER 1 PAGE)
 4 +0
 5 PFLG
 0 VDU
 /8 1

TTY +0
 4 +1
 5 PFLG
 0 TTY
 /8 1

PFLG +1
[ REP REPF NIFCS RC QSTOP ]

REP +0   (REPEAT COMM. STRING)
 4 REPF
 7 FIRST
 4 REPCO
 1 -1
 5 REPCO
 7 QSTOP
 11 RC
 8 RC+2
 0 REP
/8 1
FIRST 11 NIFCS
 8 NIFCS+1
 8 ;+1
 7 INCRCO
IRCOR 5 REPCO
 10 REPF
 8 REP+1
INCRCO 1 +1
 8 IRCOR

REPF +0
REPCO +0

[ NP PRINT QOUT1 PN PFLG QSTOP ]

NP +0
 8 ;+3    (O/P NEW PAGE)
 4 +1    (O/P END OF LAST PAGE)
 8 ;+2
 4 +0
 5 FLAG
 4 PFLG
 7 VDUS
 4 +10
 15 6148
 15 6148
 4 +141
 15 6148
 11 PRINT
 8 PRINT+1
 +3
 \  -
 &010000
 &010000
 &010000
 &030000
 4 FLAG
 7 ;+2
 8 XNP
 11 PRINT
 8 PRINT+1
 +3
 +0
 +0
 +0
 +0
 +0
 +0
 \ PA
 \GE 
 &030000
 10 PN
 4 PN
 11 QOUT1
 8 QOUT1+1
 +0
 4 +141
 15 6148
 4 +10
 15 6148
 15 6148
XNP 4 +0
 5 FLAG
 5 PRINT+8   (RESET LINECOUNT)
 0 NP
/8 1
VDUS 4 PN
 10 PN
 7 XNP
 11 PRINT
 8 PRINT+1
 +3
 \...
 &030000
 4 +0
 15 2052
 2 +141
 7 XNP
 8 QSTOP
FLAG +0


[ DISAVE DOSAVE VERIFY ]

VERIFY +1 (+1 TO ENABLE VERIFICATION,+2 GLOBALS, +3 FOR MACRO.)
DISAVE -1
DOSAVE +1



(TRANSFER ROUTINES)

[  T  ]

T +0
 0 T
/0 1
/0 0
/4 0
 0 T
/0 2
/0 0
/5 0
 0 T
/0 1
/10 0
 0 T
/0 2
/10 0
 0 T
 5 X
 6 &77
 1 -1
/7 3
 4 X
 14 8186
 6 &77
 1 -1
/7 3
 4 X
 14 8180
 6 &77
 1 -1
/7 3
 1 -2
 7 ;+2
 8 T+1
 4 +3
/8 3      (STOPCODE NOW SUPERFLUOUS)



X +0



[ TL1L2D L1S L1C L1F L2S L2C L2F T OCL ]


TL1L2D +0
 4 L1S
 5 L1C
 2 L1F
 7 EX
 4 L2S
 5 L2C
 11 T
 8 T+1
 0 L1C
 0 L2C
 4 L2C
 5 L2F
 11 OCL
 8 OCL+1
EXIT 0 TL1L2D
/8 1

EX 4 L2S   (IMMEDIATE EXIT IF L1 EMPTY)
 5 L2C
 5 L2F
 8 EXIT


[ TCL2 RC WL2 L2F ]

TCL2 +0
 11 WL2
 8 WL2+2  (INITIALISE)
LOOP 11 RC
 8 RC+1   (GET NEXT CHAR)
 11 WL2
 8 WL2+1
 1 -1     (WAS IT A <NL>)
 7 ;+2    (IF SO EXIT)
 8 LOOP
 4 WL2+4
 5 L2F
 0 TCL2
/8 1

[ TL2L1 T L2S L2C L1S L1C L2F L1F ]

TL2L1 +0
 4 L2S
 5 L2C
 4 L1S
 5 L1C
 11 T
 8 T+1
 0 L2C
 0 L1C
 4 L1C
 5 L1F
 4 L2C
 5 L2F
 0 TL2L1
/8 1

[ TL2B T L2S L2C L2F BC BFMAX BOF ]

TL2B +0
 4 L2S
 5 L2C
 2 L2F
 7 ;+5
 11 T
 8 T+1
 0 L2C
 0 BC
 4 L2S
 5 L2C
 5 L2F
 4 BC
 2 +8192
 9 FIRST
OK 4 BC
 2 BFMAX
 9 BOF
 0 TL2B
/8 1
FIRST 4 BC
 2 +8139
 9 ;+2
 8 OK
 4 +8192
 5 BC
 8 OK



[ TAL1 AC AF END T L1C L1S L1F  ]

TAL1 +0    (TRANSFER A TO L1)
 4 L1S
 5 L1C
 5 L1F
 4 AC
 2 AF
 7 END
 11 T
 8 T+1
 0 AC
 0 L1C
 4 L1C
 5 L1F
 4 L1S
 5 L1C
 0 TAL1
 /8 1

[ MNL DCL ECL L2S L2C L2F AF T BC NLC L1S L1C AC 
 L1F OCL TL2B TL1L2D END CC TAL1 TCL2 TL2L1 ]

MNL +0    (MOVE TO NEXT LINE. NOT CALLED BY *N)
 11 TL2B
 8 TL2B+1
SKIP 4 AC
 2 AF
 7 END
 10 NLC
 11 TAL1
 8 TAL1+1
 11 TL1L2D
 8 TL1L2D+1
 0 MNL
/8 1


DCL +0  (DELETE CURRENT LINE)
 4 L2S
 5 L2C
 5 L2F  (SET L2 EMPTY)
 0 DCL
/8 1

ECL +0   (EXCHANGE CURRENT LINE)
 11 TL2L1
 8 TL2L1+1
 11 TCL2
 8 TCL2+1
 11 OCL
 8 OCL+1
 0 ECL
/8 1
[ ILB ILA T CC BC L2S L2C L2F TL2B L1S L1C L1F TL1L2D NLC OCL BFMAX
 BOF TCL2 TL2L1 ]

ILB +0    (INSERT COMMAND LINE BEFORE CURRENT LINE)
 11 TL2L1
 8 TL2L1+1
 11 TCL2
 8 TCL2+1
 11 TL2B
 8 TL2B+1
 11 TL1L2D
 8 TL1L2D+1
 0 ILB
/8 1

ILA +0   (INSERT COMM. LINE AFTER CUR. LINE)
 11 TL2B
 8 TL2B+1
 4 NLC
 5 SAFE
 4 +9999
 5 NLC
 11 TCL2
 8 TCL2+1
 11 TL2L1
 8 TL2L1+1
 11 TL1L2D
 8 TL1L2D+1
 4 SAFE
 5 NLC
 0 ILA
/8 1

SAFE +0

[ MLN NLC L2S L2C L2F T AC BC L1S L1C OCL PRINT AF NOIN END TL1L2D 
 TL2B TAL1 ]

MLN +0
 11 NOIN
 8 NOIN+1
 5 SOFAR
 7 SPECOUT
 11 TL2B
 8 TL2B+1
 8 START
ANOTHER 4 AC
 2 AF
 7 END
 11 T
 8 T+1
 0 AC
 0 BC
 7 START
 8 END
START 10 NLC
 10 SOFAR
 4 SOFAR
 7 ;+2
 8 ANOTHER
 4 L1S
 5 L1C
 4 L2S
 5 L2C
 11 TAL1
 8 TAL1+1
 11 TL1L2D
 8 TL1L2D+1
 0 MLN
/8 1

SPECOUT 11 OCL
 8 OCL+1
 8 ;-4


SOFAR +0

[ DLN VERIFY MLN DCL ]

DLN +0    (DELETE LINE NUMBER)
 4 VERIFY
 1 -1
 5 VERIFY
 11 MLN
 8 MLN+1
 11 DCL
 8 DCL+1
 10 VERIFY
 0 DLN
/8 1


[DUN NOIN NLC VERIFY MNL DCL ]

DUN +0
 11 NOIN
 8 NOIN+1
 1 +1
 5 SOFAR
 11 DCL  (INCLUDE THIS LINE)
 8 DCL+1
 4 VERIFY
 5 VSAVE
 4 +0
 5 VERIFY
 8 GO
ANOTHER 10 SOFAR
 11 MNL
 8 MNL+1
 11 DCL
 8 DCL+1
GO 4 SOFAR
 7 ;+2
 8 ANOTHER
DONE 4 VSAVE
 5 VERIFY
 11 MNL
 8 MNL+1
 0 DUN
/8 1

VSAVE +0
SOFAR +0

[ HLP PRINT INSS QCHOP HELPF DOSAVE PNE ]

HLP +0
 4 HELPF
 7 FULL
 11 PRINT
 8 PRINT+1
 +3
 &010000
 \SOR
 \RY,
 \ NO
 \ HE
 \LP.
&010000
 &030000
OUT 0 HLP
/8 1

FULL 4 DOSAVE
 5 DOS
 4 +3
 5 DOSAVE
 11 PNE
 8 PNE+1
 4 DOS
 5 DOSAVE
 8 OUT


LINE +0
COUNT +0
FINSS 0 INSS
HELPF +0
DOS +0

[ IBA IBB TL2B LLL1 L2S L2C L2F MNL OCL LL L1C L1S BC T 
 AF AC BFMAX PRINT ]

IBB +0
 11 LLL1
 8 LLL1+1
 11 OCL
 8 OCL+1
 0 IBB
/8 1

IBA +0
 11 TL2B
 8 TL2B+1
 11 LLL1
 8 LLL1+1
 4 L2S
 5 L2C
 5 L2F
 11 MNL
 8 MNL+1
 0 IBA
 /8 1

LLL1 +0
 4 L1S
 5 LL+2
NEWL 11 LL
 8 LL+1
 4 LL+5
 7 ;+2
 8 EXIT
 4 L1S
 5 L1C
 11 T
 8 T+1
 0 L1C
 0 BC
 4 AC   (AF-AC+BC"GE"BFMAX-20)
 2 AF
 1 BC
 2 BFMAX
 1 -40
 9 WARN
 8 NEWL
WARN 11 PRINT
 8 PRINT+1
 +3
 \STO
 \RE 
 \FUL
 \L  
 &010000
&030000
EXIT 0 LLL1
/8 1

[ COP ]

COP +0
 4 +0
 5 BL
 8 ;+2
NEWBL 10 BL

 4 +0
 15 2048
 15 6144
 10 BL
 7 NEWBL
NBL 4 BL
 2 +1
 5 BLC
BLR 4 +0
 15 2048
 15 6144
 7 ;+2
 8 NBL
 10 BLC
 4 BLC
 0 COP
/7 0
 8 BLR
BLC +0
BL +0


 
[ FLB FLC DLB DLC TL2B RL1 L1S L1C L1F T AC NLC RC BC TL1L2D 
 PRINT AF SNF DUB DUC L2S L2C L2F QSTOP ]

FLB +0    (FIND LINE BEGINNING WITH)
 4 +0
 5 BEG
 4 FLB
 5 FLC
 8 FLC+2

DLB +0   (DELETE LINE BEGINNING WITH)
 4 +0
 5 DTL
 11 FLB
 8 FLB+1
 0 DLB
/8 1

DLC +0   (DELETE LINE CONTAINING)
 4 +0
 5 DTL
 11 FLC
 8 FLC+1
 0 DLC
/8 1


DUB +0   (DELETE UP TO LINE BEGINNING WITH)
 4 +0
 5 DEL
 11 FLB
 8 FLB+1
 0 DUB
/8 1

DUC +0    (DELETE UP TO LINE CONTAINING)
 4 +0
 5 DEL
 11 FLC
 8 FLC+1
 0 DUC
/8 1

FLC +0   (FIND LINE CONTAINING)
 10 BEG
 11 TL2B
 8 TL2B+1
 4 RC+4
 5 X4
 4 RC+5
 5 X5
 8 ;+5
AGAIN 4 X4
 5 RC+4
 4 X5
 5 RC+5
 11 RC
 8 RC+1
 5 DELIM
 1 -1
 7 QSTOP    (NL CANNOT DELIMIT)
 11 RL1
 8 RL1+2
 4 L1S
 5 L1C
 4 AF
 2 AC
 9 ;+2
 8 SNF
 11 T
 8 T+1
 0 AC
 0 L1C
 4 L1C
 5 L1F
 4 L1S
 5 L1C
 10 NLC
 4 RL1+4
 5 L1A
 4 RL1+5
 5 L1B    (SET SEARCH TO BEGINNING OF LINE)
OK 11 RL1
 8 RL1+1
 7 SSF  (TEXT IS SP)
 5 THIS   (THIS IS TEXT)
 4 +0
 5 SPFLAG
SSFR 4 RC+4
 5 SAFIN
 4 RC+5
 5 SASHIF
 11 RC
 8 RC+1
 5 THAT   (THAT IS COMMAND STRING)
 2 +1  (END OF COMMAND STRING)
 7 FOUND    (MATCH SUCCESSFUL)
 4 THAT
 2 DELIM
 7 FOUND  (NL OR DELIMITER ENDS STRING)
 4 THIS
 2 +1   (END OF TEXT LINE)
 7 ;+2
 8 NNL
 4 THAT
 2 &76   (^ MATCHES NEWLINE)
7 ;+2
 8 NBG
 11 RC
 8 RC+1
 5 THAT
 2 +1
 7 FOUND
 4 THAT
 2 DELIM
 7 FOUND
 8 NBG
NNL 4 THAT
 2 &37  (? MATCHES ANYTHING)
 7 OK
 4 THAT
 2 THIS
 7 OK  (CHARACTERS AGREE)
4 SPFLAG
 7 FAIL
 4 SAFIN
 5 RC+4
 4 SASHIF
 5 RC+5
 8 OK
FAIL 4 BEG
 7 NBG
 4 X4
 5 RC+4
 4 X5
 5 RC+5
 11 RC
 8 RC+1  (IGNORE DELIMITER)
 4 L1A  (RECALL L1)
 5 RL1+4
 4 L1B
 5 RL1+5
 11 RL1
 8 RL1+1
 4 RL1+4
 5 L1A
 4 RL1+5
 5 L1B
 8 OK
NBG 4 DEL
 7 AGAIN
 4 L1S
 5 L1C
 11 T
 8 T+1
 0 L1C
 0 BC
 8 AGAIN
FOUND 4 L2S
 5 L2C
 5 L2F
 4 DTL
 7 ;+3   (DELETE THIS LINE)
 11 TL1L2D
 8 TL1L2D+1
EXIT 10 DEL
 10 DTL
 0 FLC
/8 1

SSF 4 THIS
 7 ;+4  (PREV. TEXT WAS ALSO SP.)
 4 +0
 5 THIS
 8 SSFR
 10 SPFLAG
 8 SSFR

BEG +0
THIS +0
THAT +0
SAFIN +0
SASHIF +0
SPFLAG +0
DELIM +0
DEL +1
DTL +1    (SE TO ZERO IF THIS LINE TO BE DELETED)
L1A +0
L1B +0
X4 +0
X5 +0


(S/R LOADLINE - LOADS ONE LINE OF TEXT INTO BUFFER)
(16/7/75) (C.H.M.) (INCORPORATES DOUBLE DOLLAR AND BACKARROW )


[ LL QCHIN PRINT QPAUSE CONTROL QSTOP QDIN QDOUT QCHOP ]
LL +0            (LINK)
LL1 8 GO         (TRIGGER)
LL2 STAD +2000   (BUFFER START ADDRESS)
LL3 FIND +2000   (NEXT FREE WORD)
LL4 SIZE +39     (SIZE OF BUFFER [120 CHARS])
LL5 FLAG +0      (SET TO +1 IF STOPCODE IS FOUND)

GO 4 +0
 5 FLAG
 4 +1
 5 CONTROL
 4 STAD
 5 FIND
 4 -2
 5 NODOL
NW 4 -3
 5 SHIFT
 4 +0
 5 SOFAR
NCH 4 FLAG2
 9 INSC
 11 QCHIN
 8 QCHIN+1
 1 -3
 7 STOP
 1 +3
RET 5 SAVE
 2 &04 (DOLLAR)
 7 DOLLAR
 4 -2
 5 NODOL
 4 SAVE
 1 -63
 7 BACK (BACKARROW)
DOLR 4 SOFAR
 14 6
 6 &777700
1 SAVE
 5 SOFAR
 4 SAVE
 1 -1
 7 NL
 1 -2
 7 NL
 10 SHIFT
 4 SHIFT
 7 ;+2
 8 NCH
 0 FIND
 4 SOFAR
/5 0
 10 FIND
 4 SIZE
 1 STAD
 2 FIND
 9 NW
 8 ERROR1
SHL 4 SOFAR
 14 6
 6 &777700
 5 SOFAR
NL 10 SHIFT
 4 SHIFT
 7 ;+2
 8 SHL
 4 SOFAR
 0 FIND
/5 0
 10 FIND
 0 LL
 4 XFL
/7 1
 4 +0
 5 XFL
 8 LL+1
STOP 4 STAD
 2 FIND
 7 ;+2
 8 ERROR2
SCOK 10 FLAG
 4 +3
 8 RET
ERROR1 11 PRINT
 8 PRINT+1
 +3
&010000
\>12
\0 C
\HAR
\ACT
\ERS
\/LI
\NE 
 &010000
&030000
 4 -3
 5 SHIFT
 4 +0
 5 SOFAR
 4 +1
 8 RET  (INSERT NEWLINE)
ERROR2 11 PRINT
 8 PRINT+1
+3
&010000
\STO
\PCO
\DE 
\IN 
\TEX
\T  
 &010000
&030000
 4 -1  (INSERT NL,SC)
 5 FLAG2
 4 +1
 8 RET  (INSERT NL)

INSC 10 FLAG2
 8 SCOK  (INSERT SC)
FLAG2 +0

SHIFT +0
SOFAR +0
SAVE +0
XFL +0
NODOL -2

DOLLAR 10 NODOL
 4 NODOL
 7 ;+2
 8 DOLR
 4 +3
 5 QDOUT
 4 QDIN
 1 -3
 7 TTY
 10 XFL
 11 PRINT
 8 PRINT+1
 +3
&040400 (DOUBLE DOLLAR FOR ADVICE ONLY)
&010000
&030000
 8 DOLR
TTY 4 +1
 11 QCHOP
 8 QCHOP+1
 8 LL+1

BACK 4 SHIFT
 1 +3 
 7 PREVW
 1 -4
 5 SHIFT
 8 SHR
PREVW 4 -1
 5 SHIFT
 4 FIND
 1 -1
 5 FIND
 0 FIND
/4 0
 8 ;+2
SHR 4 SOFAR
 14 8186
 5 SOFAR
 8 NCH


[ NOIN NIFCS BLC NLC PRINT QSTOP RST ]

NOIN +0
 11 NIFCS
 8 NIFCS+1
 8 ;+2  (-)
 8 CALC
 2 +0   (+)
CR 9 EXNOIN
 7 EXNOIN
 1 BLC
 2 NLC
 2 +0
 9 RSTOP
 11 PRINT
 8 PRINT+1
 +3
 \NO 
 \LIN
 \E  
 &030000
 8 QSTOP
RSTOP 5 MOVES
 11 PRINT
 8 PRINT+1
 +3
 \*RS
 \T  
 &010000
 &030000
 11 RST
 8 RST+1
 4 MOVES
EXNOIN 0 NOIN
 /8 1
CALC 7 DEFA
 2 NLC
 8 CR
DEFA 4 -1
 8 EXNOIN

MOVES +0



[ SSS NIFCS BFMAX ]

SSS +0     (SET STORE SIZE)
 11 NIFCS
 8 NIFCS+1
 2 +0
 8 ;+1
 7 OUT
 5 BFMAX
OUT 0 SSS
 /8 1

[ VON VOF VERIFY ]

VON +0
 10 VERIFY
 0 VON
/8 1
VOF +0
 4 VERIFY
 1 -1
 5 VERIFY
 0 VOF
/8 1




[ DSP NOIN MNL VERIFY TL2B ]

DSP +0       (DISPLAY)
 4 VERIFY
 5 SAVE
 4 +1
 5 VERIFY
 11 NOIN
 8 NOIN+1
 5 COUNT
RTN 11 MNL
 8 MNL+1
 10 COUNT
 4 COUNT
 7 ;+2
 8 RTN
 4 SAVE
 5 VERIFY
 0 DSP
/8 1




COUNT +0
SAVE +0

(ERROR ROUTINES)

[ END BOF PRINT QSTOP ENDFLG LISTST NLC ELC ROT ]

END +0     (PRINT'END OF TEXT@)
 4 NLC
 5 ELC
 4 ENDFLG
 7 ;+2
 8 LISTST  (SUPPRESS MESSAGE IF CAUSED BY *LIST)
 11 PRINT
 8 PRINT+1
 +3
 \END
 \ OF
 \ TE
 \XT 
&030000
 8 QSTOP

BOF +0     (PRINT'B STORE OVERFLOW@)

 11 PRINT
 9 PRINT+1
 +3
 \B S
 \TOR
 \E O
 \VER
 \FLO
 \W  
&030000
 11 ROT
 8 ROT+1
 8 QSTOP


[ INSS
 LOA SSC RSC SID SOD HLP CLP COP PSC VON VOF PRO PLL TBS RPN
 ROT EOT PNE RST ROL OCL MNL MLN DCL ECL ILB ILA FLC FLB DLC DLB DET
 DUC DUB IBA IBB DSP EXS ISA ISB COS GXS GDS GIA GIB CON ITA ITB MAC
 DUN DLN RLN LIST DPC EPC PCH DNE RLW SPS VDU TTY SSS REP CLR DSC SPEC ]

INSS \LOA
 0 LOA
 \SSC
 0 SSC
 \RSC
 0 RSC
 \SID
 0 SID
 \SOD
 0 SOD
 \HEL
 0 HLP
 \CLP
 0 CLP
 \COP
 0 COP
 \CLE
 0 CLR
 \PSC
 0 PSC
 \VON
 0 VON
 \VOF
 0 VOF
 \PRO
 0 PRO
 \TAB
 0 TBS
 \RPN
 0 RPN
 \PCH
 0 PCH
 \DNE
 0 DNE
 \RLW
 0 RLW
 \SPS
 0 SPS
 \VDU
 0 VDU
 \TTY
 0 TTY
 \SSS
 0 SSS
 \REP
 0 REP
 \DSC
 0 DSC
 \SPE
 0 SPEC
 \DPC
 0 DPC
 \EPC
 0 EPC
 -2
  +0   (ONLY ABOVE INSTRUCTIONS WITHOUT PROG. IN STORE)
 \PLL
 0 PLL
 \ROT
 0 ROT
 \EOT
 0 EOT
 \PUN
 0 PNE
 &600000  (P)
 0 PNE
 \RST
 0 RST
 &620000
 0 RST
 \ROL
 0 ROL
 \OCL
 0 OCL
 &570000   (O)
 0 OCL
 &560000    (N)
 0 MLN
 \MLN
 0 MLN
 \DCL
 0 DCL
 &440000    (D)
 0 DCL
 \ECL
 0 ECL
 \ILB
 0 ILB
 \ILA
 0 ILA
 \FLC
 0 FLC
 \FLB
 0 FLB
 \DLC
 0 DLC
 \DLB
 0 DLB
 \DET
 0 DET
 \DUC
 0 DUC
 \DUB
 0 DUB
 \IBA
 0 IBA
 \IBB
 0 IBB
 \DSP
 0 DSP
 \EXS
 0 EXS
 \ISA
 0 ISA
 \ISB
 0 ISB
 \COS
 0 COS
 \GXS
 0 GXS
 \GDS
 0 GDS
 \GIA
 0 GIA
 \GIB
 0 GIB
 \CON
 0 CON
 &430000
 0 CON
 \ITA
 0 ITA
 \ITB
 0 ITB
 \MAC
 0 MAC
 \DUN
 0 DUN
 \DLN
 0 DLN
 \RLN
 0 RLN
 \LIS
 0 LIST
-1
[ PLL RC PRO ]

PLL +0
 11 PRO
 8 PRO+1
AGAIN 11 RC
 8 RC+1
 1 -1
 7 OUT
 1 -26
 7 OUT
 1 +27
 12 +3
 14 17
 1 ADDRES
 5 OBEY
 4 -3
 5 COUNT
OBEY +0
 5 TRANSL
 14 8183
 15 6144
 4 TRANSL
 15 6144
 10 OBEY
 10 COUNT
 4 COUNT
 7 AGAIN
 8 OBEY
OUT 11 PRO
 8 PRO+1
 0 PLL
/8 1

TRANSL +0
COUNT +0
ADDRES 4 STD

STD +0  (START OF TYPEFACE DATA, 3 WORDS/CHAR)
 +0
 +0
 +0 (NEWLINE IS NOT PERMITTED)
 +0
 +0
 &20 (")
 &10010
 &20020
 &24 (\)
 &76024
 &76024
 &46 ($)
 &111177
 &111062
 &143 (PERCENT)
 &23010
 &144143
 &106 (&)
 &151121
 &51126
 +0 (')
 &4002
 &1000
 &34 ()
 &42101
 +0
 +0 ()
 &101
 &42034
 &22 (*)
 &14077
 &14022
 &10 (+)
 &10074
 &10010
 +0 (,)
 &70070
 &170000
 &10 (-)
 &10010
 &10010
 +0 (.)
 &140
 &140000
 &140 (/)
 &20010
 &4003
 &76 (0)
 &161111
 &107076
 &104 (1)
 &102177
 &100100
 &142 (2)
 &121111
 &101106
 &42 (3)
 &101111
 &111066
 &10 (4)
 &14012
 &177010
 &57 (5)
 &111111
 &111061
 &76 (6)
 &111111
 &111062
 &3 (7)
 &1161
 &11007
 &66 (8)
 &111111
 &111066
 &46 (9)
 &111111
 &111076
 +0 (:)
 &66066
 +0
 +0 (;)
 &66166
 +0
 &10 (<)
 &20044
&102000
 &24 (=)
 &24024
 &24024
 +0 (>)
 &102044
 &20010
 &2 (?)
 &1161
 &11006
 +0 (@)
 &1002
 &4000
 &176 (A)
 &11011
 &11176
 &177
 &111111
 &111066
 &76
 &101101
 &101042
&177
 &101101
 &101076
 &177
 &111111
 &111101
 &177
 &11011
 &11001
 &76
 &101101
 &111072
 &177
 &10010
 &10177
 &101
 &101177
 &101101
 &61
 &101101
 &77001
 &177
 &10024
 &42101
 &177 (L)
 &100100
 &100140
 &177
 &4030
 &4177
 &177
 &3004
 &30177
 &76
 &101101
 &101076
 &177
 &11011
 &11006
 &76
 &101121
 &141176
 &177
 &11031
 &51106
 &46
 &111111
 &111062
 &1
 &1177
 &1001
 &77
 &100100
 &100077
 &7
 &30140
 &30007
 &37
 &160070
 &160037
 &143
 &24010
 &24143
 &3
 &4170
 &4003
 &141 (Z)
 &121111
 &105103
 &177 ([)
 &101101
 +0
 +0 (\)
 +0
 +0
 +0 (])
 &101
 &101177
 &4 (^)
 &2177
 &2004
 &160 (? OR BACKARROW NOT POSSIBLE)
 &160
 &120160



[ PCH LL ELC SCE PN DISAVE QDIN L1S L1C L1F ENPAR ]


PCH +0     (PARITY CHECK & LINE/PAGE COUNT)
 10 ENPAR
 4 +0
 5 ELC
 5 PN
 5 SCE
 4 DISAVE
 5 QDIN
 4 L1S
 5 L1C
 5 L1F
 5 LL+2
 4 -60
 5 PPC
AGAIN  11 LL
 8 LL+1
 4 LL+5
 7 ;+2
 8 EXST
 10 ELC
 10 PPC
 4 PPC
 7 ;+2
 8 AGAIN
 10 PN
 4 -60
 5 PPC
 8 AGAIN

EXST 0 PCH
 /8 1


PPC +0


[ GXS GIA GIB GDS PRINT QOUT1 
 EXS ISA ISB COS SNF  QSTOP FLC VERIFY MAC RST RC ]

MAC +0   (MACRO EXCHANGE STRINGS -USE WITH CARE)
 4 +0
 5 TOTAL
 4 MAC
 5 GXS
 4 VERIFY
 1 -1
 5 VERIFY
 4 -1
 5 MACFLG
 8 GXS+1

GXS +0   (GLOBAL EXCHANGE STRINGS)
 11 GLO
 8 GLO+1
 0 EXS
 0 GXS
/8 1

GIA +0   (GLOBAL INSERT STRING AFTER)
 11 GLO
 8 GLO+1
 0 ISA
 0 GIA
/8 1

GIB +0   (GLOBAL INSERT STRING BEFORE)
 11 GLO
 8 GLO+1
 0 ISB
 0 GIB
/8 1

GDS +0
 11 GLO
 8 GLO+1
 0 COS
 0 GDS
/8 1

GLO +0
 4 RC+4
 5 X4
 4 RC+5
 5 X5
 4 VERIFY
 1 -1
 5 VERIFY
 4 -1
 5 GLOFLG  (=-1 FOR GLOBAL INSTR.)
 4 +0
 5 NOSTR
AGAIN 4 X4
 5 RC+4
 4 X5
 5 RC+5
 4 VERIFY
 1 -1
 5 VERIFY
 11 FLC
 8 FLC+1
 10 VERIFY
 4 X4
 5 RC+4
 4 X5
 5 RC+5
 0 GLO
/0 1
/11 0
/8 1
 10 NOSTR
 8 AGAIN
SNF 4 GLOFLG
 7 STNFND  (NOT GLOBAL)
 10 VERIFY
 4 NOSTR
 7 STNFND  (GLOBAL, BUT NO STRING FOUND, OR MAYBE END OF MACRO)
 1 TOTAL
 5 TOTAL
 10 VERIFY
 4 VERIFY
 7 SKIPSON
 11 PRINT
 8 PRINT+1
 +3
 \STR
 \ING
 \ OC
 \CUR
 \ED 
&030000
 4 NOSTR
 11 QOUT1
 8 QOUT1+1
 +0
 11 PRINT
 8 PRINT+1
+3
 \ TI
 \MES
 &010000
&030000
SKIPSON 4 +0
 5 GLOFLG
 5 NOSTR (RESET GLOBAL)
 11 RST
 8 RST+1
 4 MACFLG
 0 GLO
/7 2
 4 X4
 5 RC+4
 4 X5
 5 RC+5
 8 GXS+1  (RETURN FOR ANOTHER MACRO PASS)
STNFND 4 MACFLG
 9 MACOUT  (MACRO FINISH)
 11 PRINT
 8 PRINT+1
 +3
 \STR
 \ING
 \ NO
 \T F
 \OUN
 \D  
&030000
 4 GLOFLG
 9 ;+2
 8 FIN
 11 RST  (GLOBAL FINISH)
 8 RST+1
FIN 4 +0
 5 GLOFLG
 8 QSTOP

MACOUT 4 +0
 5 MACFLG
 4 TOTAL
 5 NOSTR
 4 -1
 5 GLOFLG
 8 SNF


NOSTR +0
GLOFLG +0
MACFLG +0
TOTAL +0
X4 +0
X5 +0



[ STRING RC RL1 WL2 T L1S L1C L1F L2S L2C L2F M1A M1B M3A M3B 
 PRINT QSTOP SNF DELIM ]

STRING +0
 11 RL1
 8 RL1+2
 11 WL2
 8 WL2+2  (INITIALISATION PROCEDURES)
 4 RC+4
 5 X4
 4 RC+5
 5 X5
 4 +2
 5 THIS
 4 +1
 5 LASTRL
 5 LASTRC
 4 RL1+4
 5 M1A
 4 -3
 5 M1B
 4 L2S
 5 L2C
 2 L2F
 7 SNF
 4 L1S
 5 L1C
 11 T
 8 T+1
 0 L2C
 0 L1C   (TRANSFER L2 BACK INTO L1)
 4 L1S
 5 L1C
RST 4 X4
 5 RC+4
 4 X5
 5 RC+5
 11 RC
 8 RC+1  (SET DELIMITER)
 5 DELIM
 1 -1
 7 NS
OK 4 THAT
 5 LASTRC
OK1 4 RC+4
 5 M2A
 4 RC+5
 5 M2B
 11 RC
 8 RC+1
 5 THAT  (THAT IS COMMAND TEXT)
 1 -1
 7 NS   (NO STRING IF NL IN COMMAND)
 4 THAT
 2 DELIM
 7 FOUND
 4 THIS
 5 LASTRL
 11 RL1
 8 RL1+1
 5 THIS  (THIS IS TEXT STRING)
1 -1
 7 ;+2    (NL REACHED, & STRING NOT FOUND)
 8 NNL
 4 THAT
 2 &76   (^ MATCHES NEWLINE)
 7 ;+2
 8 SNF
 4 RC+4
 5 M2A
 4 RC+5
 5 M2B
 11 RC
 8 RC+1
 5 THAT
 2 +1
 7 BS
 4 THAT
 2 DELIM
 7 BS
 8 SNF
BS 4 -3
 5 RL1+5
 4 RL1+4
 1 -1
 5 RL1+4
 0 RL1+4
/4 0   (READ PREVIOUS WORD)
 6 &7777
 7 FOUND   (010000, SH=-3)
 10 RL1+5
 6 &77
 7 FOUND   (XX0100, SH=-2)
 10 RL1+5
 8 FOUND  (XXXX01, SH=-1)
NNL 4 THAT
 2 &37   (? MATCHES ANYTHING)
 7 OK
 4 THIS
 2 THAT   (DO CHARS. MATCH?)
 7 OK
 4 THIS    (CHECK SP BEFORE OR IN STRING)
 7 ;+2
 8 FAIL
 4 LASTRC
 7 ;+2
 8 FAIL
 4 M2A
 5 RC+4
 4 M2B
 5 RC+5
 8 OK1
FAIL 4 M1A
 5 RL1+4
 4 M1B
 5 RL1+5
 11 RL1
 8 RL1+1
 4 RL1+4
 5 M1A
 4 RL1+5
 5 M1B
 4 +1
 5 LASTRL
 5 LASTRC
 8 RST
FOUND 4 LASTRC     (CHECK SP AFTER STRING)
 1 THIS
 7 ;+2        (BOTH SPACES)
 8 K
KR 4 RL1+4
 5 M3A
 4 RL1+5
 5 M3B
 11 RL1
 8 RL1+1
 7 KR        (LOOP TO FIND ALL SPACES)
 8 KL
K 4 RL1+4
 5 M3A
 4 RL1+5
 5 M3B
KL 4 M2A
 5 RC+4
 4 M2B
 5 RC+5   ((RESTORE RC TO LAST CHAR B4 FINAL DELIM.)
 11 RC
 8 RC+1
 7 ;+2
 8 RESET
 11 RL1
 8 RL1+1
 7 FOUND
RESET 11 RL1
 8 RL1+2  (RESET RL1)
 0 STRING
/8 1


NS 11 PRINT
 8 PRINT+1
 +3
\NO 
\STR
\ING
&030000
 8 QSTOP

THAT +0
THIS +0
DELIM +0
LASTRC +1
LASTRL +1
M1A +0
M1B +0
M2A +0
M2B +0
M3A +0
M3B +0
X4 +0
X5 +0


[ TL13 RL1 M1A M1B M3A M3B WL2 ]

TL13 +0    (TRANSFER ACROSS FROM L1 TO L2 UNTIL M3)
 4 M3A
 2 RL1+4
 7 ;+2
 8 ;+4
 4 M3B
 2 RL1+5
 7 ENOUGH
 11 RL1
 8 RL1+1
 11 WL2
 8 WL2+1
 8 TL13+1
ENOUGH 0 TL13
/8 1


[ EXS ISA ISB COS M1A M1B M3A M3B RL1 WL2 OCL L2F RC 
 DELIM TL13 STRING ]

EXS +0    (EXCHANGE STRINGS IN CURRENT LINE)
 4 +0
 5 FLAG
4 +1
 5 COF
 8 BEGIN
COS +0    (CUT OUT STRING IN CURRENT LINE)
 4 +0
 5 FLAG
 5 COF
 4 COS
 5 EXS
8 BEGIN
ISA +0   (INSERT 2ND STRING AFTER 1ST STRING)
 4 ISA
 5 EXS
 4 -1
 4 +1
 5 FLAG
 5 COF
 8 BEGIN
ISB +0   (INSERT 2ND STRING BEFORE 1ST STRING)
 4 ISB
 5 EXS
  4 -1
 5 FLAG
BEGIN 11 STRING
 8 STRING+1
AGAIN 4 M1A   (TRANSFER L1 TO L2 UNTIL BEGINNING OF COMM. STR.)
 2 RL1+4
 7 ;+2
 8 ;+4
 4 M1B
 2 RL1+5
 7 ENOUGH
 11 RL1
 8 RL1+1
 11 WL2
 8 WL2+1
 8 AGAIN
ENOUGH 4 FLAG
 1 -1
 11 TL13
 7 TL13+1
 4 COF
 7 ECS  (IGNORE COMMAND STRING IF COS)
 11 RC
 8 RC+1
 2 DELIM
 7 ECS
 2 DELIM
 2 +1
 7 ECS
 2 +1
 11 WL2
 8 WL2+1
 8 ENOUGH
ECS 4 FLAG
 11 TL13
 9 TL13+1
 4 M3A
 5 RL1+4
 4 M3B
 5 RL1+5
FSS 11 RL1    (FINISH SOURCE STRING)
 8 RL1+1
 11 WL2
 8 WL2+1
 2 +1
 7 DONE
 8 FSS
DONE 4 WL2+4
 5 L2F
 11 OCL
 8 OCL+1
 10 COF   (RESET CUT OUT FLAG)
 0 EXS
/8 1

COF +1
FLAG +0


[ NLC BLC ELC ENDFLG ]

NLC +0     (NEWLINECOUNT)
ELC +0     (ENDING LINECOUNT)
ENDFLG +0  (FLAG CONTROLS O/P OF END-OF-TEXT OR TERMINATES LIST)
BLC +0     (BASE LINE COUNT)


[ LIST RST ENDFLG LISTST VERIFY PRINT NLC ELC MNL OCL SCE CON NP PN
 SIZE PFLG ]

LIST +0
 11 RST
 8 RST+1
 4 +1
 5 VERIFY
 4 +0
 5 PRINT+8
 10 ENDFLG
 11 OCL
 8 OCL+1
L294 11 MNL
 8 MNL+1
 8 ;-2

COUNT +0

LISTST 4 SCE
 7 CARRYON  (NOT AT END OF TAPE)
 4 +0
 5 ENDFLG
 4 PFLG
 7 LX
 4 PN
 1 -1
 7 NP1
 4 SIZE
 2 PRINT+8
 5 COUNT
NLO 4 +10
 15 6148
 10 COUNT
 4 COUNT
 9 NLO
NP1 11 NP
 8 NP+2  (DONT O/P PAGE NO.)
 4 NLC
 5 ELC
 11 RST
 8 RST+1
 4 +0
 5 PN
LX 0 LIST
/8 1

CARRYON 4 NLC
 5 ELC
 11 CON
 8 CON+1
 11 RST
 8 RST+1
 8 L294



[ TBS NIFCS TABNO ]

TBS +0   (TABSET)
 11 NIFCS
 8 NIFCS+1
 2 +0
 8 ;+1
 2 +72
 9 OUT
 2 +72
 5 TABNO
OUT 0 TBS
 /8 1

[ RLN NIFCS RST BLC NLC PRINT QSTOP ]

RLN +0   (RESET LINE NUMBER)
 11 NIFCS
 8 NIFCS+1
 8 NEGPOS
 8 ;+2
8 NEGPOS
 5 NO
 1 BLC
 2 NLC
 2 +0
 9 NOL
 5 BLC
 4 NO
 5 NLC
EX 0 RLN
/8 1

NEGPOS 5 NO
 1 BLC
 9 NOL
 5 BLC
 4 NO
 1 NLC
 5 NLC
 8 EX

NOL 11 PRINT
 8 PRINT+1
 +3
 \NO 
 \LIN
 \E  
 &030000
 8 QSTOP

NO +0



[ DNE PLL T22 PROFLG HELPF BFMAX ]

DNE +0
 4 PROFLG
 7 ;+2
 8 OUT
 4 HELPF
 7 ;+2
 8 OUT
 11 PLL
 8 PLL+1
 4 BFMAX
 6 =/15 0
 7 ONE
 11 T22
 8 T22+2
 8 OUT
ONE
 11 T22
 8 T22+1
OUT 0 DNE
 /8 1


(CHM T22 VERSION A7)
(THIS TAPE IS DESIGNED FOR INCLUSION IN PROGRAMS THAT REQUIRE)
(A SELF-DUMP FACILITY. ON ENTRY AT T22+1 MODULE 0 IS PUNCHED,)
(PRECEDED BY THE CONTENTS OF LOCATIONS 8135-8179 IN I.I. FORMAT)
(THIS WILL USUALLY BE T.23. ENTRY AT T22+2 DUMPS 8-8135 & 8192-)
(16383. IT CONTAINS CORE-CLEARS WHICH ARE PUNCHED BEFORE T23,AS)
(DATA WITHIN SUBROUTINE. CHECK THAT 8171 CONTAINS CORRECT TRIGGER)
(ADDRESS.)

[ T22 ]

T22 +0
 8 ONE
 4 =/15 8147   (NORMALLY 4 +16383)
 5 LIMIT2
 4 CC2-1
 11 DUMP
 8 DUMP+1
 0 CC2
A 4 CC1-1
 11 DUMP
 8 DUMP+1
 0 CC1
 4 +0
 5 8175
 5 8176
 4 =/15 8147
 11 DUMP
 8 DUMP+1
 0 8135
 4 +0
 5 Q
 5 CS1
 5 CS2
 4 +8
B 5 ADDRESS
 4 +1
 5 PNBAF
C 0 ADDRESS
 /4 0
 7 D
E 4 PNBAF
 7 J
K 4 +128
 5 BIT8
 4 ADDRESS
 11 PUNCH
 8 PUNCH+1
 4 ADDRESS
 1 CS1
 5 CS1
 4 ADDRESS
 7 L
 4 +0
 5 PNBAF
J 0 ADDRESS
 /4 0
 11 PUNCH
 8 PUNCH+1
 0 ADDRESS
 /4 0
 1 CS2
 5 CS2
S 10 ADDRESS
 4 ADDRESS
 2 LIMIT1
 7 M
 4 ADDRESS
 2 LIMIT2
 7 N
 4 Q
 7 C
 4 +0
 5 Q
 8 T
D 4 +1
 5 PNBAF
G 10 Q
 8 S
T 0 ADDRESS
 /4 0
 7 F
 4 PNBAF
 1 -1
 5 PNBAF
 7 I
 8 E
I 4 ADDRESS
 1 -1
 5 ADDRESS
 8 E
F 10 PNBAF
 8 G
M 4 LIMIT2
 9 N
 4 +8192
 8 B
N 4 +0
 5 ADDRESS
 8 K
L 4 CS2
 11 PUNCH
 8 PUNCH+1
 4 CS1
 11 PUNCH
 8 PUNCH+1
 0 T22
 /8 1
ONE 4 =/15 8147
 5 LIMIT2
 8 A

CS1 +0
CS2 +0
LIMIT1 +8135
LIMIT2 +16383
ADDRESS +8
PNBAF +1
Q +0

DUMP +0
 5 THIRD
 4 +0
 15 6144
 15 6144
 4 +12
 15 6144
 4 +8179
 11 PUNCH
 8 PUNCH+1
 4 +12
 15 6144
 4 =8 8182
 11 PUNCH
 8 PUNCH+1
 4 +12
 15 6144
 4 THIRD
 11 PUNCH
 8 PUNCH+1
 0 DUMP
 /4 1
 5 ADD
R 4 +12
 15 6144
 0 ADD
 /4 0
 11 PUNCH
 8 PUNCH+1
 10 ADD
 10 THIRD
 4 THIRD
 0 DUMP
 /7 2
 8 R

THIRD +0
ADD +0

PUNCH +0
 14 8174
 4 +0
 14 4
 1 BIT8
 15 6144
 4 +0
 5 BIT8
 14 7
 15 6144
 4 +0
 14 7
 15 6144
 0 PUNCH
 /8 1

BIT8 +0

 /15 8187
CC1 8 8177
 8 8181
 10 1
 /5 1
 8 8175

 /15 8180
CC2 0 8178
 10 8171
 4 8179
 /5 0
 4 8171
 6 8178
 2 8178
 7 8181
 8 8169
 8 8168
+8191
+0



<! Halt !><! Halt !><! Halt !><! Halt !><! Halt !><! Halt !><! Halt !><! Halt !>