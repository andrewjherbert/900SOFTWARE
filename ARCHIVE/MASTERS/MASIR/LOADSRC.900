
 (900 LOADER ISSUE 3)
*PROG JENLD
*NOMEM
<! Halt !>

[STARTEX QSTARR MAXSTORE MAINEN LODSET BCOMM ]
 ^8
 8 LODSET
 >6
  +0
 0 15
 /8 STARTEX
 0 MAINEN
 >+109
MAXSTORE +16384
  +2
QSTARR +8130 (MASTER FREE STORE POINTERS, SET BY LODSET)
  +8191
  -1
  -1
  -1
  -1
  -1
  -1
  -1
  -1
  -1
  -1
  -1
  -1
  -1
  -1
BCOMM +128
[QRR LDICT BCOMM MAINEN PRICH LOAD STARTEX QDOUT QCHOP
QPAUSE
POINTER QSTARR MAXSTORE QINRLB QRWORD QSTOP]
FREELO +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
FREETO +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
POINTER 0 LDICT
DICTAD 0 LDICT
QFPCHOP +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
  +0
LOAD 5 PARAM (LDMK =0 IF LIBRARY SCAN)
 11 LMOD
 14 18
 5 LMOD (LOADER MODULE NUMBER IN BITS 14-17)
 2 +0
 5 LMOD2 (ADDRESS OF MODULE 0 RELATIVE TO LOADER)
 4 MAINAD
 1 LMOD
 0 LMOD2
 /5 18
 11 TESWMK
 8 TESWMK+1
 4 PARAM
 6 +1
 7 INITIAL
 8 OFFWEGO
INITIAL 10 PARAM
 5 STARTAD
 5 COUNT
 5 BUFWD
 5 LASTAD
 4 PARAM
 6 &40
 7 NOSAVE
 4 POINTER
 5 PSAVE
CHK 0 COUNT
 /4 DSTARR
 9 NXT
 /4 FREETOP
 6 &17777
 /5 DSTARR
NXT 10 COUNT
 4 MAXSTORE+1
 2 COUNT
 9 CHK
 5 COUNT
NOSAVE 4 PARAM
 6 &20
 7 RESTR
 4 -16 (RESET DSTARR TO ORIGINAL VALUES IF FROZEN LAYOUT)
 5 COUNT ( NO LONGER REQD)
INITST 0 COUNT
 /4 QSTARR+16
 /5 DSTARR+16
 10 COUNT
 4 COUNT
 9 INITST
 5 BCOMMK
 8 SVE
RESTR 4 PSAVE
 8 ;+3
SVE
 4 DICTAD
 5 PSAVE
 5 POINTER
 4 PARAM
 6 &4
 7 SET
 4 PARAM
 6 &10
 7 SET
 4 +0
 5 QRWRD+1
 4 -48
 5 COUNT
LOADD 0 COUNT
 /4 T23+48
 5 QRWRD
 11 QOUTSCB
 8 QOUTSCB+2
 10 COUNT
 4 COUNT
 9 LOADD
 5 QRWRD+2
SET 0 COUNT
 4 +0
 /5 QFPCHOP
 4 COUNT
 12 +8192
 14 17
 /5 FREELOW
 /1 DSTARR
 /5 FREETOP
 /4 DSTARR
 /2 QSTARR
 7 ;+2
 8 ;+3
 4 -1
 /5 QMCAD
 10 COUNT
 4 +16
 2 COUNT
 9 SET
 4 BCOMM
 5 FREELO
 4 PARAM
 6 &4
 7 OFFWEGO ( NO SCB SO NO BUFFER)
 4 MAXSTORE+1
 1 -1
 7 ;+3
 4 +16383
 8 ;+2
 4 +8179 ( LOCATES SCB BUFFER AT TOP OF MODULE 1 IN STORE SIZES >8K
ADJUSTS BUFFER ADDRESS FOR CASES WHERE LOADER IS LOCATED
IN MODULES OTHER THAN 0)
 1 LMOD2
 5 SCBUF
 1 -260
 5 BUFAD
 1 -2
 5 BUFPTR
 0 BUFPTR
 4 -1
 /5 0
OFFWEGO
 4 PARAM
 6 +2
 2 +2
 5 LDMK
 5 LIBMK
 4 POINTER
 5 SPOINT
 4 -16
 5 COUNT
CLRSAV 0 COUNT (CLEAR REF.STORE ALLOC.ENTRIES)
 /4 FREETOP+16
 /5 SAVEST+16
 10 COUNT
 4 COUNT
 9 CLRSAV
 5 ERRPAR
 5 IMF
 5 VALUE
 11 QINRLB
 8 QINRLB+1
 4 QRWORD+1
 2 +7
 7 SEVEN
 8 ERR0
SEVEN 4 QRWORD
 2 &740000
 7 LHALT (CODE 7 30,0)
 1 +1
 7 STARRT (CODE 7 30,1)
 8 ERR0
STARRT
 11 QINRLB
 8 QINRLB+1
 5 SUMCH
 4 +0
 5 QRWORD+2
 5 CNT1
 11 QINRLB
 8 QINRLB+1
 5 TEMP
 5 NAM
 11 QINRLB
 8 QINRLB+1
 5 TEMP+1 (NAME OF PROG UNIT)
 5 NAM+1
 4 NAM
 7 SECT3 (SKIP IF NO UNIT NAME)
 11 WORDSEARCH
 8 WORDSEARCH+1
 9 SETDICT
 11 SETITEM
 8 SETITEM+1
 10 VALUE
 8 SECT3
SETDICT 0 PTR1
 /4 2
 9 ;+2
 10 LDMK
SECT3 11 QINRLB
 8 QINRLB+1
 4 QRWORD+1 (PATCHES)
 1 -4
 7 COD4
 1 -1
 7 COD5
 1 -1
 7 COD6
 1 -1
 7 COD7
 4 QRWORD
 5 PDSIZE
 11 STALLOC
 8 STALLOC+1
 5 CBA
 1 PDSIZE
NOALOC
 5 NLWA
 10 CBA
 4 CBA
 5 ACTADR
 5 FIRST
 4 NLWA (ADDRESS OF TOP END OF PROGRAM UNIT)
 5 LAST
 6 &360000
 5 MODADR (PROG UNIT MODULE ADDRESS)
 14 8179
 5 MODNR (MODULE NR.)
 0 MODNR
 /4 QMCAD
 6 +8191
 5 MODQMC (ADDRESS OF QMC IN THIS MODULE)
 4 NAM
 7 ;+6 (SKIP IF NO NAME)
 5 TEMP
 4 NAM+1
 5 TEMP+1
 11 UPDATE
 8 UPDATE+1
 11 QINRLB
 8 QINRLB+1
 9 MAYCHEK
 0 PTR1
 /4 2
 6 &200000
 7 ;+2
 8 DONTCHEK+3
 /4 2
 1 &200000
 /5 2
 8 DONTCHEK
MAYCHEK 4 VALUE
 7 DOCHEK
 8 DONTCHEK
DOCHEK 0 PTR1
 /4 2
 6 &200000
 11 CHEX
 7 CHEX+1
 8 DONTCHEK+3
COD4 4 QRWORD (LOAD INTO SAME MODULE AS GLOBAL LABEL)
 5 TEMP
 4 PTR1
 5 X
 11 QINRLB
 8 QINRLB+1
 5 TEMP+1
 11 WORDSEARCH
 8 WORDSEARCH+1
 9 ;+2
 8 ER10CC
 0 PTR1
 /4 2
 9 ;+2
 8 ERR10C
 4 X
 5 PTR1
 /4 4
 14 8179
 8 ;+2
COD5 4 QRWORD (LOAD INTO SPECIFIED MODULE)
 6 +15
 5 COUNT
 11 QINRLB
 8 QINRLB+1
 5 PDSIZE
 0 COUNT
 7 COD51
 2 +8174
 9 ERR7C
 4 +0
 5 SIZE
 11 STALLOC
 8 EROOM (INPUT QMC)
 0 COUNT
 /2 FREELO
 1 PDSIZE
 1 -1
 9 ;+2
 8 ERR3C
 4 PDSIZE
COD51
 /2 FREETO
 /5 FREETO
 5 CBA
 10 CBA
 8 NULL
COD6 4 QRWORD (PATCH TO GLOBAL LABEL)
 5 TEMP
 4 PTR1
 5 X
 11 QINRLB
 8 QINRLB+1
 5 TEMP+1
 11 QINRLB
 8 QINRLB+1
 11 WORDSEARCH
 8 WORDSEARCH+1
 9 COD61
ER10CC
 11 SETITEM
 8 SETITEM+1
 8 ERR10C
COD61
 0 PTR1
 /4 2
 9 ;+2
 8 ERR10C
 4 X
 5 PTR1
 /4 4
 1 QRWORD
 5 CBA
 8 NXWORD
ERR10C 11 QINRLB
 8 QINRLB+1
 4 ;+2
 8 ERR3C+1
 8 ERR10
COD7
 4 QRWORD
 5 CBA
NXWORD
 14 8179
 6 +15
 5 COUNT
 4 +0
 5 SIZE
 11 QINRLB
 8 QINRLB+1
 5 PDSIZE
 7 NULL
 0 COUNT
 11 STALLOC
 8 EROOM (INPUT QMC)
 4 CBA
 6 +8191
 1 PDSIZE
 6 =/15 0
 7 ;+2
 8 ERR7C
NULL
 4 PDSIZE
 1 CBA
 1 -1
 5 NLWA
 8 NOALOC+2 (AFTER PATCH THE LOADER CANNOT CONTINUE TO LLOCATE SPACE FOR PROG. UNIT)
SKIPBL
 11 SKIP
 8 SKIP+1
 8 SKIPUP
DONTCHEK 4 QRWORD
 0 PTR1
 /5 3
 11 QINRLB
 8 QINRLB+1 (SIZE OF LOCAL DATA)
 2 NLWA
 5 NWA
 5 NCWA
 1 +1
 5 LDBA
 5 BA2
 11 QINRLB
 8 QINRLB+1
 11 QINRLB
 8 QINRLB+1 (NR. OF FOREWARD REFERENCES)
 5 FRTSIZ
 11 QINRLB
 8 QINRLB+1 (TYPE OF PROG UNIT)
 6 &000007
 7 SECT8
 8 TESMK
SKIP +0
 4 SPOINT
 5 POINTER
 11 CLEARH
 8 CLEARH+1
 4 -16 (RESET FREESTORE LIMITS TO STATE THAT EXISTED
BEFORE PROGRAM WAS SCANNED)
 5 COUNT
RESTOL 0 COUNT
 /4 SAVEST+16
 /5 FREETOP+16
 /4 QMCAD+16
 9 ;+2
 8 SKIP2
 4 -1
 /5 QMCAD+16
 4 +0
 /5 QFPCHOP+16
SKIP2
 10 COUNT
 4 COUNT
 9 RESTOL
 0 SKIP
 /8 1
SKIPUP
 11 QINRLB (IGNORE UNIT)
 8 QINRLB+1
 4 QRWORD+1
 2 +7
 7 TESCODE
 8 SKIPUP
TESCODE 4 QRWORD
 1 +1
 7 OFFWEGO
 8 SKIPUP
SECT8 4 +0
 5 ACTADR
 11 QINRLB
 8 QINRLB+1
 5 TEMP
 4 QRWORD+1
 2 +6
 7 TESMK
 11 QINRLB
 8 QINRLB+1
 5 TEMP+1 (TEMP CONTAINS GLOBAL LABEL)
 11 WORDSEARCH
 8 WORDSEARCH+1
 9 WHAT
 4 LIBMK (IF LIB. SCANNING A NULL UNIT, ONLY SATISFY)
 6 PDSIZE (UNLOCATED REFERENCES ALREADY PRESENT)
 2 LIBMK
 1 PDSIZE
 7 ;+2
 8 SECT81
 11 QINRLB (SKIP TO NEXT LABEL)
 8 QINRLB+1
 11 QINRLB
 8 QINRLB+1
 8 SECT8
SECT81
 11 SETITEM
 8 SETITEM+1
PRESENT 0 PTR1
 4 &200000
 /1 2
 /5 2
GOON 11 QINRLB
 8 QINRLB+1
 9 ABSOLUTE
 6 &200000
 7 DATASEC
 4 CBA
 8 ONE
WHAT 0 PTR1
 /4 2
 9 GOON
 4 LDMK
 7 ;+2
 8 GOON
 4 PTR1
 5 PTRHLD
 4 SPOINT
LIBLIST
 5 PTR1
 2 POINTER
 7 LISEND
 11 LISTLA
 8 LISTLA+1
 4 PTR1
 1 ESIZE
 8 LIBLIST
LISEND
 4 PTRHLD
 5 PTR1
 10 LDMK
 8 GOON
DATASEC 4 BA2
ONE 5 ACTADR
ABSOLUTE 11 QINRLB
 8 QINRLB+1
 1 ACTADR
 5 ACTADR
 11 UPDATE
 8 UPDATE+1
 8 SECT8
TESMK 4 LDMK
 7 SKIPBL
 11 CLEARH
 8 CLEARH+1
OFFAGAIN 4 -16
 5 COUNT
CLRSV2 0 COUNT
 /4 FREETOP+16
 /5 SAVEST+32
 10 COUNT
 4 COUNT
 9 CLRSV2
 11 QINRLB
 8 QINRLB+1
 4 QRWORD+1
 2 +6
 7 ENDS9 (END OF SECTION 9)
 4 QRWORD
 7 BLANK (BLANK COMMON)
 5 TEMP
 11 QINRLB
 8 QINRLB+1 (NAME OF UNIT)
 5 TEMP+1
 11 QINRLB
 8 QINRLB+1
 5 HOLD
 11 QINRLB
 8 QINRLB+1
 4 +0
 5 FSTMK
CARRYON 11 WORDSEARCH
 8 WORDSEARCH+1
RETURN 9 ISIN
 11 SETITEM
 8 SETITEM+1
 4 HOLD
 0 PTR1
 /1 2
 1 FSTMK
 /5 2
 4 QRWORD
 /5 3
 /4 2
 6 &200000
 7 TESST
 4 FSTMK
 9 ;+2
 8 OFFAGAIN
 4 ADSAVE (IF LOCATED SET ADDR)
 /5 4
 8 OFFAGAIN
TESST /4 3
 9 OFFAGAIN
 1 &400000
 11 STALLOC
 8 STALLOC+1 (ALLOCATE SPACE FOR COMMON BLOCK)
 0 PTR1
 1 +1
 /5 4
 4 &400000
 /1 2
 /5 2 (LOCATED)
 11 LISTLA
 8 LISTLA+1
 8 OFFAGAIN
ISIN 0 PTR1
 /4 4
 5 ADSAVE
 /4 2
 6 &400000
 1 &100000
 5 FSTMK
 /4 2
 6 &200000
 7 COULDBE
 4 HOLD
 6 &200000
 7 ;+2
 8 ;+4
 /4 2
 9 SAMEOFF
 8 R2
 /4 3
 7 ;+4
 2 QRWORD
 7 SAMEOFF
 8 R2
 4 QRWORD
 /5 3 (IF OFFSET WAS =0 USE THIS ENTRY)
SAMEOFF 4 HOLD
 6 &001777
 /1 2
 /5 2
 8 OFFAGAIN
COULDBE
 4 HOLD
 6 &200000
 7 NOTOFF
R2 11 WORDSEARCH
 8 WRNG
 8 RETURN
NOTOFF
 4 HOLD
 6 &1777
 /1 2
 /5 2
 /4 3
 9 ISNT
MAYBE 4 QRWORD
 9 ERR6
ATLAST /2 3
 7 OFFAGAIN
 9 ERRX (SIZE OF NAMED COMMON WRONG - TOO BIG)
 11 OPNAM (TOO SMALL)
 8 CBWARN
 11 PAUSE
 8 PAUSE+1
 8 OFFAGAIN
ISNT 4 HOLD
 6 &100000
 7 XCHEK
 4 QRWORD
 /5 3
 8 OFFAGAIN
XCHEK
 11 CHEX
 8 CHEX+1
 8 OFFAGAIN
CHEX +0
 0 PTR1
 /4 2
 6 &200000
 7 ;+2
 8 CHEXIT
 /4 3
 6 &200000
 7 CHOK
CK13 /4 3
 6 &17777
 5 HOLD
 4 QRWORD
 6 &17777
CKKK 2 HOLD
 7 CHEXIT
 4 &011243
 11 OPNAM
 8 CLW
 11 PAUSE
 8 PAUSE+1
CHEXIT 0 CHEX
 /8 1
CHOK 4 QRWORD
 6 &200000
 7 CK16
 8 CK13
CK16 /4 3
 6 &177777
 5 HOLD
 4 QRWORD
 6 &177777
 8 CKKK
BLANK 4 -1
 5 TEMP
 11 QINRLB
 8 QINRLB+1
 4 +0
 5 TEMP+1
 11 WORDSEARCH
 8 WORDSEARCH+1
 9 PREZNT
 11 SETITEM
 8 SETITEM+1
PREZNT 11 QINRLB
 8 QINRLB+1
 0 PTR1
 1 &400000
 /5 2
 11 QINRLB
 8 QINRLB+1
 9 ERR6
 0 PTR1
 /2 3
 9 LAB1
 8 OFFAGAIN
LAB1
 4 BCOMM
 14 8179
 5 COUNT
 4 QRWORD
 1 BCOMM
 5 TEMP
LAB0 0 COUNT
 /4 FREELOW
 2 TEMP
 9 LAB3
 /4 FREETOP
 2 TEMP
 9 LAB2
 /4 FREETOP
 1 +1
 5 TEMP+1
 4 COUNT
 1 +1
 5 TEMPRY
 0 TEMPRY
 4 TEMP+1
 /2 FREELOW
 7 LAB4
 8 ERR5
LAB4 0 COUNT
 /4 FREETOP
 /5 FREELOW
 10 COUNT
 4 COUNT
 2 MAXSTORE+1
 9 ERR5
 8 LAB0
LAB2 4 TEMP
 /5 FREELOW
LAB3 4 BCOMM
 0 PTR1
 /5 4
 2 TEMP
 /5 3
 4 BCOMMK
 7 ;+2
 8 OFFAGAIN
 10 BCOMMK
 11 LISTLA
 8 LISTLA+1
 8 OFFAGAIN
ENDS9
 4 POINTER
 1 LMOD
 5 TEMP
 14 8179
 5 MP
 0 MP
 /4 FREELOW
 5 X
 2 TEMP
 9 BIGLOW
 4 TEMP
 /5 FREELOW
BIGLOW 4 +0
 5 COUNT
 0 MP
 /4 DSTARR
 9 THISMOD
 4 PARAM
 6 +4
 7 NEXTMOD
 4 TEMP
 6 +8191
 /2 DSTARR
 2 FRTSIZ
 9 RMENUF
NEXTMOD 0 COUNT
 /4 DSTARR
 9 NA
 /4 FREELOW
 /2 FREETOP
 2 FRTSIZ
 9 RMENUF
 7 RMENUF
NA 10 COUNT
 4 COUNT
 2 MAXSTORE+1
 7 ERR5
 8 NEXTMOD
THISMOD 4 POINTER
 6 +8191
 1 FRTSIZ
 6 =/15 0
 7 ;+2
 8 NEXTMOD
 4 POINTER
 8 RMENUF+2
RMENUF 4 LMOD
 /2 FREELO
 5 FRTAD
 4 PARAM
 6 &4
 7 INPQMC
 0 MP
 4 X
 /5 FREELO
INPQMC 0 MODNR (QMC INPUT HERE TO AVOID GETTING DUPLICATE)
 /4 QMCAD (COPIES ON P/TAPE)
 9 ;+2
 8 NEXTRLB
 1 +1
 7 NEXTRLB
 4 +17
 5 CNT1
 4 QMCWORD
INPUT 5 HOLD
 6 =/0 0
 7 DONTADD
 4 QMCAD1
DONTADD 0 CNT1
 /1 QMC
 5 TEMP
 4 CNT1
 1 -17
 1 QMCAD2
 5 ADRES
 4 TEMP
 11 STORE
 8 STORE+1
 4 CNT1
 1 -1
 5 CNT1
 9 NEXTRLB
 4 HOLD
 14 1
 8 INPUT
NEXTRLB 11 QINRLB
 8 QINRLB+1
 0 QRWORD+1
 /8 CODES
CODES 8 NEXTRLB
 8 SETUP
 8 CODE2
 8 CODE3
 8 CODE4
 8 CODE5
 8 CODE6
 8 CODE7
CODE2
 4 CBA
 8 ;+2
CODE3 4 BA2
 6 &017777
 1 QRWORD
 8 SETUP
CODE4 6 &017777
 14 5
 14 8187
 1 NWA
 8 C6
CODE5
 4 MODADR
 2 COMBA
 6 &377777
 1 QRWORD
 8 SETUP
CODE6 6 &001777
 1 FRTAD
 5 I
 4 QRWORD
 14 8182
 6 &7
 0 I
 /1 0
C6
 6 &017777
 5 TEMP
 4 QRWORD
 6 &760000
 1 TEMP
SETUP 11 STOR2
 8 STOR2+1
 8 NEXTRLB
STOR2 +0
 5 TEMP
 4 NWA
 5 ADRES
 4 TEMP
 11 STORE
 8 STORE+1
SUBSETUP 4 +1
 2 NWA
 5 NWA
 0 STOR2
 /8 1
CODE7 6 &017777
 5 TEMPRY
 4 QRWORD
 14 8179
 6 +31
 5 TEMP
 1 -15
 9 VALTYP
 4 QRWORD
 1 +1
 7 TYPE31
 8 ERR11 (ILLEGAL FORMAT OF R.L.B.)
VALTYP 4 TEMPRY
 0 TEMP
 11 STOR2
 /8 TYPE
 8 NEXTRLB
TYPE 8 ERR11
 8 TYPE1
 8 TYPE2
 8 TYPE3
 8 TYPE4
 8 TYPE5
 8 SUBSETUP+1
 8 TYPE7
 8 TYPE8
 8 TYPE9
 8 TYPE10
 8 TYPE11
 8 TYPE12
 8 TYPE13
 8 TYPE14
TYPE1
 11 DICTSEARCH
 8 DICTSEARCH+1
 0 PTR1
 /4 2
 9 ISNEG
 8 ERR2
ISNEG /4 4
 5 COMBA
 8 NEXTRLB
TYPE2
 1 FRTAD
 5 I
 0 I
 4 NWA
 1 +1
TIPTOO
 1 NOFFSET
 /5 0
 4 +0
 8 ;+2
TYPE3
 1 -4096
 5 NOFFSET
 8 NEXTRLB
TYPE4 4 IMF
 7 ZEERO
 4 NWA
 5 NLWA
 8 TESTWO
ZEERO 4 NWA
 5 NCWA
TESTWO 4 +0
 5 IMF
 4 TEMPRY
 7 OREZ
 1 CBA
 8 SUBSETUP+2
OREZ 4 NCWA
 8 SUBSETUP+2
TYPE5 4 IMF
 7 CHAN1
 4 NWA
 5 NLWA
 8 CHAN2
CHAN1 4 NWA
 5 NCWA
 10 IMF
CHAN2 4 TEMPRY
 7 CHANG1
 1 LDBA
 8 SUBSETUP+2
CHANG1 4 NLWA
 8 SUBSETUP+2
TYPE7
 11 QINRLB
 8 QINRLB+1
 8 SUBSETUP+2
TYPE8 1 MODQMC
 1 =8 0
 11 STOR2
 8 STOR2+1
 4 =11 0
 1 MODQMC
 11 STOR2
 8 STOR2+1
 8 NEXTRLB
TYPE9
 11 DICTSEARCH
 8 DICTSEARCH+1
 0 PTR1
 /4 2
 9 SNEG
 4 NWA
 11 CHAIN
 8 CHAINB
 8 SUBSETUP
SNEG /4 4
 6 &17777
 5 PP
 /4 4
 6 &760000
 5 MP
 4 NWA
 6 &760000
 2 MP
 7 SETUP
 1 PP
 8 SETUP
TYPE10
 11 DICTSEARCH
 8 DICTSEARCH+1
 0 PTR1
 /4 2
 9 ISLOC
 4 NWA
 1 -1
 11 CHAIN
 8 CHAIN+1
 4 =8 11
 1 MODQMC
 11 STOR2
 8 STOR2+1
 11 STOR2
 8 SUBSETUP
 8 NEXTRLB
ISLOC /4 4
 6 &760000
 5 MP
 2 MODADR
 7 CURMOD
 4 =8 11
 1 MODQMC
 11 STOR2
 8 STOR2+1
 4 =11 0
 1 MODQMC
 11 STOR2
 8 STOR2+1
 8 NEXTRLB
CURMOD /4 4
 6 +8191
 5 MP
 1 =8 1
 11 STOR2
 8 STOR2+1
 4 MP
 1 =11 0
 11 STOR2
 8 STOR2+1
 8 NEXTRLB
TYPE11
 4 +9
 5 X
 4 +10
 5 Y
 4 MODQMC
 1 X
 1 MODADR
 1 LMOD2
 5 TEMP+1
 4 PARAM
 6 &4
 7 STRQ
 8 NOQ
STRQ
 0 TEMP+1
 /4 0
 7 ;+2
 8 STORIT
NOQ
 4 TEMPRY
 11 DICTSEARCH
 8 DICTSEARCH+1
 0 PTR1
 /4 2
 9 LOCC
 4 PARAM
 6 &4
 7 CHNQ
 0 MODNR
 /4 QFPCHOP
 5 PP
 4 X
 2 +9
 7 QFPIN
 4 PP
 6 &1
 7 NOQ1
 8 STORIT
QFPIN 4 PP
 6 &2
 7 NOQ2
 8 STORIT
NOQ2 4 &2
 8 NOQ1+1
NOQ1 4 &1
 /1 QFPCHOP
 /5 QFPCHOP
CHNQ
 4 &400000
 1 TEMP+1
 1 LMOD
 11 CHAIN
 8 CHAIN+1
STORIT 4 Y
 8 TYPE8
LOCC
 4 MODADR
 /2 4
 5 PP
 4 TEMP+1
 1 LMOD
 5 ADRES
 /4 3
 6 &17777
 7 ;+2
 1 -4096
 1 PP
 11 STORE
 8 STORE+1
 8 STORIT
TYPE12 4 TEMPRY
 11 DICTSEARCH
 8 DICTSEARCH+1
 0 PTR1
 /4 2
 9 YES
 4 NWA
 1 &400000
 11 CHAIN
 8 CHAIN+1
 8 SUBSETUP
YES 6 &200000
 7 NOTSET
 /4 3
 6 &017777
 1 -4096
 /1 4
 5 OFFADR
 4 NWA
 6 &760000
 5 MP
 /4 3
 11 SPECIAL
 8 SPECIAL+1
 8 SETUP
NOTSET 4 NWA
 6 &760000
 /2 4
 8 SETUP
TYPE13 1 FRTAD
 5 I
 11 QINRLB
 8 QINRLB+1
 0 I
 8 TIPTOO
TYPE14 4 +5
 5 X
 4 +1
 5 Y
 8 TYPE11+4
TYPE31 11 CLEARH
 8 CLEARH+1
 0 MODNR
 /4 QMCAD
 1 +1
 7 ;+4
 /4 QMCAD
 6 +8191
 /5 QMCAD
 4 PARAM
 6 +128
 7 ;+2
 8 TY31EX
 4 \^UN
 11 PRICH
 8 PRICH+1
 4 \IT
 11 PRICH
 8 PRICH+1
 4 NAM
 11 PRICH
 8 PRICH+1
 4 NAM+1
 11 PRICH
 8 PRICH+1
 4 +0
 11 QCHOP
 8 QCHOP+1
 4 FIRST
 11 DECOUT
 8 DECOUT+1
 4 &3200
 11 PRICH
 8 PRICH+1
 4 LAST
 11 DECOUT
 8 DECOUT+1
 4 +1
 11 QCHOP
 8 QCHOP+1
TY31EX 4 STARTAD (SET STARTAD HERE SO NOT CORRUPTED BY LIBRARY UNITS)
 7 ;+2
 8 ;+3
 4 CBA
 5 STARTAD
 4 QRWORD+2
 2 SUMCH
 7 OFFWEGO
 8 ERR1
DECOUT +0
 14 8179
 6 +15
 5 DECMOD
 4 +0
 5 DECFG
 14 13
 5 DECNUM
DECLP
 4 -5
 5 DECCNT
 4 +0
 5 LZMK
 5 DIGIT
AGEN 0 DECCNT
 /4 POWER+5
 2 DECNUM
 9 PRNT
 5 DECNUM
 10 DIGIT
 8 AGEN
PRNT 4 DIGIT
 7 TESTLZ
 10 LZMK
 4 DIGIT
 1 +16
 11 QCHOP
 8 QCHOP+1
 10 SPACES
CANCEL 10 DECCNT
 4 DECCNT
 7 DECLAST
 1 -1
 7 FINI
 4 +0
 8 AGEN-1
DECLAST 10 LZMK
 8 ;-3
TESTLZ 4 LZMK
 7 CANCEL
 8 PRNT+3
FINI 4 DECFG
 7 ;+3
DECXIT 0 DECOUT
 /8 1
 10 DECFG
 4 DECMOD
 7 DECXIT
 5 DECNUM
 4 +62
 11 QCHOP
 8 QCHOP+1
 10 SPACES
 8 DECLP
DECCNT +0
DECFG +0
DECMOD +0
DECNUM +0
LZMK +0
DIGIT +0
POWER +100000
  +10000
  +1000
  +100
  +10
  +1
SETITEM +0
 0 POINTER
 4 TEMP
 /5 0
 4 TEMP+1
 /5 1
 4 +0
 /5 2
 /5 3
 4 -1
 /5 4
 4 POINTER
 1 ESIZE
 5 POINTER
 4 PARAM
 6 +4
 7 ;+3
SETXIT 0 SETITEM
 /8 1
 4 POINTER
 1 LMOD
 14 8179
 6 +15
 5 MP
 14 13
 0 MP
 /2 FREETOP
 9 ;+2
 8 SETXIT
 /4 DSTARR
 9 SETXIT
 8 ERR5
STORE +0
 5 WS
 4 PARAM
 6 &4
 7 CORE
 4 ADRES
 6 &377777
 1 +1
 2 LASTAD
 7 NXTLO
 11 BUFOUT
 8 BUFOUT+1
NXTLO 4 BUFWD
 1 -260
 11 BUFOUT
 7 BUFOUT+1
 4 ADRES
 6 &377777
 5 LASTAD
 4 BUFWD
 2 SCBUF
 5 PP
 0 PP
 4 WS
 /5 0
 10 BUFWD
 8 STEXT
CORE
 4 LMOD
 2 ADRES
 5 RELADD
 0 RELADD
 4 WS
 /5 0
STEXT
 0 STORE
 /8 1
BUFOUT +0
 4 BUFWD
 7 BUFEXT
 4 LASTAD
 11 SCB
 8 SCB+2
 4 BUFWD
 1 -1
 2 SCBUF
 5 PP
SCBLOP 0 PP
 /4 0
 11 SCB
 8 SCB+1 (OUTPUT WORDS IN BUFFER)
 4 PP
 2 SCBUF
 7 BUFEXT
 10 PP
 8 SCBLOP
BUFEXT 5 BUFWD
 0 BUFOUT
 /8 1
CHAIN +0
 5 CHNWD
 6 &377777
 1 LMOD2
 5 CHNAD
 4 PARAM
 6 &4
 7 FNA0
 4 +0
 8 FNB1
FNA0
 0 PTR1
 /4 4
 0 CHNAD
 /5 0
 4 CHNWD
 0 PTR1
 /5 4
EXTCHN 0 CHAIN
 /8 1
CHAINB 5 CHNWD
 4 PARAM
 6 &4
 7 FNB0
FNB1 5 FUNMKR
 4 BUFPTR
 5 BUFFY
 0 BUFPTR
 /4 0
 1 +1
 7 MINI
 /4 0
 1 LMOD2
 5 BUFPTR
 8 MINNIE
MINI 4 BUFPTR
 1 -2
 5 BUFPTR
 6 =/15 0
 2 POINTER
 6 =/15 0
 7 ;+2
 8 ;+4
 4 POINTER
 2 BUFPTR
 9 ERR5
 0 BUFPTR
 4 -1
 /5 0
MINNIE 0 PTR1
 4 FUNMKR
 7 ;+2
 4 &400000
HEX
 /1 4
 0 BUFFY
 /5 0
 4 CHNWD
 /5 1
 4 BUFFY
 1 LMOD
 6 &377777
 0 PTR1
 /5 4
 8 EXTCHN
FNB0
 4 CHNWD
 5 ADRES
 4 -1
 11 STORE
 8 STORE+1
 8 EXTCHN
SCB +0
 8 EE1
 5 QRWRD
 4 +8
 8 SCBEXT
EE1 5 QRWRD
 4 +0
SCBEXT 5 QRWRD+1
 11 QOUTSCB
 8 QOUTSCB+1
 0 SCB
 /8 1
MAINEN 11 TESWMK
 8 TESWMK+1
 4 +3
 5 QDOUT
 4 +0
 5 COUNT
 5 EXIN
 4 DICTAD
UNLOCS 5 PTR1
 2 POINTER
 7 MFIN
 0 PTR1
 /4 2
 6 =/4 0
 7 ;+3
 9 NXLAB
 8 ADDER
 10 COUNT
 4 &011265
 11 OPNAM
 8 OPNAM+1
ADDER
 4 PTR1
 1 ESIZE
 8 UNLOCS
NXLAB /4 0
 2 \MAI
 7 SECWD
 /4 0
 2 \EX
 7 ADEX
 8 ADDER
ADEX 4 +19
 5 ADRES
 /4 4
 11 STORE
 8 STORE+1
 4 +1
 5 EXIN
 8 ADDER
SECWD /4 1
 2 \N
 7 ;+2
 8 ADDER
 /4 4
 5 STARTAD
 8 ADDER
MFIN 4 COUNT
 7 NOUNS
 4 OVERAD
 1 LMOD
 0 LMOD2
 /5 18
 8 QSTOP
NOUNS
 4 &014757
 11 PRICH
 8 PRICH+1
 4 PARAM
 6 +64
 7 STM3
 4 -9
 5 COUNT
MSOUT 0 COUNT
 /4 MESSAG+9
 11 PRICH
 8 PRICH+1
 10 COUNT
 4 COUNT
 9 MSOUT
STM1 4 -10
 5 SPACES
 0 COUNT
 /4 FREELO
 11 DECOUT
 8 DECOUT+1
STM2 4 +0
 11 QCHOP
 8 QCHOP+1
 10 SPACES
 4 SPACES
 9 STM2
 0 COUNT
 /4 FREETO
 11 DECOUT
 8 DECOUT+1
 4 +1
 11 QCHOP
 8 QCHOP+1
 10 COUNT
 4 MAXSTORE+1
 2 COUNT
 9 STM1
STM3
 4 +18
 5 ADRES
 4 STARTAD
 7 QSTOP
 11 STORE
 8 STORE+1
 4 PARAM
 6 &4
 7 QSTOP
 4 +16
 5 ADRES
 4 EXIN
 1 +18
 11 STORE
 8 STORE+1
 10 ADRES
 4 =/8 0
 11 STORE
 8 STORE+1
 11 BUFOUT
 8 BUFOUT+1 (OUTPUT REST OF BUFFER IF ANY)
 4 +0
 5 QRWRD
 4 +8
 5 QRWRD+1
 11 QOUTSCB
 8 QOUTSCB+1
 4 QRWRD+2
 5 QRWRD
 11 QOUTSCB
 8 QOUTSCB+1
 8 QSTOP
OVERAD 0 OVERRD
OVERRD
 4 &015766
 8 NOUNS+1
MESSAG \^^
  \FRE
  \E S
  \TOR
  \E^F
  \ROM
  +0
  +0
  \TO^
SPACES +0
QOUTSCB +0
 8 QENT2
 4 +76
 15 6144
QENT2 4 QRWRD
 1 QRWRD+2
 5 QRWRD+2
 4 QRWRD+1
 0 QRWRD
 14 4
 15 6144
 14 7
 6 +127
 15 6144
 14 7
 6 +127
 15 6144
 0 QOUTSCB
 /8 1
OPNAM +0
 11 PRICH
 8 PRICH+1
 4 \LW
 11 PRICH
 8 PRICH+1
OPNAM1 4 ERRMK
 7 OPNAM2
 4 ERRPAR
 6 +1
 7 ;+2
 8 OPNOUT
OPNAM2
 0 PTR1
 /4 0
 11 PRICH
 8 PRICH+1
 0 PTR1
 /4 1
 11 PRICH
 8 PRICH+1
OPNOUT
 0 OPNAM
 /8 1
CBWARN 4 &011243
 11 PRICH
 8 PRICH+1
 4 \OM
 8 CWARN
CLW 11 PRICH
 8 PRICH+1
 4 \LW
CWARN 11 PRICH
 8 PRICH+1
ERRCOM 4 NAM
 11 PRICH
 8 PRICH+1
 4 NAM+1
 11 PRICH
 8 PRICH+1
 4 +0
 11 QCHOP
 8 QCHOP+1
 8 OPNAM1
CLEARH +0
 4 DICTAD
 5 PTR1
 5 DICPTR
NITEM 4 PTR1
 2 SPOINT
 7 ;+2
 8 ;+3
 4 DICPTR
 5 SPOINT
 4 PTR1
 2 POINTER
 7 ALLCLR
 4 PSAVE
 2 PTR1
 0 PTR1
 9 FROZEN (DO NOT COLLAPSE FROZEN DICTIONARY)
 /4 2
 6 =/4 0
 2 =/4 0
 7 DELETE
FROZEN /4 2
 6 =/15 0
 /5 2
 6 &600000
 2 &600000
 7 ;+2 (IF LOCATED + OFFSET)
 8 ;+2
 /5 3 (SET OFFSET =0)
 4 +0
 5 COUNT
COPPY 0 PTR1
 /4 0
 0 DICPTR
 /5 0
 10 PTR1
 10 DICPTR
 10 COUNT
 4 ESIZE
 2 COUNT
 9 COPPY
 8 NITEM
DELETE 4 ESIZE
 1 PTR1
 5 PTR1
 8 NITEM+1
ALLCLR 0 CLEARH
 4 DICPTR
 5 POINTER
 /8 1
LISTLA +0
 4 PARAM
 6 +64
 7 LISXIT
 4 +3
 5 QDOUT
 4 +1
 11 QCHOP
 8 QCHOP+1
 11 OPNAM
 8 OPNAM2
 4 +0
 11 QCHOP
 8 QCHOP+1
 0 PTR1
 /4 4
 11 DECOUT
 8 DECOUT+1
LISXIT 0 LISTLA
 /8 1
UPDATE +0
 0 PTR1
 /4 2
 9 ;+2
 8 UPDAT1
 4 PDSIZE (IF LABEL ALREADY LOCATED & UNIT SIZE = 0)
 6 LIBMK (& LIBRARY SCAN, DO NOT UPDATE AGAIN, BUT CONTINUE)
 2 PDSIZE
 1 LIBMK
 7 UPDOUT
 4 LDMK (IF SCANNING & UNIT SIZE NOT = 0,)
 7 SKIPBL (SKIP THIS UNIT IF NOT ALREADY CALLED)
 8 ERR4 (OTHERWISE, LABEL DECLARED TWICE)
UPDAT1 0 PTR1
 /4 2
 1 &400000
 /5 2
 /4 4
TESTMIN 5 P
 0 PTR1
 /4 2
 6 &200000
 7 ZEROFF
 /4 3
 7 ZEROFF
 6 &760000
 5 FNMK
 /4 3
 6 &017777
 1 -4096
 1 ACTADR
 5 OFFADR
 8 NOFF
ZEROFF 4 ACTADR
 5 OFFADR
 4 &760000
 5 FNMK
NOFF 0 PTR1
 4 ACTADR
 /5 4
 4 P
 1 +1
 7 HOP
 4 P
 1 LMOD2
 5 W
 0 W
 /4 0
 5 HOLD
 4 PARAM
 6 +4
 7 ZERPR
 4 BUFPTR
 1 LMOD
 /5 0
 4 W
 5 BUFPTR
 /4 1
 5 P
 4 HOLD
 2 =15 8191
 7 END7T9
 4 HOLD
 9 PPARM
E 4 P
 9 ZERPR
 4 -1
 8 EXTUP
PPARM 1 +1
 7 E
EE 4 P
 8 EXTUP
END7T9 4 -1
 5 HOLD
 8 EE
ZERPR 4 +0
EXTUP
 5 MKR2
 4 P
 6 &360000
 5 MP
 14 8179
 5 TEMPRY
 4 P
 9 TISNEG
 4 MKR2
 9 CALLQMC
 7 ;+2
 8 UP7T9
 4 +1
 2 P
 1 LMOD2
 5 W
 0 W
 /4 0
 6 +8191
 1 MP
UP7T9
 5 ADRES
 5 RELX
 4 ACTADR
 6 &360000
 2 MP
 7 SAMEMOD
 4 MP
 2 ACTADR
 11 STORE
 8 STORE+1
 4 MKR2
 7 CQ2
 8 SWOP
CALLQMC
 4 ACTADR
 6 &360000
 2 MP
 7 CSUB
CQ2 4 P
 5 ADRES
 4 =11 0
 0 TEMPRY
 /1 QMCAD
 6 &377777
 11 STORE
 8 STORE+1
 4 =8 11
 0 TEMPRY
 /1 QMCAD
 6 &377777
 10 ADRES
 11 STORE
 8 STORE+1
 8 SWOP
TISNEG
 4 P
 5 ADRES
 4 FNMK
 11 SPECIAL
 8 SPECIAL+1
 11 STORE
 8 STORE+1
SWOP 4 HOLD
 8 TESTMIN
SAMEMOD
 11 STORE
 8 STORE+1
 4 MKR2
 7 CSUB
 8 SWOP
CSUB
 4 P
 5 ADRES
 4 ACTADR
 6 +8191
 5 ACT
 1 =11 0
 11 STORE
 8 STORE+1
 4 =8 1
 1 ACT
 10 ADRES
 11 STORE
 8 STORE+1
 8 SWOP
HOP
 4 PTR1
 5 PTRHLD
 11 WORDSEARCH (SEE IF NAME OCCURS AGAIN)
 8 WRNG
 9 UPDAT1
 4 PTRHLD
 5 PTR1
 4 LDMK
 7 ;+3
 11 LISTLA
 8 LISTLA+1
UPDOUT
 0 UPDATE
 /8 1
SPECIAL +0
 6 =/15 0
 5 TEMPRY
 1 =1 0
 7 SL15
 1 =1 0
 7 SL14
 1 =15 0
 7 F15
 1 =1 0
 7 F14
 4 OFFADR
 6 +8191
 1 TEMPRY
EXX 0 SPECIAL
 /8 1
SL15 4 MP (+NAME)
SL14
 2 OFFADR
 8 EXX
F15 4 MP
F14
 2 OFFADR (+NAME/)
 6 &760000
 8 EXX
WORDSEARCH +0
 4 DICTAD
LOOK 5 PTR1
 2 POINTER
 7 ENDDD
 0 PTR1
 /4 0
 2 TEMP
 7 NEXTWD
 8 WRNG
NEXTWD /4 1
 2 TEMP+1
 7 GOTIT
WRNG 4 PTR1
 1 ESIZE
 8 LOOK
GOTIT 4 -1
ENDDD 0 WORDSEARCH
 /8 1
DICTSEARCH +0
 5 TEMP
 4 DICTAD
GO 5 PTR1
 0 PTR1
 2 POINTER
 7 ERR9
 /4 2
 6 &017777
 2 TEMP
 7 FOUNDIT
 4 ESIZE
 1 PTR1
 8 GO
FOUNDIT 0 DICTSEARCH
 /8 1
STALLOC +0
 5 SIZE2
 7 CANLAP
 9 CANLAP
 2 +8174
 9 ERR7C
 4 +0
 8 LAB
ERR7C 11 SKIP
 8 SKIP+1
 8 ERR7
CANLAP 6 &377777
 5 SIZE2
 4 -1
LAB 5 OVERX
 4 MAXSTORE+1
 5 COUNT
 4 +0
 5 QMCMARK
GOGO
 4 +1
 2 COUNT
 5 COUNT
 9 ERR3D
GOGO1 4 SIZE2
GOGO2 5 SIZE
 0 COUNT
 /4 FREELOW
 /2 FREETOP
 1 +1
 5 FREEAREA
 4 OVERX
 9 COMBLE
 /4 QMCAD
 9 QMCABS
 8 COMBLE
QMCABS 4 SIZE
 1 +18
 5 SIZE
 10 QMCMARK
COMBLE 4 FREEAREA
 2 SIZE
 7 ERM2
 9 ERM2
 4 QMCMARK
 7 OKKK
 4 +0
 5 QMCMARK
 4 SIZE
 1 -18
 5 SIZE
OKKK 4 OVERX
 7 GOGO
 4 FREEAREA
 2 SIZE
 5 TEMP+1
 4 +1
 2 COUNT
 5 COUNT
 9 ERR12C (NOT ENOUGH ROOM)
 0 COUNT
 /4 FREELO+1
 1 -1
 /2 FREETO
 7 STALC1
 4 -16
 5 CNT1
RESET 0 CNT1
 /4 SAVEST+32
 /5 FREETOP+16
 10 CNT1
 4 CNT1
 9 RESET
 8 GOGO1
STALC1 /4 FREETOP
 /5 FREETO+1
 4 TEMP+1
 8 GOGO2
ERM2
 4 OVERX
 9 QMCIN ( IF COMMON BLOCK DO NOT ALLOCATE QMC)
EROOM
 /4 QMCAD
 9 ;+2
 8 QMCIN (ALREADY IN)
 /4 FREELO
 1 +18
 /2 FREETO
 9 ERR3C (NOT ENOUGH ROOM)
 /4 FREETOP
 5 QMCAD2
 1 -17
 6 +8191
 5 QMCAD1
 1 =/0 0
 /5 QMCAD
 8 WASQMC
 4 +18
 5 SIZE
WASQMC 4 SIZE
 7 ;-3
QMCIN 0 COUNT
 4 SIZE
 /2 FREETOP
 /5 FREETOP
 0 STALLOC
 /8 1
PAUSE +0
 10 WMK
 4 PARAM
 6 +256
 7 ;+2
 8 ;+3
 11 QPAUSE
 8 QPAUSE+1
 4 +0
 5 WMK
 0 PAUSE
 /8 1
TESWMK +0
 4 WMK
 7 ;+4
 11 SKIP
 8 SKIP+1
 5 WMK
 0 TESWMK
 /8 1
ERR3D 4 OVERX
 7 ERR3C
ERR12C 4 ;+2
 8 ERR3C+1
 8 ERR12
 8 ERR3
ERR3C
 4 ;-1
 5 ERRJMP (SET CORRECT ERROR JUMP INSTRUCTION)
 4 PTR1 (SAVE POINTER TO ITEM CAUSING ERROR..)
 5 X (..USED BY ERR10 TO OUTPUT LABEL NAME)
 4 LDMK (ERRORS 3 & 10 NOT VALID IF..)
 7 ;+2 (..WHEN LIBRARY SCANNING, UNIT IS NOT REQD)
 8 ERRLIB
 4 -5 (SKIP TO TYPE WORD TO SEE IF SECTION 8 PRESENT)
 5 COUNT
 11 QINRLB
ERRLLP 8 QINRLB+1
 10 COUNT
 4 COUNT
 9 ERRLLP
 4 QRWORD
 6 +7
 7 ;+2
 8 SKIPBL (FORTRAN UNIT, IGNORE ERROR & SKIP PROG UNIT)
DSECT8 11 QINRLB (DUMMY SECTION 8)
 8 QINRLB+1
 5 TEMP
 4 QRWORD+1
 2 +6
 7 SKIPBL (END OF SECTION 8, IGNORE ERROR & SKIP PROG UNIT)
 11 QINRLB
 8 QINRLB+1
 5 TEMP+1
 11 WORDSEARCH
 8 WORDSEARCH+1
 9 DS8IN
 11 QINRLB (LABEL NOT IN DICT, SKIP TO NEXT LABEL)
 8 QINRLB+1
 11 QINRLB
 8 QINRLB+1
 8 DSECT8
DS8IN 0 PTR1
 /4 2
 9 SKIPBL (ALREADY LOCATED, SKIP THIS UNIT)
ERRLIB (PRESENT & NOT LOCATED, VALID ERROR)
 11 SKIP
 8 SKIP+1
 4 X
 5 PTR1
ERRJMP +0 (CONTAINS 8 ERR3 OR 8 ERR10)
ERR12 10 ERRPAR
ERR11 10 ERRPAR
ERR10 10 ERRPAR
ERR9 10 ERRPAR
ERRX
 10 ERRPAR
ERR7 10 ERRPAR
ERR6 10 ERRPAR
ERR5 10 ERRPAR
ERR4 10 ERRPAR
ERR3 10 ERRPAR
ERR2 10 ERRPAR
ERR1 10 ERRPAR
ERR0 10 ERRMK
 11 QRR
 8 QRR+1
 0 DATA
 4 ERRPAR
 7 ;+4
 11 OPNAM
 8 ERRCOM
 4 +0
 5 ERRMK
LHALT 11 QPAUSE
 8 QPAUSE+1
 8 LOAD+1
ERRMK +0
DATA 0 +0
 0 1
  \LDR
ERRPAR +0
NAM >+2
PTR1 +0
BUFFY +0
PSAVE +0
PARAM +0
BUFWD +0
ESIZE +5
COUNT +0
CNT1 +0
SUMCH +0
FRTSIZ +0
HOLD +0
QMCAD >+16
QMCAD1 +0
QMCAD2 +0
QMC +0 (QMC)
 0 5
 /5 3
 4 5
 8 11
  +0
  +0
  +0
  &760000
  +0 (ADDRESS OF QFP)
 4 9 (ENTRY TO ARITH. PACKAGE)
 5 7 (NORMAL ENTRY FOR SUBROUTINES)
 0 7
 6 8
 2 0
 /5 0
 6 8
 /8 1
QMCWORD &276032
QMCMARK +0
X +9
Y +10
P +0
MP +0
W +0
RELX +0
ACT +0
EXIN +0
SCBUF +0
PDSIZE +0
NWA +0
NCWA +0
NLWA +0
CBA +0
COMBA +0
LDBA +0
BA2 +0
TEMP >+2
TEMPRY +0
SIZE +0
SIZE2 +0
OVERX +0
IMF +0
FRTAD +0
FREEAREA +0
NOFFSET +0
I +0
ACTADR +0
OFFADR +0
FNMK +0
LDMK +0
LIBMK +0
WMK +0
BCOMMK +0
QRWRD >+3
VALUE +0
FSTMK +0
DICPTR +0
MODNR +0
MODQMC +0
MODADR +0
PP +0
SPOINT +0
ADSAVE +0
PTRHLD +0
LMOD +0
LMOD2 +0
ADRES +0
MKR2 +0
RELADD +0
WS +0
CHNWD +0
CHNAD +0
LASTAD +0
BUFPTR +0
BUFAD +0
FUNMKR +0
MAINAD 0 MAINEN
STARTAD +0
SAVEST >32
FIRST +0
LAST +0
DSTARR >16 (SLAVE TO QSTARR)
T23 0 8179
 8 8182
  -45
 4 8179
 15 2048
 7 8136
 5 8173
 6 8178
 7 8151
 4 8173
 15 2048
 15 2048
 7 8160
 5 8174
 1 8175
 5 8175
 4 8179
 15 2048
 8 8138
 0 8174
 4 8173
 15 2048
 15 2048
 /5 0
 1 8175
 5 8175
 10 8174
 8 8148
 15 2048
 15 2048
 15 2048
 2 8175
 7 8164 (CHECK OK SO STOP)
 15 6144
 8 8165 (CHECK FAIL OUTPUT DIFFERENCE)
 14 0
 14 0
 14 0
 14 0
 7 8171
 8 8165
  +0
  +8
  +0
  +0
 8 8135
  +128
  +0 (PTEXEC 15/4/70 VERSION 6)
[STARTEX QEXCON QCHOP QDOUT LOAD QEP QSTOP QPAUSE]
STARTEX
ASK
 4 +141
 15 6148
 4 +10
 15 6148
 4 +95
 15 6148
RDTP
 4 +0
 15 2052
 6 &000177
 1 -48
 9 RDTP
 5 CHAR
 1 -8
 9 DIGIN
 1 -11
 7 CC
 1 -9
 7 LL
 1 -1
 7 MM
 1 -2
 7 OPTIN
 1 -3
 7 RR
 1 -5
 7 ;+0 (W)
 8 RDTP
QEXCON +0
CC 0 QEP
 /8 1
LL
 4 OPTION
 0 +0
 3 OPTION
 8 LOAD
AMAIN=18
RR
MM 4 +0
 5 QEXCON
 4 OPTION
 11 W
 14 18
 2 +0
 5 W
 4 W
 0 W
 /1 AMAIN
 5 W
 0 W
 4 OPTION
 /8 0
OPTIN 4 +0
 5 OPTION
 8 RDTP
DIGIN
 4 OPTION
 0 +0
 14 3
 1 CHAR
 5 OPTION
 8 RDTP
CHAR +0
OPTION +64
CT +0
W +0
QSTOP 8 ASK
QEP +0
QPAUSE +0
 4 QPAUSE
 5 QEP
 8 ASK
[QINRLB QRWORD ]
QINRLB +0
 4 +0
 15 2048
 7 ;-2
 5 QRWORD
 14 8188
 6 &000007
 5 QRWORD+1
 4 QRWORD
 15 2048
 15 2048
 5 QRWORD
 1 QRWORD+1
 1 QRWORD+2
 5 QRWORD+2
 4 QRWORD
 0 QINRLB
 /8 1
QRWORD >+3
[ QCHOP QDOUT
QDOUTS QRR QPAUSE ]
PW +0
PW1 +0
CH +0
PARITY
  +0
 0 +0
 3 PW
P1
 7 P2
 10 PW
 5 PW1
 1 -1
 6 PW1
 8 P1
P2
 4 PW
 0 PARITY ( PARITY BIT IS L.S. ACC)
 /8 1
QCHOP
  +0
 5 CHAR1
 4 QCHOP
 5 QCHOP+7
 8 NORMAL
 0 QCHOP+7 (EXIT VIA QCHOP)
 /8 1
  +0
 8 NORMAL
 8 SLAVE
CHAR1 +0
NORMAL
 4 CHAR1
 0 QDOUT
 /0 QDOUT
 /8 0
QDOUT +1
 0 PTOUT
 0 PTOUT
 0 PTOUT
 0 PTOUT
 0 PTOUT
 0 PTOUT
 0 PTOUT
 0 PTOUT
 0 PTOUT
 0 PTOUT
QDOUTS +0 (SMAVE)
SLAVE
 8 ;+0
PTOUT
 6 =/0 255
 9 SPEC ( OUTPUT BINARY PATTERN)
 1 -1
 7 NL ( TEST FOR NEWLINE)
 1 +33
 5 CH
 11 PARITY
 8 PARITY+1
 14 7
 6 +128
 1 CH ( ADD CORRECT PARITY BIT)
SPEC
 5 CH
 4 QDOUT
 1 -3
 7 PTP ( JUMP IF TELEPRINTER REQIRED)
 4 CH
 15 6144 ( OUTPUT TO PUNCH)
EXIT
 6 &377400
 7 QCHOP+5
 14 8184
 1 -1 ( FORM NEXT CHARACTER IN NEWLINE SEQUENCE)
 8 SPEC
PTP
 4 CH
 15 6148 (TELEPRINTER)
 8 EXIT
NL
 4 &205615 ( SEQUENCE FOR NEWLINE)
 8 SPEC
[QRR PRICH QDOUT QCHOP QPAUSE]
QRR +0
 4 +3
 5 QDOUT
 4 +1
 11 QCHOP
 8 QCHOP+1
 4 +10
 11 QCHOP
 8 QCHOP+1
 0 QRR
 /0 1
 /4 3
 5 STATUS
 /4 2
 11 PRICH
 8 PRICH+1
 4 +0
 11 QCHOP
 8 QCHOP+1
 4 -6
 5 CNT
OCTL
 4 STATUS
 14 8177
 6 +7
 1 &20
 11 QCHOP
 8 QCHOP+1
 4 STATUS
 14 3
 5 STATUS
 10 CNT
 4 CNT
 9 OCTL
 4 +0
 11 QCHOP
 8 QCHOP+1
 0 QRR
 /8 2
STATUS +0
MASK &000077
LS1 +0
CNT +0
PRICH +0 (ROUTINE TO PRINT 3 CHARACTERS WHOSE INTERNAL CODES ARE IN A)
 5 LS1
 4 +3
 5 QDOUT
 4 LS1
 14 8180
 6 MASK
 11 QCHOP
 8 QCHOP+1
 4 LS1
 14 8186
 6 MASK
 11 QCHOP
 8 QCHOP+1
 4 LS1
 6 MASK
 11 QCHOP
 8 QCHOP+1
 0 PRICH
 /8 1 (RETURN) (END OF PRICH)
% (905 LOADER SETTING ROUTINE)
[LODSET LDICT QDIN QCHIN QSTARR MAXSTORE LOAD STARTEX]
LDICT
LODSET 4 +3
 5 QDIN
 4 +0
 15 2052
 1 -141
 7 LODSET
 1 +131
 7 LODSET
 1 -73
 7 SISIN
 1 -115
 7 FISIN
 1 +130
 7 DISIN
 1 -136
 7 LISIN
 8 LODSET
SISIN 11 DIGCHK
 8 DIGCHK+1
 11 QPINM (INPUTS ABS ADDRS OF STORE SIZE)
 8 QPINM+1
 5 MAXSTORE
 14 8179
 6 +31
 5 MAXSTORE+1 (NO OF MODULES)
 8 LODSET+2
FISIN 4 QSTAD (ADDRESS OF QSTARR)
 5 TEMP
 11 DIGCHK
 8 DIGCHK+1
 11 SININ
 8 SININ+1
 5 HOLDMD
 1 TEMP
 5 TEMP
 11 DIGCHK
 8 DIGCHK+1
 11 QPINM
 8 QPINM+1
 1 -1
 0 TEMP
 /5 0
 4 HOLDMD
 7 LODSET
 8 FISIN
QSTAD 0 QSTARR
TEMP +0
HOLDMD +0
DISIN 5 TABLE
 11 RUNOUT
 8 RUNOUT+1
 4 -48
 5 W
LOOP 0 W
 4 +76
 15 6144
 /4 T23ADD+48
 11 OPWORD
 8 OPWORD+1
 10 W
 4 W
 9 LOOP
 5 WORDSM
 5 DIRSM
 4 +8
 5 DIRMK
 4 NEWMOD
 5 FIRST
 1 TABLE
 11 OPWORD
 8 OPWORD+1
 1 DIRSM
 5 DIRSM
DUMP 0 FIRST
 /4 0
 11 OPWORD
 8 OPWORD+1
 1 WORDSM
 5 WORDSM
 10 FIRST
 4 DICTAD
 2 FIRST
 9 DUMP
 4 +8
 5 DIRMK
 11 OPWORD
 8 OPWORD+1
 1 DIRSM
 5 DIRSM
 4 =8 8
 11 OPWORD
 8 OPWORD+1
 1 WORDSM
 5 WORDSM
 4 +8
 5 DIRMK
 4 +15
 11 OPWORD
 8 OPWORD+1
 1 DIRSM
 5 DIRSM
 4 -3
 5 W
DMPEND 0 W
 /4 TABLE+3
 11 OPWORD
 8 OPWORD+1
 1 WORDSM
 5 WORDSM
 10 W
 4 W
 9 DMPEND
 4 +8
 5 DIRMK
 4 +0
 11 OPWORD
 8 OPWORD+1
 4 WORDSM
 11 OPWORD
 8 OPWORD+1
 4 DIRSM
 11 OPWORD
 8 OPWORD+1
 11 RUNOUT
 8 RUNOUT+1
 8 LODSET
OPWORD +0
 5 WORD
 14 8174
 4 DIRMK
 14 4
 15 6144
 4 +0
 5 DIRMK
 14 7
 15 6144
 4 +0
 14 7
 15 6144
 4 WORD
 0 OPWORD
 /8 1
WORD +0
RUNOUT +0
 4 -80
 5 W
 0 W
 4 +0
 11 OPWORD
 8 OPWORD+1
 10 W
 4 W
 9 RUNOUT+3
 0 RUNOUT
 /8 1
TABLE +0
 0 15
 /8 STARTEX
W +0
DIRMK +0
FIRST +0
WORDSM +0
DIRSM +0
NEWMOD 0 MAXSTORE
T23ADD 0 8179
 8 8182
  -45
 4 8179
 15 2048
 7 8136
 5 8173
 6 8178
 7 8151
 4 8173
 15 2048
 15 2048
 7 8160
 5 8174
 1 8176
 5 8176
 4 8179
 15 2048
 8 8138
 0 8174
 4 8173
 15 2048
 15 2048
 /5 0
 1 8175
 5 8175
 10 8174
 8 8148
 15 2048
 15 2048
 15 2048
 2 8175
 7 8167
 15 6144
 8 8165
 15 2048
 15 2048
 15 2048
 2 8176
 7 8171
 8 8165
  +0
  +8
  +0
  +0
 8 8135
  +128
  +0
LISIN 11 DIGCHK
 8 DIGCHK+1
 11 SININ
 8 SININ+1
 0 +0
 14 13
 8 DISIN
QPINM +0
 4 +0
 5 NUMBER
 5 MODREL
 11 QCHIN
 8 QCHIN+1
 5 CHAR
 7 END
 1 -1
 7 QPINM
 1 -61
 7 ARROW
 1 +51
 7 QPINM
 1 -5
 9 END
 1 -10
 9 ;+2
 8 END
 0 +0
 4 NUMBER
 14 3
 1 NUMBER
 1 NUMBER
 1 CHAR
 1 -16
 5 NUMBER
 8 QPINM+4
ARROW 4 MODREL
 7 ;+2
 8 END
 10 MODREL
 4 NUMBER
 5 ADR
 4 +0
 5 NUMBER
 8 QPINM+4
END 4 MODREL
 7 ;+6
 0 +0
 4 NUMBER
 14 13
 1 ADR
 8 ;+2
 4 NUMBER
 0 QPINM
 /8 1
NUMBER +0
MODREL +0
CHAR +0
ADR +0 (SUBRTN TO INPUT INTEGERS)
SININ +0
 4 +0
 5 NUMBA
IPC 11 QCHIN
 8 QCHIN+1
 5 ICCH
 1 -16
 9 SININ+1
 1 -10
 9 ISS
 1 -2
 9 FINIS
 8 SININ+1
ISS 4 NUMBA
 14 3
 6 &777770
 1 NUMBA
 1 NUMBA
 1 ICCH
 1 -16
 5 NUMBA
 8 IPC
FINIS 4 NUMBA
 0 SININ
 /8 1
NUMBA +0
ICCH +0 (SUBRTN TO CHECK FOR DIGITS)
DIGCHK +0
 4 +0
 15 2052
 1 -43
 0 DIGCHK
 /7 1
 8 DIGCHK+1
DICTAD 0 LDICT
[QCHIN QDIN QPAUSE QRR]
QCHIN
  +0
 8 NORM
 8 SLAVE1
  +0
  +0
 0 QCHIN
 /8 1
NORM
 0 QDIN
 /0 QDIN
 /8 0
QDIN
  +1
 0 PTIN
 0 PTIN
 0 PTIN
 0 PTIN
 0 PTIN
 0 PTIN
 0 PTIN
 0 PTIN
 0 PTIN
 0 PTIN
QDINS +0 (SLAVE)
SLAVE1 8 ;+0
PTIN +0 ( INPUT INTERNAL CODE CHAR)
 4 QDIN
 1 -3
 7 TPIN ( JUMP IF TELEPRINTER INPUT)
 4 +0
 15 2048
GOT
 5 CHAR
 11 PARITY
 8 PARITY+1
 6 +1
 7 EVPAR ( CORRECT PARITY)
INERR
 4 +0
 11 QRR
 8 QRR+1
 0 DATA
 4 +0
 8 EXIT2
TPIN
 15 2052 ( TELEPRINTER INPUT)
 8 GOT
EVPAR
 4 CHAR
 6 +127
 1 -32
 9 LESS
 1 -64
 9 KCODE
 1 -27
 9 LOWCAS ( LOWER CASE CHARS)
 1 -4
 7 QCHIN+1 ( ERASE)
 8 INERR
LOWCAS 1 +58
NWLF 1 -63 ( ADD 1)
KCODE 1 +64 (ADD 64)
EXIT2
 0 QCHIN
 /8 1
LESS
 1 +22
 7 NWLF ( NEWLINE)
 1 +1
 7 EXIT2 ( TAB)
 1 -4
 7 QCHIN+1 ( CARRIAGE RETURN)
 1 -7
 7 HALT
 4 CHAR
 7 QCHIN+1 ( BLANK)
 8 INERR
HALT
 11 QPAUSE
 8 QPAUSE+1
 8 QCHIN+1
DATA
 0 QCHIN
 0 1
  \CHN
CHAR +0
PARITY +0
 0 +0
 3 PW
P1 7 P2
 10 PW
 5 PW1
 1 -1
 6 PW1
 8 P1
P2 4 PW
 0 PARITY
 /8 1
PW +0
PW1 +0
%
<! Halt !>

<! 132 !>
