
*17

((VT220 UTILITY FOR PREPARING PAPER TAPES)
((VERSION: 31/01/2014)                      
((A.J. HERBERT)


[PERFOR PAGINA RUNOUT TTYIN TTYOUT PTPOUT PTRIN]

TTYIN=2052		(DEVICE NUMBERS)
TTYOUT=6148
PTRIN=2048
PTPOUT=6144


ENTRY	8	PERFOR	(TYPE IN A TAPE FROM KEYBOARD   )
	8	PAGINA	(OUTPUT PAGE BY PAGE LISTING    )	
	8	RUNOUT	(PRODUCE 6" OF BLANK TAPE       )

((PERFORATE)

( A PROGRAM TO READ IN TEXT FROM THE ONLINE CONSOLE     )
( AND OUTPUT TO PUNCHED PAPER TAPE WITH SIMPLE EDITING  )
( FACILITIES.  ALSO INCLUDES PROGRAM TO PRODUCE SCREEN- )
( BY-SCREEN LISTING OF AN INPUT TAPE.                   )
( CARRIAGE RETURN: END OF LINE                          )
( NEWLINE:	   END OF LINE                          )
( ERASE:           DELETE PREVIOUS CHARACTER            )
( CANCEL - CTL-X:  DELETE CURRENT LINE                  )
( HALT - CTL-T:	   OUTPUT BUFFERED INPUT TO TAPE        )

[PERFOR TTYIN TTYOUT PTPOUT BUF]

PERFOR	4	+0	(RESET BUFFER POINTER)
	5	POS	(LAST CH INDEX IN BUFFER)
	5	START	(INDEX OF END OF LAST LINE INPUT)	
READ	4	+0
	15	TTYIN	(READ NEXT CHARACTER INTO BUFFER)
	7	;-1	(SKIP BLANKS)
	5	CH
	6	+127
	1	-10	(TEST FOR CTL-J:  ASCII CODE  10)
	7	NEWLINE
	1	-3	(TEST FOR RETURN: ASCII CODE  13)
	7	STORE
	1	-7	(TEST FOR CTL-T:  ASCII CODE  20)
	7	HALT
	1	-4	(TEST FOR CTL-X:  ASCII CODE  24)
	7	CANCEL
	1	-8	(TEST FOR SPACE:  ASCII CODE  32)
	9	READ    
	1	-95	(TEST FOR ERASE:  ASCII CODE 127)
	7	ERASE

STORE	4	CH
	0	POS
	/5 	BUF	(STORE INPUT CHARACTER IN BUFFER)
	10	POS
	2 	+141	(CHECK FOR RETURN, END OF LINE)
	7	OUTNL

	4	START	(CHECK FOR LINE WRAP AT COLUMN 80)
	2	POS
	1 	-80
	7	OUTCR
	8	READ	(GET NEXT CHARACTER)

NEWLINE	4	+141	(INSERT CARRIAGE RETURN)
	15	TTYOUT
        0	POS
	/5      BUF	(STORE RETURN)
        10      POS
	8	CHECK

OUTCR	4	+141	(OUTPUT CR)
	15	TTYOUT
OUTNL	4	+10	(OUTPUT NL)
	15 	TTYOUT
CHECK	4	POS
	2	+7799	(SEE IF ROOM FOR ANOTHER LINE OF INPUT)
	7	FULL
	4	POS
	5	START	(UPDATE END OF LAST LINE INDEX)
	8	READ	(GET NEXT CHARACTER)

HALT	4       +20	(PUT HALT CODE IN BUFFER)
	0	POS	
	/5	BUF
	10	POS

FULL	4	+0	(NB BUF WILL BE AT LEAST ONE CHAR LONG)	
	5	INDEX
NXTOUT	2	POS	(SEE IF ALL BUFFERED CHARACTERS OUTPUT)
	7	PERFOR	(IF SO RESUME INPUTTING)
	0	INDEX	(NEXT CHARACTER IN BUFFER)
	/4 	BUF
	10 	INDEX
	1	-20	(HALT CODE?)
	7	HLTSEQ
	/4 	BUF	
	15	PTPOUT
	1	-141	(END OF LINE?)
	7	EOLSEQ
	4 	INDEX	
	8	NXTOUT	(PUNCH NEXT CHARACTER, IF ANY)

EOLSEQ	4	+10	(PUNCH NL BLANK)
	15	PTPOUT
	4	+0
	15 	PTPOUT
	4	INDEX
	8 	NXTOUT	(GET NEXT LINE)

HLTSEQ	4	+0	(OUTPUT BLANKS, HALT, BLANKS)
	15	PTPOUT
	15	PTPOUT
	15 	PTPOUT
	15	PTPOUT
	4	+20	(HALT CODE)
	15	PTPOUT
	8	;+0

CANCEL	4	POS	(CANCEL LINE - DELETE FROM SCREEN)
	7	READ	(BUFFER EMPTY, NOTHING TO DO)
	4	+141	(CARRIAGE RETURN)
	15	TTYOUT
	4	START
	5 	INDEX
NXTCAN	2	POS	(SEE IF ALL BUFFERED CHARACTERS BLANKED)
	7	CANDON	
	4	+160	(BLANK NEXT CHARACTER ON LINE)
	15 	TTYOUT
	10	INDEX
	4	INDEX
	8	NXTCAN	(BLANK NEXT CHARACTER, IF ANY)
CANDON	4	+141	(CARRIAGE RETURN)
	15	TTYOUT
	4	START
	5	POS	(RESET TO START OF LINE)
	8 	READ	(GET NEXT LINE)

ERASE	4	POS	(ERASE LAST CHARACTER INPUT)
	7	READ	(NOTHING TO ERASE)
	1	-1	(SET BACK BUFFER POINTER)
	5	POS
	4	+136	(BACKSPACE)
	15	TTYOUT
	4	+160	(SPACE)
	15	TTYOUT
	4	+136
	15 	TTYOUT
	8	READ	(REJOIN BUFFER FILLING LOOP)

POS	>1
INDEX	>1
START	>1
CH	>1


((PAGINATE)

( A PROGRAM TO DISPLAY TEXT ON THE ONLINE CONSOLE PAGE  )
( BY PAGE.  TYPE RETURN AFTER PAGE FULL TO START        ) 
( FILLING NEXT PAGE                                     )

[PAGINA TTYIN TTYOUT PTRIN BUF]

PAGINA	4	+0	(RESET COUNTERS)
	5	LINECT
LINE	5 	CHARCT
	5	BUFCT
CHAR	4	+0
SKIP	15	PTRIN	(READ NEXT CHARACTER)
	7	SKIP	(SKIP BLANKS)
	2	+255	
	7	SKIP	(SKIP ERASE)
	1	-114	
	7	SKIP 	(SKIP CR)
	1	-121
	7	;+0	(STOP ON HALT CODE)
	1	-10
	7	EOL	(TREAT NL AS END OF LINE)
	1 	-1
	7	TAB	(TABS AT EVERY 8 COLUMNS)	
	2	+9	(RESTORE INPUT CHARACTER)
	5 	CH
	6	+127	(MASK OFF PARITY)
	2	+31	
	9	;+2
	8	CHAR	(SKIP OTHER NON-PRINTING)
	4	CH
	0	BUFCT
	/5 	BUF
	10	BUFCT
	10	CHARCT
	4	CHARCT
	2 	+80
	7	EOL	(FORCE WRAP AT END OF LINE)
	8	CHAR	(GET NEXT CHARACTER)

TAB	4	CHARCT	(ENCOUNTERED A HORIZONTAL TAB)
	1	+7	(CALCULATE NEXT TAB STOP)
	14	8189	(DIVIDE BY 8)
	14	3
	5	CHARCT
	2	+80	(CHECK NOT PAST END OF LINE)
	7	EOL
	9	EOL
	4	+9
	0	BUFCT
	/5	BUF
	10	BUFCT
	8	CHAR

EOL	4	+0	(SHOW BUFFERED LINE)
	5 	POS
NEXT	2	BUFCT
	7	DONE
	0	POS
	/4	BUF
	15	TTYOUT
	10	POS
	4	POS
	8	NEXT
DONE	10	LINECT
	4	LINECT
	2	+25
	7	PAUSE	(PAUSE IF SCREEN FULL)
	4	+141	(PUNCH NEWLINE SEQUENCE CR, NL)
	15	TTYOUT
	4	+10
	15	TTYOUT
	4	+0
	8 	LINE	(GET NEXT LINE)

PAUSE	15	TTYIN	(WAIT FOR CR)
	2	+141	
	7	;+3 	(RESUME)
	4	+0
	8	PAUSE
	4	+141	(PUNCH NEWLINE SEQUENCE CR, NL)
	15	TTYOUT
	4	+10
	15	TTYOUT
	8 	PAGINA	(FILL NEXT SCREEN)		

CHARCT 	>1
LINECT	>1
BUFCT   >1
CH	>1
POS	>1

((RUNOUT)

(PRODUCE 6" OF BLANK TAPE)

[RUNOUT PTPOUT]

RUNOUT	4	-60	(6" OF TAPE)
	5 	COUNT
NEXT	4	+0
	15	PTPOUT
	10	COUNT
	4 	COUNT
	7	;+0
	8	NEXT

COUNT	>1	

[BUF]

BUF=300		(REST OF STORE IS USED AS BUFFER)

%
<! Halt !>
