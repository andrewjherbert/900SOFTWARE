BEZIER CURVE;

"BEGIN" "REAL" P0X,P0Y,P1X,P1Y,P2X,P2Y,T,OMT,X,Y,V1X,V1Y,V2X,V2Y;

"REAL" SCALEX, SCALEY;

"PROCEDURE" SETO(E,SCAA,SCAO,WAY); "VALUE" E,SCAA,SCAO,WAY;
   "INTEGER" E,WAY; "REAL" SCAA, SCAO;
"BEGIN"
   SCALEX := SCAA;
   SCALEY := SCAO;
   SETORIGIN(E,WAY)
"END";

"PROCEDURE" MOVPEN(X,Y); "VALUE" X, Y; "REAL" X,Y;
"BEGIN"
   MOVEPEN(X*SCALEX,Y*SCALEY)
"END";

"PROCEDURE" DRWLINE(X,Y); "VALUE" X,Y; "REAL" X,Y;
"BEGIN"
   DRAWLINE(X*SCALEX,Y*SCALEY)
"END";

"PROCEDURE" AXES(XW,YW,PX,NX,PY,NY); "VALUE" XW,YW,PX,NX,PY,NY;
   "REAL" XW,YW; "INTEGER" PX,NX,PY,NY;
"BEGIN" "REAL" X, Y;
   MOVPEN(-NX*XW,0.0); DRWLINE(PX*XW,0.0);
   MOVPEN(0.0,-NY*YW); DRWLINE(0.0,PY*YW);
   "FOR" X := -NX*XW "STEP" XW "UNTIL" PX*XW+XW/2.0 "DO"
   "BEGIN"
   	   MOVPEN(X,0.0); DRWLINE(X,0.1)
   	"END";
   	"FOR" Y := -NY*YW "STEP" YW "UNTIL" PY*YW+YW/2.0 "DO"
   	"BEGIN"
   	   MOVPEN(0.0,Y); DRWLINE(0.1,Y)
   	"END"
"END";

"COMMENT" POSITION GRAPH OF BEZIER CURVE;
SETO(100,1.0,1.0,0);
MOVPEN(0,100);
SETO(100,100.0,100.0,0);
MOVPEN(0.0,0.0);

"READ" P0X,P0Y,P1X,P1Y,P2X,P2Y;

MOVPEN(P0X,P0Y);

"FOR" T := 0.0 "STEP" 0.1 "UNTIL" 1.01 "DO"
"BEGIN"
  OMT := 1.0 - T;
  X := (OMT * OMT * P0X) + (2.0 * OMT * T * P1X) + (T * T * P2X);
  Y := (OMT * OMT * P0Y) + (2.0 * OMT * T * P1Y) + (T * T * P2Y);
  DRWLINE(X,Y);
"END";

AXES(0.1,0.1,50,0,50,0);

"COMMENT" POSITION GRAPH OF "CURVE STITCHING"";
MOVPEN(0.0,6.0);
SETO(100,100.0,100.0,0);

V1X := P0X - P1X;
V1Y := P0Y - P1Y;
V2X := P2X - P1X;
V2Y := P2Y - P1Y;

"FOR" T := 0.0 "STEP" 0.1 "UNTIL" 1.01 "DO"
"BEGIN"
  OMT := 1.0 - T;
  MOVPEN(T*V1X,T*V1Y);
  DRWLINE(OMT*V2X,OMT*V2Y);
"END";

AXES(0.1,0.1,50,0,50,0);

"END"

1.0  4.0
0.0  0.0
4.0  1.0

<! Halt !>


