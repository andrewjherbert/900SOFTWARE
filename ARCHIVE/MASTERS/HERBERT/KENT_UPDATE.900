
C KENT PRIZE UPDATE PROGRAM.  A.J. HERBERT 15/07/2012.
C
C READS A TAPE OF UPDATES TO APPLY TO A MASTER TAPE OF COMPONENT PRICES.
C
C BOTH TAPES CONTAIN RECORDS OF THE FORM
C AA 1234       1.23
C WHERE AA 1234 IS A COMPONENT NAME COMPRIZING TWO LETTERS AND FOUR 
C DIGITS AND 1.23 IS A PRICE IN pOUNDS AND PENCE,
C  0.00 <= PRICE <= 999.99.
C THE COMPONENT NAME [[ 9999 IS USED AS AN END OF FILE SENTINEL.
C
C THE UPDATE FILE IS REPRESENTED INTERNALLY AS A TWO DIMENSIONAL ARRAY
C WITH COLUMN 1 CONTAINING THE COMPONENT NAME LETTERS, COLUMN 2 THE
C COMPONENT NAME DIGITS AND COULM 3 THE PRICE IN PENCE.
C
C THE PROGRAM READS IN AND VERIFIES THE UPDATE TAPE, SORTS IT, THEN 
C MERGES THE UPDATES WITH THE MASTER TAPE, PUNCHING A NEW MASTER.
C
C LIMITATIONS:
C	UPDATE TAPE CANNOT CONTAIN MORE THAN 1500 RECORDS.
C
C
C MAIN PROGRAM
C
      INTEGER UPDATE (3, 1500), DIGS(4)
      INTEGER UPDCNT, UPDMAX, MASCNT, UPDIDX, ERRORS
      INTEGER MNAM, MDIGS, UNAM, UDIGS
      INTEGER POUND, PENCE, UPRICE, MPRICE
      INTEGER SENT1, SENT2, LETA, LETZ, A, B, C, SPC
      REAL PRICE
C
C     ...PUT UPDATE ARRAY IN COMMON TO REDUCE PROGRAM SIZE
      COMMON / ARRAYS / UPDATE  
      COMMON DIGS
C
      DATA UPDMAX / 1500 /, SENT1, SENT2 / 3H[[ , 9999 /
      DATA LETA, LETZ, SPC / 1HA, 1HZ, 1H  /
C
C     ...ANNOUNCE PROGRAM
      WRITE (3, 903)
  903 FORMAT (37HKENT PROGRAMMING PRIZE UPDATE PROGRAM/
     1        25HANDREW HERBERT 15/07/2012)
C
C     ...INITIALIZE ERROR COUNT
      ERRORS = 0
C
C     ...INSTRUCT OPERATOR TO LOAD UPDATE TAPE
      WRITE (3, 900)
  900 FORMAT (//23H***LOAD UPDATES TAPE***//)
      PAUSE 
C
C     ...READ IN UPDATES, COUNTING IN UPDCNT
      UPDCNT = 0
   10 READ  (1, 910) UNAM, UDIGS, POUND, PENCE
  910 FORMAT (A3, I4, 1X, I3, 1X, I2)
      UPRICE = POUND * 100 + PENCE
C
C     ...LOOK FOR SENTINEL
      IF (      UNAM  .EQ. SENT1
     1    .AND. UDIGS .EQ. SENT2 ) GOTO 100
C     ...VALIDATE RECORD
      CODE
      	4 	UNAM
	6	&770000
	5	A
	4 	UNAM
	14 	6
	6	&770000
	5	B
	4 	UNAM
	14 	12
	6	&770000
	5	C
      FORTRAN
      IF ( A .LT. LETA   ) GOTO 20
      IF ( A .GT. LETZ   ) GOTO 20
      IF ( B .LT. LETA   ) GOTO 20
      IF ( B .GT. LETZ   ) GOTO 20
      IF ( C .NE. SPC    ) GOTO 20
      IF ( UDIGS .LT. 0  ) GOTO 20
      IF ( POUND .LT. 0  ) GOTO 20
      IF ( PENCE .LT. 0  ) GOTO 20
		
C
C     ...RECORD OK, CHECK SPACE IN UPDATE
      IF (UPDCNT .GT. UPDMAX) GOTO 30
C
C     ...INSERT RECORD IN UPDATE
      UPDCNT = UPDCNT + 1
      UPDATE(1, UPDCNT) = UNAM
      UPDATE(2, UPDCNT) = UDIGS
      UPDATE(3, UPDCNT) = UPRICE
      GOTO 10
C
C     ...REJECT RECORD
   20 CALL DIGITS (UDIGS)
      WRITE (3, 920) UNAM, (DIGS(I),I=1,4), POUND, PENCE
  920 FORMAT (17HUPDATE REJECTED: , A2, 1X, 4I1, 1X, I3, 1H., I2)
      ERRORS = ERRORS + 1
      GOTO 10
C
C     ...UPDATE FILE FULL
   30 WRITE (3, 930)
  930 FORMAT (48HTOO MANY UPDATES, RERUN WITH SMALLER UPDATE FILE)
      STOP
C
C     ...UPDATE FILE COMPLETE, STOP IF ERRORS
  100 IF ( ERRORS .LE. 0 ) GOTO 110
      WRITE (3, 940) ERRORS
  940 FORMAT (/22HUPDATE ABANDONED AFTER, I6, 7H ERRORS//)
      STOP
C 
C     ...SORT UPDATE FILE
 110  CALL SORT (UPDATE, UPDCNT)
C
C     UPDATE MASTER FILE
C
C     ...INSTRUCT OPERATOR TO LOAD MASTER TAPE
      WRITE (3, 950)
  950 FORMAT (//22H***LOAD MASTER TAPE***//)
      PAUSE
C
C     ...MERGE UPDATES INTO NEW MASTER TAPE
      MASCNT = 0
      UPDIDX = 1
C
C     ...GET NEXT MASTER RECORD
  120 READ  (1, 910) MNAM, MDIGS, POUND, PENCE
      MPRICE = POUND * 100 + PENCE
C
C     ...ARE THERE UPDATES STILL TO DO?
C     ...IF NOT COPY REST OF MASTER
  130 IF ( UPDIDX .GT. UPDCNT) GOTO 140
      UNAM  = UPDATE(1,UPDIDX)
      UDIGS = UPDATE(2,UPDIDX)
C
C     ...CHECK IF MASTER RECORD < UPDATE
      IF (       MNAM  .LT. UNAM  ) GOTO 140
      IF (       MNAM  .EQ. UNAM 
     1     .AND. MDIGS .LT. UDIGS ) GOTO 140
C
C     ... MASTER >= UPDATE SO INSERT UPDATE
      PRICE = UPDATE (3,UPDIDX) / 100.0
      CALLDIGITS (UDIGS)
      WRITE (1, 970) UNAM, (DIGS(I),I=1,4), PRICE
  970 FORMAT (A2, 1X, 4I1, F7.2)
C
C     ...ADVANCE INDICES, THEN LOOP
      MASCNT = MASCNT + 1    
      UPDIDX = UPDIDX + 1
C
C     ... IF AN UPDATE, NEED TO READ NEXT MASTER RECORD
      IF (       MNAM  .EQ. UNAM
     1     .AND. MDIGS .EQ. UDIGS ) GOTO 150
      GOTO 130
C
C     ...MASTER RECORD TO BE COPIED, THEN READ NEXT MASTER
  140 PRICE = MPRICE / 100.0
      CALL DIGITS (MDIGS)
      WRITE (1, 970) MNAM, (DIGS(I),I=1,4), PRICE
      MASCNT = MASCNT + 1     
C
C     ...CHECK FOR SENTINEL
 150  IF (       MNAM  .EQ. SENT1
     1     .AND. MDIGS .EQ. SENT2 ) GOTO 200     
      GOTO 120
C
C     ...MERGE COMPLETE
  200 WRITE (3, 980) UPDCNT, MASCNT
  980 FORMAT(//17HUPDATE COMPLETE: , I6, 11H UPDATES,  , 
     1I6, 16H RECORDS WRITTEN//)
      STOP
      END
C
C
C SUBROUTINE TO BREAK 4 DIGIT INTEGER INTO SINGLE DIGITS
C
C
      SUBROUTINE DIGITS (NUM)
      INTEGER DIGS(4)
      INTEGER NUM, N
      COMMON DIGS
      N = NUM
      DIGS(1) = N / 1000
      N = N - DIGS(1) * 1000 
      DIGS(2) = N / 100
      N = N - DIGS(2) * 100
      DIGS(3) = N /10
      DIGS(4) = N - DIGS(3) * 10
      RETURN
      END
<! HALT !>