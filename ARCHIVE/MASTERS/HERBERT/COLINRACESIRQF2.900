
*19

((COLIN EBY VINTAGE COMPUTER RACE AJH 4/6/2017)

[ COLIN QF ]
COLIN     4     DIGITS    (NUMBER OF DECIMAL PLACES)
          2     +0
          5     DP        (COUNTER FOR SCALING PI, 4.0)
          4     FOUR      (F:=4.0)
          5     F
          4     FOUR+1
          5     F+1
          4     PI        (PI := 3.141592653589793)
          5     WS
          4     PI+1
          5     WS+1
NEXTDP    11    QF        (SCALE PI AND F)
          8     QF+1
          4     WS        (WS *:= 10)
          12    TEN
          5     WS
          4     F         (F *:= 10)
          12    TEN
          5     F
          +0
          10    DP        (TEST FOR DP=0)
          4     DP
          9     NEXTDP
          4     +0        (ADD := "TRUE")
          5     ADD
          11    TRUNC     (PIMULT := TRUNCATE[WS])
          8     TRUNC+1
          11    QF
          8     QF+1
          4     WS
          5     PIMULT
          4     ZERO      (APPROX := 0.0)
          5     APPROX
          4     ONE       (N := -1.0)
          2     ZERO
          5     N
FOR       4     N         ("FOR" N := N + 2.0)
          1     TWO
          5     N
          4     APPROX    (TRUNCATE[APPROX])
          5     WS
          +0
          11    TRUNC
          8     TRUNC+1
          11    QF
          8     QF+1
          4     WS
          2     PIMULT    (PIMULT-APPROX)
          7     EXIT
          4     ADD
          7     TRUE      (TEST ADD)
          11    QF        (ADD="FALSE")
          8     QF+1
          4     F         (F)
          13    N         (/N)
          2     APPROX    (APPROX-F/N)
          8     UPDATE
TRUE      11    QF
          8     QF+1
          4     F         (F)
          13    N         (/N)
          1     APPROX    (APPROX-F/N)
UPDATE    5     APPROX
          +0              (LEAVE QF)
          4     ADD       (ADD := 'NOT' ADD)
          2     -1
          5     ADD
          11    QF
          8     QF+1
          8     FOR

EXIT      +0              (LEAVE QF)
          4     +141      (OUTPUT NEWLINES)
          15    6148
          4     +10
          15    6148
          15    6148
          4     WSADD     (SET UP WS FOR FLOAT)
          5     QF+3
          4     DIGITS    (COPY DIGITS TO WS)
          5     WS
          11    QF
          8     QF+1
          6     3         (FLOAT DIGITS)
          6     7         (INTEGER I/0)
          15    6148
          6     8         (FLOATING I/O)
          6     8191      (SET FORMAT)
          1     128       (SAMELINE, ALIGNED[8,0])
          4     PIMULT
          15    6148
          4     TWO       (N-2.0)
          2     N
          15    6148
          4     APPROX
          15    6148
          +0
          8     ;+0

ADD       >1              (FLAG FOR ADD OR SUBTRACT)
APPROX    >2              (CURRENT APPROXIMATION)
DP        >1              (NUMBER OF DECIMAL PLACES REQUIRED)
F         >2              (4 * 10^DP)
N         >2              (ITERATION COUNT)
PIMULT    >2
WS        >2

WSADD     0     WS        (ADDRESS WS+0)
DIGITS    +4
ZERO      +0              (O.0)
          +0
HALF      &200000
          &000000
ONE       &200000
          &000001
TWO       &200000         (+2.0)
          &000002
FOUR      &200000         (+4.0)
          &000003
TEN       &240000         (+10.0)
          &000004
PI        &311037         (+3.14159265)
          &265002

(SUBROUTINE TO CALCULATE INTEGRAL PART OF A REAL NUMBER)
TRUNC     >1
          4     WS+1      (UNPACK WS)
          6     &377600
          5     W+1
          4     WS+1
          14    11
          14    8181
          5     W+2
          4     WS
          5     W
          7     SAVE      (RESULT IS ZERO)
          4     W+2      (EXPONENT)
          9     ;+4
          1     -34
          9     ;+3
          8     SAVE      (EXPONENT>= 34 IMPLIES INTEGER)
          4     -34       (NEGATIVE EXPONENT - SHIFT OUT FRACTION)
          6     =0 8191   (EXPONENT LESS THAN 34 - SHIFT OUT FRACTION)
          1     =14 0
          5     ;+4
          0     W+1       (LSB OF MANTISSA)	
          14    1
          4     W         (MSB OF MANTISSA)
          >1              (CALCULATED SHIFT)
          3     W+1
          5     W
          4     +34
          5     W+2       (SET NEW EXPONENT)
SAVE      11    QF
          8     QF+2      (UNPACKED MODE)
          4     W         (STANDARDIZE)
          14    0
          6     1         (PACKED MODE)
          5     WS
          +0
          0     TRUNC
          /8    1

W         >3

<! Halt !>
