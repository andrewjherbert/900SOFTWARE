*DEFINE TEST [YES]
*PROG CIO
[ MESS QCHOP QDOUT RBEGIN QFCHIN OPENO OPENI CLOSEO CLOSEI MDABORT
 "RADOSP "RADOSQ "RADINP QRR QCHIN QDIN
 QDBEGIN QSTOP QFCHIN QFCHOP QDCHECK QDCOPEN QDCREATE QDCLOSE ]


(ERROR HANDLING AND MESSAGES---------------------------------)
ERF CALLG(MESS)
 \^"
 11 P
 8 P+1
 4 FLAG
 2 +OPENO
 7 OPENER
 CALLG(MESS)
 \ DO
 \ES
 \NOT
 \ EX
 \IST
 \"
 8 NULL

OPENER CALLG(MESS)
 \ AL
 \REA
 \DY
 \EXI
 \STS
 \"
 8 NULL

ER 4 RADOSQ
 1 +1
 2 RADOSP
 9 ENDPAR
 CALLG(MESS)
 \^BA
 \D P
 \ARA
 \MET
 \ER-
 \ "
 11 P
 8 P+1
 8 NULL

ENDPAR CALLG(MESS)
 \^PA
 \RAM
 \ETE
 \R M
 \ISS
 \ING
 \"
NULL 4 +0
 0 FILE
 /0 1
 /5 0 (SET NO DISC FILE FLAG)
 0 FLAG
 /0 0   (IF FROM OPENO - OUTPUT CHANNEL:=0)
 /8 1   (IF FROM OPENI - EXIT 1, IE NO MORE IP FILES)

P +0    (PRINT CURRENT PARAMETER)
 0 RADOSQ
 /4 0
 1 -27
 0 P
 /7 1
 1 +15
 10 RADOSQ
 7 ENDP
 1 \  ,
 7 P+1 (IGNORE SPACES)
 MQCHOP
 8 P+1

ENDP 4 RADOSQ
 5 RADOSP
 /8 1
ERTN +0 (QFCHIN/CHOP ERROR ROUTINE)
 0 ERTN
 /1 1
 ATB
 /4 0
 7 EOF (READ END OF FILE)
 2 +0
 5 WW
 CALLG(QRR)
 0 DATA
 8 STOP
EOF 4 RADINP
 5 RADOSQ
 CALLG(MESS)
 \^^"
 11 P
 8 P+1
 CALLG(MESS)
 \ NO
 \T T
 \ERM
 \INA
 \TED
 \"
 8 STOP (WHAT ELSE?)

(INITIALISE ROUTINES----------------------------------------)
RBEGIN +0 (INITIALISE)
 4 :RBEGIN/
 2 +385
 5 RADOSP
 5 RADOSQ
*IF TEST=YES [ (READ COMMAND INTO RADOS BUFFER)
 11 FLAG (PRECAUTIONARY CODE)
 11 OPENO
 11 FILE (WHEN TESTING MAY NOT TYPE &NAME, RIGHT)
 8 RSTART
 0 WW
RSTART CALLG(MESS)
 \^TY
 \PE
 \COM
 \MAN
 \D^>
 \"
 4 +OPENO
 5 FLAG
 4 +0
 5 WW
 4 :RBEGIN/
 2 +385
 5 RADOSP
 0 +QDIN/
 4 +3
 /5 QDIN
 CALLG(QCHIN)
 2 \  &
 7 LOOP
 8 RSTART
LOOP CALLG(QCHIN)
 0 RADOSP
 /5 0
 1 -27
 7 TESTEND (;)
 1 -36
 7 BACKAR
 1 +62
 7 RSTART
 10 RADOSP
 10 WW
 4 WW
 1 -90
 9 LOOP
 8 RSTART
BACKAR 4 RADOSP
 1 -1
 5 RADOSP
 4 WW
 1 -1
 9 RSTART
 5 WW
 8 LOOP
TESTEND 4 RADOSQ
 5 RADOSP
 CALLG(MESS)
 \^"  ]
*IFNOT TEST=YES [
 0 +QDOUT/
 4 +3
 /5 QDOUT]
 11 GET
 8 GET+1
 8 FOUNDC
 MQCHOP (COPY CRA NAME TO TTY)
 8 GET+1

FOUNDC 5 FTO (CLEAR FLAG)
 CALLG(MESS)
 \^"
 CALLG(QDBEGIN)
 0 RBEGIN
 /7 1
STOP 11 CLOSEO
 8 CLOSEO+1 (TRY AND SAVE OP FILE)
 CALLG(QSTOP) (QDBEGIN FAILED, OR QCHIN AT EOF)

(READ FROM COMMAND-------------------------------------------------)
GET +0
 0 RADOSP
 /4 0
 5 WW
 1 -27
 0 GET
 /7 1 (END OF COMMAND)
 1 +15
 10 RADOSP
 /7 1
 1 +12
 7 GET+1 (IGNORE SPACES)
 1 -16
 9 ER (LESS '0')
 1 -10
 9 OKC
 1 -7
 9 ER
 1 -26
 9 OKC (LETTER)
 8 ER


OKC 4 WW
 0 GET
 /8 2

(READ FILE NAME-------------------------------------------------)
FILE +0
 4 RADOSP
 5 RADOSQ (POINTS TO START OF PARAMETER)
 0 FILE
 /4 1
 1 =/5 20
 5 LIVE
 4 +0
 5 A
 5 A+1
 5 WORD
 0 -18 (ZERO FILE TABLE)
LIVE +0 (BECOMES /5 FTX+19)
 SKB
 8 LIVE
NEXT 5 SHIFT
 11 GET
 8 GET+1
 8 FEXIT
 0 SHIFT
 /8 ;+1
 0 +0
 14 6
 0 +0
 14 6
 0 WORD
 /1 A
 /5 A
 4 SHIFT
 10 SHIFT
 10 SHIFT
 1 -4
 9 NEXT+1
 4 WORD
 10 WORD
 7 NEXT
 11 GET
 8 GET+1
 8 FEXIT
 8 ER (PARAMETER TOO LONG)
FEXIT 4 A
 7 ER (NO PARAMETER AT ALL)
 6 &7777
 7 NUMERIC (OR SINGLE LETTER)
OKTER 0 FILE
 /0 1
 4 A
 /5 0
 4 A+1
 /5 1
 0 FILE
 /8 3

NUMERIC 0 FILE
 4 +0
 /0 1
 /5 0 (SET FLAG IN FILE TABLE)
 4 A
 14 8180
 6 &77
 1 -16 ('0')
 9 ER
 2 +10
 9 OKTER
 2 +10
 0 FILE
 /8 2

A +0
 +0
RADOSQ +0 (START OF PARAMETER)
RADOSP +0 (CURRENT CHARACTER)
RADINP +0 (LAST RADOSQ FOR IP FILE)
SHIFT +0
WORD +0

(OPEN FILE------------------------------------------------)
OPENO +0
 4 +OPENO
 5 FLAG
 11 FILE
 8 FILE+1
 0 FTO
 8 NODISO
 CALLG(QDCREATE)
 QPARAM(FTO,+0)
 CALLG(QDCHECK)
 QPARAM(FTO)
 7 OKPENO
 5 WW
 1 -15 (IE ALREADY IN USE)
FAIL 7 ERF
 CALLG(QRR)
 0 DATA
 8 NULL
OKPENO CALLG(QFCHOP)
 QPARAM(FTO,+7,FTO+20,ERTN)
 4 +7
NODISO 0 OPENO
 /8 1

OPENI +0
 4 +OPENI
 5 FLAG
 0 RADOSP
 /4 0
 1 -27
 0 OPENI
 5 FTI (IF ZERO WONT BE CLOSED)
 /7 1 (NO MORE FILES)
 11 FILE
 8 FILE+1
 0 FTI
 8 NODISI
 4 RADOSQ
 5 RADINP
 CALLG(QDCOPEN)
 QPARAM(FTI,+0)
 CALLG(QDCHECK)
 QPARAM(FTI)
 7 OKPENI
 5 WW
 1 -5 (NAME NOT FOUND)
 8 FAIL
OKPENI CALLG(QFCHIN)
 QPARAM(FTI,+6,FTI+20,ERTN)
 4 +6
 NODISI 7 ER
 0 OPENI
 /8 2

(CLOSE FILE--------------------------------------------)
CLOSEO +0
 4 FTO
 7 CLOXIT
 CALLG(QFCHOP)
 QPARAM(FTO,-7,FTO+20,ERTN)
 CALLG(QDCLOSE)
 QPARAM(FTO)
 CALLG(QDCHECK)
 QPARAM(FTO)
CLOXIT 0 CLOSEO
 /7 1
 5 WW
 CALLG(QRR)
 0 DATA
 0 CLOSEO
 /8 1

CLOSEI +0
 4 FTI
 7 CLIXIT
 CALLG(QDCLOSE)
 QPARAM(FTI)
 CALLG(QDCHECK)
 QPARAM(FTI)
CLIXIT 0 CLOSEI
 /7 1
 5 WW
 CALLG(QRR)
 0 DATA
 0 CLOSEI
 /8 1

(TABLES----------------------------------------------)

FLAG +0
FTI >+148
FTO >+148
DATA  \DSE
 0 0
WW +0
%


)
