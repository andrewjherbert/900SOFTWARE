




MUSIC;
"BEGIN" "INTEGER" NOOFNOTES,TYPE,NOTEPOINT,LENGTH,NOTENUM,N,
                  NOOFLOOPS;
        "SWITCH" SS:=RESTART;
        READER (3);      PUNCH(3);
RESTART:"PRINT" ''L@HOW MANY NOTES @;
        "READ" NOOFNOTES;
        "PRINT" ''L@HOW MANY MULTIPLES OF 10MSEC IN THE SHORTEST NOTE @;
        "READ" TYPE;
        "IF" NOOFNOTES<1 "OR" TYPE<1 "THEN"
        "BEGIN" "PRINT" ''S2@I CANNOT PLAY THIS@; "GOTO" RESTART "END";
"BEGIN" "REAL" TIMEN;
        "SWITCH" S:=LREAD;
        "INTEGER" "ARRAY" NOTES[0:2*NOOFNOTES];
        "CODE" "PROCEDURE" PLAY(NOTEARRAY);
                "VALUE" NOTEARRAY;
                "INTEGER" "ARRAY" NOTEARRAY;      "ALGOL";
        NOTEPOINT:=1;
        "PRINT" ''L@TYPE@,SAMELINE,DIGITS(4),NOOFNOTES,
        ' INTEGER PAIRS GIVING PITCH AND LENGTH OF EACH NOTE@;
LREAD:    "READ" NOTENUM,LENGTH;
        "IF" NOTENUM=0 "THEN"
        "BEGIN" NOOFLOOPS:=LENGTH*TYPE; N:=0 "END"      "ELSE"
        "IF" NOTENUM=100 "THEN" N:=-1      "ELSE"
        "BEGIN" TIMEN:=103.0*EXP(LN(2.0)*(NOTENUM-1.0)/12.0);
        N:=(TIMEN-103.0)/3.0;
        NOOFLOOPS:=LENGTH*TYPE*?4/TIMEN;
        "END";
        NOTES[NOTEPOINT]:=CHECKI(-NOOFLOOPS);
        NOTES[NOTEPOINT+1]:=CHECKI(N);
        NOTEPOINT:=NOTEPOINT+2;
        "IF" NOTEPOINT<2*NOOFNOTES "THEN" "GOTO" LREAD;
        "PRINT" ''L@THANKYOU@;
        PLAY(NOTES);
"END"
"END"
<! Halt !>