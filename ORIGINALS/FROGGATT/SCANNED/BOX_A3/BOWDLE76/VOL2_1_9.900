
((BOWDLER CHARACTER INPUT & OUTPUT SUBROUTINE, 1/9/76))

(May contain some loose ends)

^SFREE   +1900 $

[CHIP CHIPF CHIPL CHIPE CHOPF CHOPL CHOPE CHOPC
LISFLG ERRORL ERRORE]

(TELECODE INPUT SUBROUTINE)

(Appears, to the users programme, to read one 903/ISO
Telecode character from tape, and convert to 7-bit code,
but reads tape in one line at a time)

CHIPE    4    CHIPF
         7    FILBUF   (Test if first entry)
         4    CHIP
         1    -10      (C-10, test if <N>)
         7    FILBUF
         1    -10      (C-20, test if <H>)
         7    ;+0

READBF   0    BUFCT
         4    BUFCT
         1    -61
         9    ;+4
         /4   LINE-61  (Read character from m.s. end)
         14   8185
         8    ;+3
         /4   LINE     (Read character from l.s. end)
         6    &177
         5    CHIP
         10   BUFCT

         0    CHIPL
         /8   1

FILBUF   5    BUFCT

(+1)     4    +0
(+2)     15   2048
         5    CHIP
         11   BITCTL
         8    BITCTE
         14   17
         9    CH1      (Parity error)

         4    CHIP
         7    FILBUF+2 (Ignore blanks)
         6    &177     (Delete parity from track 8)
         5    CHIP
         1    -13
         7    FILBUF+2 (Ignore C.R.)
         1    -114
         7    FILBUF+2 (Ignore erases)

         4    CHIPF    (Read first charater)
         7    ;+2      (Jump iff first entry)
         8    ;+3
         4    CHIP
         5    CHIPF    (Store first character)

         0    BUFCT
         4    BUFCT
         1    -61
         9    ;+7
         /4   LINE-61
         14   8185
         4    CHIP
         14   7
         /5   LINE-61  (Store character in m.s. end)
         8    ;+3
         4    CHIP
         /5   LINE     (Store character in l.s. end)
         10   BUFCT

         4    CHIP
         1    -10      (C-10, test if <N>)
         7    CLECT
         1    -10      (C-20, test if <H>)
         7    CLECT

         4    BUFCT
         1    -121
         7    CH4      (Jump iff line too long)
         8    FILBUF+1

CLECT    5    BUFCT
         8    READBF

(TELECODE OUTPUT SUBROUTINE)

(Prints one or several 7-bit code
characters in 903/ISO Telecode)

CHOPE    5    CHOP
         6    +127
         5    OPC

         4    CHOPF
         7    BLANKS   (Test for 1st entry)

TESCOD   4    OPC
         1    -9       (C-9,  test if <T>)
         7    ;+4
         1    -1       (C-10, test if <N>)
         7    ;+3
         8    INSPTY
         4    +19      (Prefix <T> by <S>)
         1    +141     (Carriage Return)
         11   BITCTL
         8    MODTES

INSPTY   4    OPC
         11   BITCTL
         8    BITCTE
         14   7
         6    +128
         1    OPC
         11   BITCTL
         8    MODTES

         4    CHOP
         6    &177
         1    -10      (C-10, test if <N>)
         7    NL
         1    -10      (C-20, test if <H>)
         7    BLANKS
         8    OUTEND

NL       4    -1024    (-4*256)
         11   BITCTL
         8    MODTES

OUTEND   4    CHOP
         1    +128
         9    CHOPE

         6    &177
         0    CHOPL
         /8   1

BLANKS   4    -46080   (-180*256)
         11   BITCTL
         8    MODTES

         4    CHOPF
         2    -1
         5    CHOPF
         9    TESCOD
         8    OUTEND

MODTES   0    BITCTL
         5    TOTAL
         4    CHOPC
         9    ;+7
         4    TOTAL
         15   6144
         1    +256
         9    ;-2
         4    LISFLG
         /7   1
         4    TOTAL
         15   6148
         /8   1

(FAST BIT-COUNTING SUBROUTINE)

(Places the parity of the character in the
Acctr. on entry in the Acctr.)

BITCTE   0    BITCTL
         14   8188
         1    =14 1
         5    ;+6
         4    +0
         14   4
         1    =14 1
         5    ;+5
         2    &323130  (Set Q-register)
TOTAL    >1
         5    TOTAL
         2    &323130  (Set Q-register)
BITCTL   >1
         1    TOTAL
         /8   1

(WORKSPACES)

CHIPF    >1   (+0 on first entry to CHIPE S/R)
CHIPL    >1   (Link)
CHIP     >1   (Last character read in)
CHOPF    >1   (+0 on first entry to CHOPE S/R)
CHOPL    >1   (Link)
CHOPC    +1   (Output mode: <0 for Error)
BUFCT    >1   (Line buffer pointer)
OPC      >1
LINE     >61  (Line buffer)
CHOP     >1

(ERROR INDICATIONS)

CH1      4    +1
         8    ;+6
CH2      4    +2
         8    ;+4
CH3      4    +3
         8    ;+2
CH4      4    +4

         0    +0
         14   6
         1    £ 0.
         5    ERRNO
         11   ERRORL
         8    ERRORE
         £CH
         £I/O
         £ ER
         £ROR
ERRNO    >1

((ERROR ROUTINE))

[ERRORL ERRORE CHOPF CHOPC CHOPL CHOPE]

(Prints out the error message following the entry
instructions in alphanumeric group form, until
a full-stop is found, then stops)

ERRORL   >1

ERRORE   4    &400000
         5    CHOPC
         4    +0
         5    CHOPF
         4    +10
         11   CHOPL
         8    CHOPE

NEXTWD   4    -2
         5    COUNT
         10   ERRORL
         0    ERRORL
         /4   0

NEXTCH   5    WORD
         14   8180
         6    &77
         1    -14      (6-bit code test if .)
         7    STOP
         1    +46      (Restore 7-bit code)
         11   CHOPL
         8    CHOPE

         4    COUNT
         7    NEXTWD
         10   COUNT

         4    WORD
         14   6
         8    NEXTCH

STOP     4    +20
         11   CHOPL
         8    CHOPE
         8    ;+0

COUNT    >1
WORD     >1
<! Halt !>