
PROJECT NETWORK ANALYSIS;
"BEGIN""INTEGER"A,B,CP1,CP2,D1,D2,D3,I,J,K,L,N,N1,N2,LAST,PAGE,RUN,
                START,TIME,TOTAL,X;
       "INTEGER""ARRAY"PERT[1:300,1:7],NAME[1:20];
       "SWITCH"SS:=AA,AB,AC,AD,AE,AF,AG,AH,AI,PA,PB;

"PROCEDURE"HEADINGS(NAME,A,D1,D2,D3,TIME,RUN,PAGE,N);
       "INTEGER""ARRAY"NAME;
       "INTEGER"A,D1,D2,D3,TIME,RUN,PAGE,N;
             "BEGIN"
             "SWITCH"SSSS:=NS,SEC,MIN,HR,DAY,WK,MTH,YR,HA;
             READER(3);
             PUNCH(3);
             A:=1;
             "PRINT"''L1S24@PROJECT NETWORK ANALYIS'L2S8@PROJECT'S5@@,
             SAMELINE;
             OUTSTRING(NAME,A);
             "PRINT"''L1S45@TIME IS IN   @;
             "GOTO"SSSS[TIME];
         NS: "PRINT"'NEUTRAL TIME@;"GOTO"HA;
        SEC: "PRINT"'SECONDS@;"GOTO"HA;
        MIN: "PRINT"'MINUTES@;"GOTO"HA;
         HR: "PRINT"'HOURS@;"GOTO"HA;
        DAY: "PRINT"'DAYS@;"GOTO"HA;
         WK: "PRINT"'WEEKS@;"GOTO"HA;
        MTH: "PRINT"'MONTHS@;"GOTO"HA;
         YR: "PRINT"'YEARS@;"GOTO"HA;
         HA: "PRINT"''L1S8@DATE OF RUN   @,SAMELINE,DIGITS(3),D1,D2,
             D3,''L1S41@NUMBER OF ACTIVITIES@,SAMELINE,DIGITS(5),N,
             ''L1S8@RUN NUMBER  @,SAMELINE,DIGITS(3),RUN,''L1S47@@,
             'PAGE @,SAMELINE,DIGITS(2),PAGE,''L2S7@EVENT NO'S5@ACT@,
             ''S6@EARLIEST'S7@LATEST'S11@CRITICAL'L1S6@PREC   SUCC@,
             '   DUR   START  FINISH START  FINISH  FLOAT   PATH'L1@@;
             "END"OF HEADINGS;

"PROCEDURE"NUMBERS(PERT,N1,N2);
       "INTEGER""ARRAY"PERT;
       "INTEGER"N1,N2;
             "BEGIN"
             "INTEGER"I,J,K,M;
             PUNCH(3);
             "FOR"I:=N2"STEP"1"UNTIL"N1+N2-1"DO"
                   "BEGIN"
                   "PRINT"''L1S2@@;
                   "FOR"J:=1"STEP"1"UNTIL"7"DO"
                   "PRINT"DIGITS(6),SAMELINE,PERT[I,J];
                   "PRINT"DIGITS(6),SAMELINE,PERT[I,7]-PERT[I,5];
                   "IF"PERT[I,7]-PERT[I,5]=0"THEN"
                      "PRINT"SAMELINE,''S7@*@;
                   M:=I-K;
                   "IF"M=5"THEN"
                         "BEGIN"
                         "PRINT"''L1@@;
                         K:=K+5;
                         "END";
                   "END";
             "END"OF NUMBERS;

"PROCEDURE"SORT(PERT,N1);
       "INTEGER""ARRAY"PERT;
       "INTEGER"N1;
             "BEGIN"
             "INTEGER"PEN,I,J,E,A;
             "SWITCH"SSS:=SA;
             J:=I:=1;
         SA: E:=PERT[I,J]-PERT[I+1,J];
             "IF"E>0"THEN"
             "BEGIN";
             "FOR"J:=1"STEP"1"UNTIL"3"DO"
                   "BEGIN"
                   A:=PERT[I,J];
                   PERT[I,J]:=PERT[I+1,J];
                   PERT[I+1,J]:=A;
                   PEN:=1;
                   "END";
             "END""ELSE"
                   "IF"E=0"THEN"
                   "BEGIN"
                   J:=2;
                   "GOTO"SA;
                   "END";
             J:=1;
             I:=I+1;
             "IF"I<N1"THEN""GOTO"SA"ELSE"
               "IF"PEN=1"THEN"
                   "BEGIN"
                   N1:=N1-1;
                   I:=1;
                   PEN:=0;
                   "GOTO"SA;
                   "END";
             "END"OF SORT;

       READER(3);
       PUNCH(3);
       A:=1;
       INSTRING(NAME,A);
       "READ"D1,D2,D3,TIME,RUN;
       "READ"N;
       "READ"START;
       N1:=N;
       "FOR"I:=1"STEP"1"UNTIL"N"DO"
       "FOR"J:=1"STEP"1"UNTIL"3"DO"
       "READ"PERT[I,J];
       "READ"X;
       "IF"X=1"THEN""GOTO"AE;

       SORT(PERT,N1);

   AE: PERT[1,4]:=START;
       I:=2;
       PERT[1,5]:=START+PERT[1,3];
   AA: "IF"PERT[I,1]=1"THEN"
             "BEGIN"
             PERT[I,4]:=START;
             PERT[I,5]:=START+PERT[I,3];
             I:=I+1;
             "GOTO"AA;
             "END";
   AC: K:=PERT[I,1];
       "FOR"J:=1"STEP"1"UNTIL"I-1"DO"
             "BEGIN";
             "IF"PERT[J,2]=K"THEN"
                   "BEGIN"
                   "IF"PERT[J,5]>PERT[I,4]
                   "THEN"PERT[I,4]:=PERT[J,5];
                   "END";
             "END";
       PERT[I,5]:=PERT[I,4]+PERT[I,3];
       "IF"I=N"THEN""GOTO"AB"ELSE"
             "BEGIN"
             I:=I+1;
             "GOTO"AC;
             "END";
   AB: I:=1;
       LAST:=PERT[I,5];
   AG: "IF"LAST<PERT[I+1,5]"THEN"LAST:=PERT[I+1,5];
       I:=I+1;
       "IF"I=N"THEN""GOTO"AI"ELSE""GOTO"AG;
   AI: "FOR"I:=1"STEP"1"UNTIL"N-1"DO"
       PERT[I,7]:=LAST+1;
       PERT[N,7]:=LAST;
       I:=N-1;
       PERT[N,6]:=LAST-PERT[N,3];
   AD: "IF"PERT[I,2]=PERT[N,2]"THEN"
             "BEGIN"
             PERT[I,7]:=LAST;
             PERT[I,6]:=LAST-PERT[I,3];
             I:=I-1;
             "GOTO"AD;
             "END";
   AH: L:=PERT[I,2];
       "FOR"J:=N"STEP"-1"UNTIL"I+1"DO"
       "IF"PERT[J,1]=L"THEN"
             "BEGIN"
             "IF"PERT[J,6]<PERT[I,7]"THEN"
             PERT[I,7]:=PERT[J,6];
             "END";
       PERT[I,6]:=PERT[I,7]-PERT[I,3];
       "IF"I=1"THEN""GOTO"AF"ELSE"
             "BEGIN"
             I:=I-1;
             "GOTO"AH;
             "END";
   AF: "FOR"I:=1"STEP"1"UNTIL"N"DO"
             "BEGIN"
             "IF"PERT[I,7]-PERT[I,5]=0"THEN"
                   "BEGIN"
                   "IF"CP2=0"THEN"
                         "BEGIN"
                         CP1:=PERT[I,2];
                         TOTAL:=PERT[I,3];
                         CP2:=1;
                         "END"
                   "ELSE""IF"PERT[I,1]=CP1"THEN"
                         "BEGIN"
                         CP1:=PERT[I,2];
                         TOTAL:=TOTAL+PERT[I,3];
                         "END"
                   "ELSE"B:=1;
                   "END";
             "END";
       N1:=N;
       PAGE:=1;
       N2:=1;
       HEADINGS(NAME,A,D1,D2,D3,TIME,RUN,PAGE,N);
       "IF"N1<37"THEN"
             "BEGIN"
             NUMBERS(PERT,N1,N2);
             "GOTO"PB;
             "END"
       "ELSE"NUMBERS(PERT,35,N2);
       "PRINT"''L5@@;
       "FOR"I:=1"STEP"1"UNTIL"70"DO"
          "PRINT"SAMELINE,'-@;
       "PRINT"''L5@@;
       N1:=N1-35;
       N2:=36;
   PA: PAGE:=PAGE+1;
       HEADINGS(NAME,A,D1,D2,D3,TIME,RUN,PAGE,N);
       "IF"N1<42"THEN"
             "BEGIN"
             NUMBERS(PERT,N1,N2);
             "GOTO"PB;
             "END"
       "ELSE"NUMBERS(PERT,40,N2);
       "PRINT"''L5@@;
       "FOR"I:=1"STEP"1"UNTIL"70"DO"
          "PRINT"SAMELINE,'-@;
       "PRINT"''L5@@;
       N1:=N1-40;
       N2:=N2+40;
       "GOTO"PA;
   PB: "PRINT"''L2S4@NUMBER OF EVENTS ='S8@@,SAMELINE,PERT[N,2],
       ''L1S4@DURATION OF CRITICAL PATH=@,SAMELINE,TOTAL,''L1S4@@,
       'MORE THAN 1 CRITICAL PATH@,SAMELINE;
       "IF"B=1"THEN""PRINT"''S4@YES@"ELSE""PRINT"''S5@NO@;
"END";
<! Halt !>