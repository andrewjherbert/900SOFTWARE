

LP1 MK2 SIMPLEX;
"BEGIN""REAL"FRA,FAC,MAX,MIN;
     "INTEGER"I,J,M,N,IP,JP;
     "SWITCH"SS:=L1,L2,L3,L4,L5;
  L4:"PRINT"PUNCH(3),''L2S10@SOLUTION OF LINEAR PROGRAMMING PROBLEMS@,
     ''L2@PLEASE TYPE IN THE NUMBER OF ROWS AND COLUMNS IN THIS ROBLEM'L2@@;
     "READ"READER(3),M,N;
     "PRINT"PUNCH(3),''L2@NOW TYPE IN @,SAMELINE,DIGITS(2),M,' ROWS AND ,
     N,' COLUMNS'L1@@;
     "BEGIN""ARRAY"A[1:M,1:N];
        "FOR"I:=1"STEP"1"UNTIL"M"DO""FOR"J:=1"STEP"1"UNTIL"N"DO"
        "READ"READER(3),A[I,J];
     L3:MAX:=0;
        "FOR"J:=1"STEP"1"UNTIL"N-1"DO"
        "IF"A[1,J]>MAX"THEN""BEGIN"MAX:=A[1,J];JP:=J;"END";
        "IF"MAX"LE"0"THEN""GOTO"L1;MIN:=10000;
        "FOR"I:=2"STEP"1"UNTIL"M"DO"
        "BEGIN"FRA:=A[I,N]/A[I,JP];"IF"FRA<0"THEN""GOTO"L5;
             "IF"MIN>FRA"THEN""BEGIN"MIN:=FRA;IP:=I;"END";
     L5:"END";FAC:=A[IP,JP];
        "FOR"J:=1"STEP"1"UNTIL"N"DO"A[IP,J]:=A[IP,J]/FAC;
        "FOR"I:=1"STEP"1"UNTIL"M"DO"
        "BEGIN""IF"I=IP"THEN""GOTO"L2; FAC:=A[I,JP];
          "FOR"J:=1"STEP"1"UNTIL"N"DO"A[I,J]:=A[I,J]-A[IP,J]*FAC;
     L2:"END";
        "PRINT"PUNCH(3),''L3S5@@;
        "FOR"I:=1"STEP"1"UNTIL"N"DO""PRINT"PUNCH(3),DIGITS(2),
        SAMELINE,I,'  @;
        "PRINT"PUNCH(3),''L1@@;
        "FOR"I:=1"STEP"1"UNTIL"M"DO"
        "BEGIN""PRINT"PUNCH(3),DIGITS(2),I,'  @;
           "FOR"J:=1"STEP"1"UNTIL"N-1"DO""PRINT"PUNCH(3),FREEPOINT(3),
           SAMELINE,A[I,J];
           "PRINT"PUNCH(3),FREEPOINT(6),SAMELINE,A[I,N];
        "END";
        "PRINT"PUNCH(3),''L2@PIVOT WAS @,SAMELINE,DIGITS(2),IP,'  @,JP
        ''L@   MAX WAS @,MAX,'  MIN WAS @,MIN;
        "GOTO"L3;
      L1:"PRINT"PUNCH(3),''L2S10@MAXIMUM VALUE IS@,SAMELINE,-A[1,N],
        ''L2S10@END OF LINEAR PROGRAMMING PROBLEM'L5@@;
     "END";"GOTO"L4;
 "END";
