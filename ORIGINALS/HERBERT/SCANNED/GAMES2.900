<! Legible Header OOO   OOO  O   O OOOOO  OOO         OOO   !>
<! Legible Header    O O   O O   O O     O   O       O   O  !>
<! Legible Header      O   O OO OO O     O               O  !>
<! Legible Header   OO OOOOO O O O OOOO   OOO          O    !>
<! Legible Header    O O   O O   O O         O              !>
<! Legible Header    O O   O O   O O     O   O       O      !>
<! Legible Header OOO  O   O O   O OOOOO  OOO        OOOOO  !>
<! Legible Header                                           !>
GAMES;
"BEGIN"
"INTEGER"A,FIRSTGAME,B,D1,D2,GAMESELECT,C,D,E,F,G,H,I,J,K,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,
BALANCE,SCORE1,SCORE2,BLACK,WHITE,RATING,R1,R2,R3,R4,
BET,COUNT,FIRST,CH,MOVE,TEMP;

"REAL"L;

"SWITCH"SS:=NEWGAME,REMARK,READGAME,NIM,MASTERMIND,CRAPDICE,NOUGHTS,BANDIT,RETURN,
L190,L310,L80,L220,L350,L360,L430,L470,L540,L550,L610,L620,L640,L690,L710,
L760,L770,L830,L860,L920,L940,L970,L1060,L1090,L1110,L1020,
MMSTART,STRAIGHT,TRYAGN,READ,NOGOOD,MISTAKE,FINDNEXT,ANOG,OOPS,
DIVIDE,DIV,MORE,WINNER,FINISH,COMWINS,OPPWINS,START,AGAIN1,AGAIN2,
NONEQUIV,SELECTPILE,OPPSGO,LASTRESORT,ONE,TWO,YES,OUT,
OPPSTARTS,INPUT,SAFETEST,UNSAFE;

"INTEGER""ARRAY"Z1[1:3,1:3],Z2[1:9,1:3,1:3],Z3[1:3,1:3],X1[1:3],
PILE2[1:7],PILE3[1:7],BINARY[1:7],
PILE1[1:7],RESULT[1:7],PROPOS[1:4],BOARD[1:14,1:6],TABLE[1:6];

"BOOLEAN"F1,FOUND,WHICH,AGAIN;

"BOOLEAN""ARRAY"UP[1:5],UB[1:4];
  "PROCEDURE"IDSPLA;
  "BEGIN"AGAIN:="FALSE";
    MOVE:=MOVE+1;
    "IF"WHICH"THEN""FOR"Q:=1"STEP"1"UNTIL"4"DO"BOARD[MOVE,Q]:=PROPOS[Q];
    "IF"PROPOS[1]=7"THEN""GOTO"MISTAKE;
    "FOR"N:=1"STEP"1"UNTIL"MOVE"DO""BEGIN"
      "PRINT"''L@@;
      "FOR"Q:=1"STEP"1"UNTIL"4"DO""BEGIN"
        "IF"BOARD[N,Q]=1"THEN""PRINT"'  RED    @;
        "IF"BOARD[N,Q]=2"THEN""PRINT"'  GREEN  @;
        "IF"BOARD[N,Q]=3"THEN""PRINT"'  YELLOW @;
        "IF"BOARD[N,Q]=4"THEN""PRINT"'  WHITE  @;
        "IF"BOARD[N,Q]=5"THEN""PRINT"'  BLACK  @;
        "IF"BOARD[N,Q]=6"THEN""PRINT"'  BLUE   @;
        "END" OF COLOURS;
      "IF"N"NE"MOVE"OR"WHICH="FALSE""THEN"
        "PRINT"SAMELINE,BOARD[N,5],DIGITS(3),BOARD[N,6];
      "END" OF MOVES;
    "IF"WHICH="FALSE""THEN""GOTO"FINDNEXT;
    REMARK:"PRINT"''L@PLEASE MARK. @;
    "IF"MOVE=1"THEN""PRINT"'(BLACK FIRST, THEN WHITE) @;
    "READ"A,B;
"IF"A>4"OR"B>4"OR"A<0"OR"B<0"OR"A+B>4"THEN""BEGIN""PRINT"''L@EH ?  TRY AGAIN @;"GOTO"REMARK;"END";
    "IF"A=4"THEN""BEGIN"
      "IF"MOVE=1"THEN""PRINT"''L@THAT WAS TRIVIAL.
TRY SOMETHING A BIT HARDER NEXT TIME.@;
      "IF"MOVE>1"THEN""PRINT"''L@THANKS FOR THE GAME@;
     "IF"MOVE>1"AND"MOVE<4"THEN""PRINT"''L@IT WAS EASY. @;
      "IF"MOVE>3"AND"MOVE<7"THEN""PRINT"''L@IT WAS ABOUT AVERAGE@;
      "IF"MOVE>6"THEN""PRINT"''L@IT WAS A BIT TRICKY, BUT I WILL FORGIVE YOU@;
      "GOTO"ANOG;
      "END";
    BOARD[MOVE,5]:=A;
    BOARD[MOVE,6]:=B;
    "IF"MOVE=14"THEN""BEGIN"
      "PRINT"''L@I GIVE UP@;
      "GOTO"ANOG;
      "END";
    "GOTO"OUT;

FINDNEXT:
    "IF"BOARD[MOVE,5]=4"THEN""BEGIN"
      "IF"MOVE=1"THEN""PRINT"''L@THAT WAS JOLLY SMART. HOW DID YOU DO IT @;
      "IF"MOVE>1"AND"MOVE<5"THEN""PRINT"''L@YOUR PERFORMANCE IS VERY GGOD@;
      "IF"MOVE>4"AND"MOVE<9"THEN""PRINT"''L@YOUR PERFORMANCE IS ONLY AVERAGE@;
      "IF"MOVE>8"THEN""PRINT"''L@I AM SURE YOU CAN DO BETTER IF YOU TRY@;
      "GOTO"ANOG;
      "END";
    "IF"MOVE=14"THEN""BEGIN"
      "PRINT"''L@I AM AFRAID I HAVE RUN OUT OF PATIENCE@;
      "GOTO"ANOG;
      "END";
    "GOTO"OUT;

MISTAKE:
    "PRINT"''L@I THINK YOU HAVE MADE A MISTAKE@;

ANOG:
    "IF"WHICH"THEN"SCORE1:=SCORE1+MOVE"ELSE""BEGIN"
      SCORE2:=SCORE2+MOVE;
      RATING:=(1000*SCORE1)"DIV"SCORE2;
      "PRINT"''L@YOUR GAME RATING NOW IS: @,SAMELINE,RATING;
      "END";
    "PRINT"''L@TYPE YES OR NO FOR ANOTHER GAME: @;
    ADVANCE(3);
    "IF"BUFFER(3,'N@)"THEN""BEGIN"
      "PRINT"'O'L@THANK YOU FOR PLAYING. GOODBYE.'L2@@;
"GOTO"NEWGAME;
      "END";
    "IF"BUFFER(3,'Y@)"THEN""BEGIN"
      "PRINT"'ES'L@@;
      AGAIN:="TRUE";
      "GOTO"OUT;
      "END";
"GOTO"NEWGAME;

OUT:
    "END" OF IDSPLA PROCEDURE;

"PROCEDURE"MARK;
  "BEGIN"BLACK:=WHITE:=0;
    "FOR"Q:=1"STEP"1"UNTIL"4"DO"UP[Q]:=UB[Q]:="TRUE";
    "FOR"Q:=1"STEP"1"UNTIL"4"DO""IF"BOARD[T,Q]=PROPOS[Q]"THEN""BEGIN"
      BLACK:=BLACK+1;
      UB[Q]:=UP[Q]:="FALSE";
      "END";
    "FOR"N:=1"STEP"1"UNTIL"4"DO"
    "FOR"Q:=1"STEP"1"UNTIL"4"DO"
    "IF"UP[Q]"AND"UB[N]"AND"BOARD[T,N]=PROPOS[Q]"THEN""BEGIN"
      WHITE:=WHITE+1;
      UB[N]:=UP[Q]:="FALSE";
      "END";
    "END" OF MARK PROCEDURE;

"PROCEDURE"BIN(F);
"INTEGER"F;
"BEGIN"
"INTEGER"EXCHANGE;
EXCHANGE:=F;
"FOR"N:=1"STEP"1"UNTIL"7"DO"
BINARY[N]:=0;
"IF"F"GE"64"THEN""BEGIN"BINARY[7]:=1;
F:=F-64;"END";
"IF"F"GE"32"THEN""BEGIN"BINARY[6]:=1;
F:=F-32;"END";
"IF"F"GE"16"THEN""BEGIN"BINARY[5]:=1;
F:=F-16;"END";
"IF"F"GE"8"THEN""BEGIN"BINARY[4]:=1;
F:=F-8;"END";
"IF"F"GE"4"THEN""BEGIN"BINARY[3]:=1;
F:=F-4;"END";
"IF"F"GE"2"THEN""BEGIN"BINARY[2]:=1;
F:=F-2;"END";
"IF"F"GE"1"THEN""BEGIN"BINARY[1]:=1;
F:=F-1;"END";
F:=EXCHANGE;
"END";
"PROCEDURE"DIN(DENARY);"INTEGER""ARRAY"DENARY;
"BEGIN"Q:=0;"IF"DENARY[7]=1"THEN"Q:=Q+64;
"IF"DENARY[6]=1"THEN"Q:=Q+32;"IF"DENARY[5]=1"THEN"Q:=Q+16;
"IF"DENARY[4]=1"THEN"Q:=Q+8;"IF"DENARY[3]=1"THEN"Q:=Q+4;
"IF"DENARY[2]=1"THEN"Q:=Q+2;"IF"DENARY[1]=1"THEN"Q:=Q+1;
"END";
"PROCEDURE"CHEAT;
"BEGIN"
CH:=CH+1;
"IF"CH=1"THEN""PRINT"''L@I HAVE A FEELING YOU MAY BE TRYING TO CHEAT
PLEASE DONT@;
"IF"CH=2"THEN""PRINT"'
YOU HAVE BEEN WARNED. DONT CHEAT.@;
"IF"CH=3"THEN""PRINT"'
BLOODY STOP IT OR THERE WILL BE TROUBLE@;
"IF"CH=4"THEN""BEGIN""PRINT"'
I REFUSE TO PLAY ANY MORE@;
"GOTO"NEWGAME;
"END";
"GOTO"OPPSGO;
"END"OF CHEAT PROCEDURE;



  "PROCEDURE"CHARO;
    "BEGIN""IF"FOUND="FALSE""THEN"
    "BEGIN"F1:="TRUE";
    "FOR"X:=1"STEP"1"UNTIL"3"DO"
    "FOR"Y:=1"STEP"1"UNTIL"3"DO""BEGIN"
      "IF"Z2[Z,X,Y]<2"AND"Z1[X,Y]"NE"Z2[Z,X,Y]"THEN"F1:="FALSE";
      "IF"Z2[Z,X,Y]=3"AND"Z1[X,Y]"NE"0"THEN"F1:="FALSE";
    "END";
    "IF"F1"THEN""BEGIN"
        "FOR"X:=1"STEP"1"UNTIL"3"DO"
        "FOR"Y:=1"STEP"1"UNTIL"3"DO"
          "IF"Z2[Z,X,Y]=3"THEN"Z1[X,Y]:=-1;
          FOUND:="TRUE";
        "END";
    "END";
      TEMP:=Z1[1,1];
      Z1[1,1]:=Z1[3,1];
      Z1[3,1]:=Z1[3,3];
      Z1[3,3]:=Z1[1,3];
      Z1[1,3]:=TEMP;
      TEMP:=Z1[1,2];
      Z1[1,2]:=Z1[2,1];
      Z1[2,1]:=Z1[3,2];
      Z1[3,2]:=Z1[2,3];
      Z1[2,3]:=TEMP;
    "END";

  "PROCEDURE"REVERSE;
    "BEGIN""FOR"X:=1"STEP"1"UNTIL"3"DO"
      "FOR"Y:=1"STEP"1"UNTIL"3"DO" Z3[X,Y]:=Z1[X,Y];
      "FOR"X:=1"STEP"1"UNTIL"3"DO"
      "FOR"Y:=1"STEP"1"UNTIL"3"DO" Z1[X,Y]:=Z3[Y,X];
    "END";


READER(3);
PUNCH(3);


NEWGAME:
"PRINT"''L@DO YOU KNOW THE NUMBER OF A GAME YOU WANT TO PLAY ? @;
ADVANCE(3);
"IF"BUFFER(3,'Y@)"THEN""BEGIN""PRINT"'ES'L@TYPE IT IN THEN @;
"GOTO"READGAME;"END";"PRINT"'O@;
"PRINT"'


THE FOLLOWING GAMES ARE AVAILABLE:

                1. MASTERMIND
                2. NOUGHTS & CROSSES
                3. 3 PILE NIM
                4. ONE ARMED BANDIT
                5. DICE

 O.K. TYPE IN THE RIGHT NUMBER FOR THE GAME YOU WANT TO PLAY
 FOLLOWED BY A SPACE. @;

READGAME:"READ"GAMESELECT;
"IF"GAMESELECT<1"OR"GAMESELECT>5"THEN""BEGIN"
"PRINT"'
I THINK YOU MUST HAVE MADE A MISTAKE. GAME @,SAMELINE,DIGITS(2),GAMESELECT,'
DOES NOT EXIST...TRY AGAIN @;
"GOTO"READGAME;
"END";
"IF"GAMESELECT=1"THEN""GOTO"MASTERMIND;
"IF"GAMESELECT=2"THEN""GOTO"NOUGHTS;
"IF"GAMESELECT=3"THEN""GOTO"NIM;
"IF"GAMESELECT=5"THEN""GOTO"CRAPDICE;
"GOTO"BANDIT;
NIM:
FIRST:=1;CH:=0;
"PRINT"''L@DO YOU KNOW HOW TO PLAY ? @;
ADVANCE(3);
"IF"BUFFER(3,'N@)"THEN""PRINT"'O
WELL THERE ARE THREE PILES OF OBJECTS (OFTEN PLAYED WITH MATCHES)
THE TWO PLAYERS TAKE IT IN TURN TO REMOVE OBJECTS.
THEY CAN TAKE AS MANY AS THEY LIKE FROM ANY PILE
THEY CANNOT TAKE FROM MORE THAN ONE PILE IN ONE GO.
THE OBJECT IS TO REMOVE THE LAST MATCH(ES)
IT WILL BECOME CLEAR DURING THE GAME I HOPE.
@;
"IF"BUFFER(3,'Y@)"THEN""PRINT"'ES@;
L80:
"IF"IRANDOM/131071<0.5"THEN""GOTO"OPPSTARTS;
"PRINT"''L@I WILL START, IF YOU DONT MIND
PLEASE TYPE IN THE NUMBER ON EACH PILE, NOT MORE THAN 127 EACH'L@@;
INPUT:"READ"A,B,C;
"IF"A"LE"0"OR"C"LE"0"OR"B"LE"0"THEN""BEGIN""PRINT"'
ZEROS OR NEGATIVE NUMBERS ILLEGAL.....TRY AGAIN @;"GOTO"INPUT;"END";
"IF"A>127"OR"B>127"OR"C>127"THEN""BEGIN"
"PRINT"''L@TOO BIG.....LESS THAN 127 PLEASE @;"GOTO"INPUT;"END";
UNSAFE:
"IF"A+B+C=0"THEN""GOTO"OPPWINS;
BIN(A);
"FOR"N:=1"STEP"1"UNTIL"7"DO"
PILE1[N]:=BINARY[N];
BIN(B);
"FOR"N:=1"STEP"1"UNTIL"7"DO"PILE2[N]:=BINARY[N];
BIN(C);
"FOR"N:=1"STEP"1"UNTIL"7"DO"PILE3[N]:=BINARY[N];
"FOR"N:=1"STEP"1"UNTIL"7"DO""BEGIN"
RESULT[N]:=PILE1[N]+PILE2[N]+PILE3[N];
"IF"RESULT[N]=3"THEN"RESULT[N]:=1;
"IF"RESULT[N]=2"THEN"RESULT[N]:=0;
"END";
"COMMENT"THIS SECTION SEES WHETHER THE POSITION IS 'SAFE@;
SAFETEST:"IF"RESULT[1]+RESULT[2]+RESULT[3]+RESULT[4]+RESULT[5]
+RESULT[6]+RESULT[7]=0"THEN""GOTO"LASTRESORT;
"COMMENT"HAVING FOUND THE POSITION TO BE UNSAFE, A SAFE ONE IS FOUND;
"FOR"N:=1"STEP"1"UNTIL"7"DO""BEGIN"
PILE1[N]:=PILE1[N]+RESULT[N];
"IF"PILE1[N]=2"THEN"PILE1[N]:=0;
"END";
DIN(PILE1);
"IF"Q<A"THEN""BEGIN"
"PRINT"''L@I TAKE AWAY@,SAMELINE,(A-Q),' FROM PILE 1. PILES NOW CONTAIN@,
''L@@,Q,B,C,''L@@;
A:=Q;
"GOTO"OPPSGO;
"END";
"FOR"N:=1"STEP"1"UNTIL"7"DO""BEGIN"
PILE2[N]:=PILE2[N]+RESULT[N];
"IF"PILE2[N]=2"THEN"PILE2[N]:=0;
"END";
DIN(PILE2);
"IF"Q<B"THEN""BEGIN"
"PRINT"''L@I TAKE AWAY@,SAMELINE,(B-Q),' FROM PILE 2. PILES NOW CONTAIN@
,''L@@,A,Q,C,''L@@;
B:=Q;
"GOTO"OPPSGO;
"END";
"FOR"N:=1"STEP"1"UNTIL"7"DO""BEGIN"
PILE3[N]:=PILE3[N]+RESULT[N];
"IF"PILE3[N]=2"THEN"PILE3[N]:=0;
"END";
DIN(PILE3);
"IF"Q<C"THEN""BEGIN"
"PRINT"''L@I TAKE AWAY@,SAMELINE,(C-Q),' FROM PILE 3. PILES NOW CONTAIN@,
''L@@,A,B,Q;
C:=Q;
"END";
OPPSGO:"IF"A+B+C=0"THEN""GOTO"COMWINS;
"IF"FIRST=1"THEN""PRINT"''L@@,'YOUR GO. TYPE IN THE NUMBER YOU WISH TO REMOVE, THEN THE PILE @;
"IF"FIRST"NE"1"THEN""PRINT"''L@YOUR GO @;
"IF"FIRST=1"THEN"FIRST:=0;
"READ"P,I;
"IF"I>3"OR"I<1"THEN""BEGIN""PRINT"''L@PILE@,SAMELINE,DIGITS(3),I,' ?
BE SENSIBLE PLEASE....THERE ARE THREE PILES@;"GOTO"OPPSGO;"END";
"IF"P"LE"0"THEN"CHEAT;
"IF"I=1"THEN""BEGIN""IF"(A-P)<0"THEN"CHEAT;
A:=A-P;
"GOTO"UNSAFE;"END";
"IF"I=2"THEN""BEGIN""IF"(B-P)<0"THEN"CHEAT;
B:=B-P;
"GOTO"UNSAFE;"END";
C:=C-P;
"IF"C<0"THEN""BEGIN"C:=C+P;CHEAT;"END";
"GOTO"UNSAFE;
OPPSTARTS:"PRINT"''L@YOU START....HOW MANY IN EACH PILE ? @;
"READ"A,B,C;
"IF"A>127"OR"B>127"OR"C>127"THEN""BEGIN""PRINT"'
 TOO BIG.LESS THAN 127 PLEASE.  TRY AGAIN @;"GOTO"OPPSTARTS;"END";
"IF"A"LE"0"OR"B"LE"0"OR"C"LE"0"THEN""BEGIN""PRINT"'
SORRY...NO NUMBERS LESS THAN 1 TO START OFF WITH@;"GOTO"OPPSTARTS;
"END";
"GOTO"OPPSGO;
COMWINS:"PRINT"''L@I WIN. ANOTHER TRIUMPH FOR MODERN TECHNOLOGY@;
"PRINT"''L@WOULD YOU LIKE TO TRY AGAIN ? @;
ADVANCE(3);
"IF"BUFFER(3,'Y@)"THEN""BEGIN""PRINT"'ES
O.K THEN.....@;"GOTO"L80;"END""ELSE""BEGIN""PRINT"'O
I LIKE SOMEONE WHO KNOWS WHEN THEY ARE BEATEN@;
"GOTO"NEWGAME;
"END";
OPPWINS:"PRINT"''L@UNFORTUNATELY YOU HAVE WON. WELL DONE
ANOTHER GAME ? @;
ADVANCE(3);
"IF"BUFFER(3,'Y@)"THEN""BEGIN""PRINT"'ES
YOU WONT WIN THIS TIME.....@;"GOTO"L80;"END""ELSE""BEGIN""PRINT"'O
FINE.....PERHAPS YOU WILL PLAY SOMETHING ELSE@;
"GOTO"NEWGAME;"END";
LASTRESORT:"IF"A"GE"1"THEN""BEGIN"Q:=IRANDOM"DIV"13107;
"IF"Q=0"THEN""GOTO"LASTRESORT;
"IF"Q>A"THEN""GOTO"LASTRESORT"ELSE""PRINT"'
I TAKE AWAY@,SAMELINE,Q,' FROM PILE 1. PILES NOW CONTAIN
@,(A-Q),B,C;
A:=A-Q;
"GOTO"OPPSGO;
"END";
Q:=IRANDOM"DIV"13107;
"IF"Q=0"THEN""GOTO"LASTRESORT;
"IF"Q>B"THEN""GOTO"LASTRESORT;
"PRINT"''L@I TAKE AWAY@,SAMELINE,Q,' FROM PILE 2. PILES NOW CONTAIN
@,A,(B-Q),C;
B:=B-Q;
"GOTO"OPPSGO;

BANDIT:


COUNT:=0;
Y:=IRANDOM;
"IF"Y>130000"THEN"Y:=Y-130000;
DIVIDE:"IF"Y>100"THEN"
     "BEGIN"
     Y:=Y/10;
     "GOTO"DIVIDE;
     "END";
Y:=ENTIER(Y);
"PRINT"''L@YOUR STARTING BALANCE IS $@,SAMELINE,Y,''L@@,
'YOU PULL DOWN THE HANDLE BY PRESSING THE SPACE BAR.
JUST BET $0 TO STOP THE GAME.
HOW MANY DOLLARS WOULD YOU LIKE TO PUT IN ON YOUR FIRST PLAY ? @;
BALANCE:=Y;A:=0;
RETURN:COUNT:=0;A:=0;N:=1; "READ"BET;
"PRINT"''L@@;
"IF"BET=0"THEN""GOTO"FINISH;
"IF"BET<1"THEN"A:=1;
"IF"BET>BALANCE"THEN""BEGIN"
     "PRINT"''L@@,'VERY CLEVER. NOW BE SERIOUS@;
 "GOTO"RETURN;
"END";
"IF"BET=BALANCE"AND"BET"GE"6"THEN""PRINT"''L@@,'THAT SEEMS VERY RASH TO ME @;
MORE: N:=COUNT+1;
X1[N]:=IRANDOM;
"IF"X1[N]>130000"THEN"X1[N]:=X1[N]-130000;
DIV: "IF"X1[N]>10"THEN""BEGIN"
  X1[N]:=X1[N]/10;
  "GOTO"DIV;
"END";
X1[N]:=ENTIER(X1[N]);
"IF"X1[N]=1"THEN""PRINT"'PLUM    @;
"IF"X1[N]=2"THEN""PRINT"'CHERRY  @;
"IF"X1[N]=3"THEN""PRINT"'PEAR    @;
"IF"X1[N]=4"THEN""PRINT"'LEMON   @;
"IF"X1[N]=5"THEN""PRINT"'ORANGE  @;
"IF"X1[N]=6"THEN""PRINT"'BELL    @;
"IF"X1[N]>6"THEN""GOTO"MORE;
"IF"COUNT<2"THEN""BEGIN"
COUNT:=COUNT+1;
"GOTO"MORE;
"END";
"COMMENT"THIS SECTION SEES WHETHER A WIN HAS BEEN PLAYED;
"IF"A=1"THEN""BEGIN"
BET:=BET*2;"GOTO"WINNER;"END";
"IF"X1[1]=X1[2]"AND"X1[1]=X1[3]"THEN""BEGIN"BET:=BET*4;"GOTO"WINNER;"END";
"IF"X1[1]=X1[3]"THEN""BEGIN"BET:=ENTIER(BET*2.5);"GOTO"WINNER;"END";
"IF"X1[2]=X1[3]"THEN""BEGIN"BET:=BET*2;"GOTO"WINNER"END";
"PRINT"''L@@,'A LOSER...BALANCE: $@,SAMELINE,(BALANCE-BET),''L@@;
"IF"BALANCE-BET=0"THEN""GOTO"AGAIN2"ELSE""PRINT"'AGAIN ? @;
"PRINT"''L@@;
BALANCE:=BALANCE-BET;
"GOTO"RETURN;

WINNER:"PRINT"''L@@,'YOU WIN $@,SAMELINE,BET,' BALANCE: $@,BALANCE+BET;
"IF"A=1"THEN""PRINT"'SERVES YOU RIGHT FOR CHEATING@,''L@@;
BALANCE:=BALANCE+BET;
"IF"BALANCE"LE"0"THEN""GOTO"AGAIN2;
"PRINT"''L@@,'AGAIN ?@;
"PRINT"''L@@;
"GOTO"RETURN;
FINISH:"PRINT"''L@SO SOON ? WELL IT WAS A GOOD GAME@;
"GOTO"NEWGAME;
AGAIN2:"PRINT"''L@YOU HAVE RUN OUT OF MONEY...BETTER LUCK NEXT TIME @;
"GOTO"NEWGAME;



"COMMENT" THIS IS ADAPTED FROM THE BASIC STANDARD VERSION WITH VERY
LITTLE CHANGE. THEREFORE THERE ARE A LOT OF JUMPS WHICH ARE NOT
REALLY NECESSARY IN ALGOL, BUT ARE IN BASIC. THESE HAVE BEEN LEFT
IN FOR THE SAKE OF SIMPLICITY;

CRAPDICE:
C:=1;
"PRINT"''L@@,'THIS IS A GAME OF CRAP WITH THE COMPUTER AS YOUR OPPONENT.
DO YOU KNOW HOW TO PLAY ? @;
ADVANCE(3);
"IF"BUFFER(3,'Y@)"THEN""BEGIN"
"PRINT"'ES@;
"GOTO"L190;
"END"
"ELSE""PRINT"'O@;
"PRINT"''L@@,'    *A 7 OR AN 11 ON THE FIRST ROLL WINS
    *A 2, 3 0R 12 ON THE FIRST ROLL LOSES .

ANY OTHER NUMBER ROLLED BECOMES YOUR POINT* YOU CONTINUE TO ROLL.....
IF YOU GET YOUR POINT, YOU WIN, HOWEVER, SHOULD YOU ROLL A 7 YOU
LOSE AND THE DICE CHANGE HANDS.
JUST BET $0 TO QUIT @;

L190: "PRINT"''L@@;
Z:=5*ENTIER(10+11*(IRANDOM/131071));
"PRINT"'READY TO GO ? @;
ADVANCE(3);
"IF"BUFFER(3,'Y@)"THEN""BEGIN"
"PRINT"'ES@;
"GOTO"L220;
"END";
"PRINT"SAMELINE,'O@,''L@@;
"PRINT"'SEE YOU AROUND THEN @;
"GOTO"L1110;
L220:
"PRINT"''L@SPLENDID....YOU ARE GIVEN $@,SAMELINE,Z,' TO PLAY WITH@;
"PRINT"''L2@@;
"IF"Z-2*ENTIER(Z/2)=0"THEN""GOTO"L310;
W:=-1;
"PRINT"'I WILL ROLL FIRST.........@;
"PRINT"''L@@;
"GOTO"L350;
L310:W:=1;
"PRINT"''L@@,'YOU ROLL FIRST.........@,''L2@@;
L350:Q:=0;
L360:"PRINT"''L@@,'YOUR BET ? @;
"READ"L;
"IF"L=ENTIER(L)"THEN""GOTO"L430;
"PRINT"''L@@,'NO COINS PERMITTED PLEASE.....JUST NOTES@;
"GOTO"L360;
L430:"IF"L=0"THEN""GOTO"L1090;
"IF"L<Z+1"THEN""GOTO"L470;
"PRINT"'DONT TRY TO BET MORE THAN YOU HAVE PLEASE @;
"GOTO"L360;
L470:D1:=ENTIER(6*(IRANDOM/131071)+1);
D2:=ENTIER(6*(IRANDOM/131071)+1);
Q:=Q+1;
S:=D1+D2;
"IF"W>0"THEN""GOTO"L540;
"PRINT"'
I ROLL  @,SAMELINE,DIGITS(3),D1,' AND @,D2;
"GOTO"L550;
L540:"PRINT"''L@YOU ROLL  @,SAMELINE,DIGITS(3),D1,'  AND @,D2;
L550:"IF"Q"NE"1"THEN""GOTO"L860;
"IF" (S-2)*(S-3)*(S-12) = 0 "THEN""GOTO"L640;
"IF" (S-7)*(S-11)=0"THEN""GOTO"L710;
"IF"W>0"THEN""GOTO"L610;
"PRINT"'  SO MY POINT IS @,SAMELINE,S;
"GOTO"L620;
L610:"PRINT"'  SO YOUR POINT IS@,SAMELINE,S;
L620: P:=S;
"GOTO"L470;
L640:"PRINT"' AND CRAP OUT...@;
C:=1;
"IF"W>0"THEN""GOTO"L690;
Z:=Z+L;
"GOTO"L770;
L690:
Z:=Z-L;
"GOTO"L770;
L710:"PRINT"' AND PASS........@;
C:=1;
"IF"W>0"THEN""GOTO"L760;
Z:=Z-L;
"GOTO"L770;
L760:Z:=Z+L;
L770:"PRINT"''L@@;
"IF"Z<1"THEN""GOTO"L1060;
"PRINT"'YOU NOW HAVE $@,SAMELINE,Z;
"IF"C>0"THEN""GOTO"L830;
"PRINT"''L@@,'CHANGE DICE NOW.....@;
"PRINT"''L@@;
L830: W:=W*C;
Q:=0;
"GOTO"L360;
L860:"IF"S"NE"7"THEN""GOTO"L940;
"PRINT" ' AND LOSE....@;
C:=-1;
"IF"W>0"THEN""GOTO"L920;
Z:=Z+L;
"GOTO"L770;
L920:Z:=Z-L;
"GOTO"L770;
L940:"IF"S=P"THEN""GOTO"L970;
"PRINT"'....AND ROLL AGAIN.@;
"GOTO"L470;
L970:"IF"W>0"THEN""GOTO"L1020;
"PRINT"' AND MAKE MY POINT@;
Z:=Z-L;
"GOTO"L770;
L1020:"PRINT"' AND MAKE YOUR POINT@;
C:=1;
Z:=Z+L;
"GOTO"L770;
L1060:"PRINT"''L@@,'YOU HAVE RUN OUT OF MONEY...SORRY ABOUT THAT.@;
"GOTO"L1110;
L1090:"PRINT"'
THANKS FOR THE GAME. AND CONGRATULATIONS FOR BEING ABLE TO QUIT
WHILE YOU WERE STILL AHEAD.@;
L1110:"PRINT"''L@@,'WOULD ANYBODY LIKE TO SHOOT CRAPS WITH ME ? @;
ADVANCE(3);
"IF"BUFFER(3,'Y@)"THEN""BEGIN""PRINT"SAMELINE,'ES@;;
"GOTO"L190;
"END";
"PRINT"SAMELINE,'O@;
"GOTO"NEWGAME;

MASTERMIND:

    READER(3);
    PUNCH(3);
    TABLE[1]:=50;
    TABLE[2]:=39;
    TABLE[3]:=57;
    TABLE[4]:=55;
    TABLE[5]:=33;
    TABLE[6]:=53;
    "PRINT"''L@ COLOURS ARE:
  1  RED
  2  GREEN
  3  YELLOW
  4  WHITE
  5  BLACK
  6  BLUE
@;
    WHICH:="FALSE";
    SCORE1:=SCORE2:=0;

START:
    WHICH:="NOT"WHICH;
    "IF"WHICH"THEN""GOTO"STRAIGHT;

"COMMENT"THIS SECTION SETS THE USER A PROBLEM FOR THE COMPUTER TO MARK;
    MOVE:=0;
    "FOR"Q:=1"STEP"1"UNTIL"4"DO"PROPOS[Q]:=IRANDOM"DIV"26214+1;
    T:=0;
"PRINT"''L@RIGHT....NOW ITS YOUR TURN@;
TRYAGN:

    T:=T+1;
OOPS: "PRINT"''L@TYPE YOUR 4 COLOURS'L@@;
    "FOR"Q:=1"STEP"1"UNTIL"4"DO""BEGIN"
"COMMENT"THIS SECTION READS A USER SUGGESTION INTO BOARD;
      BOARD[T,Q]:=0;
READ: ADVANCE(3);
      "IF"DECODE(3)=56"THEN""GOTO"OOPS;
      "FOR"N:=1"STEP"1"UNTIL"6"DO""IF"DECODE(3)=TABLE[N]"THEN""BEGIN"
        BOARD[T,Q]:=N;
        "IF"N=1"THEN""PRINT"'ED    @;
        "IF"N=2"THEN""PRINT"'REEN  @;
        "IF"N=3"THEN""PRINT"'ELLOW @;
        "IF"N=4"THEN""PRINT"'HITE  @;
        "IF"N=5"THEN""PRINT"'CK  @;
        "IF"N=6"THEN""PRINT"'E   @;
        "END";
      "IF"DECODE(3)>16"AND"DECODE(3)<23"THEN""BEGIN"
        BOARD[T,Q]:=DECODE(3)-16;
        "PRINT"' @;
        "END";
      "IF"BOARD[T,Q]=0"THEN""GOTO"READ;
      "END";
    MARK;
    BOARD[T,5]:=BLACK;
    BOARD[T,6]:=WHITE;
    IDSPLA;
    "IF"AGAIN"THEN""GOTO"START;
    "GOTO"TRYAGN;


    "COMMENT"THIS SECTION PLAYS CONVENTIONAL GAME, W@ITH COMPUTER OFFERING
        PROPOSALS FOR THE USER TO MARK;

STRAIGHT:

    MOVE:=0;
    "PRINT"'
 FIND THE SWITCH AT THE BOTTOM RIGHT OF THIS TELETYPE, SWITCH
 IT TO LOCAL SO THAT I CANNOT SEE, AND TYPE YOUR FOUR COLOURS.
THEN SWITCH BACK TO LINE AND TYPE ANY LEGAL CHARACTER TO CONTINUE.
@;
    ADVANCE(3);
    "FOR"Q:=1"STEP"1"UNTIL"4"DO"PROPOSAL[Q]:=IRANDOM"DIV"26214+1;
    IDSPLA;
    "IF"AGAIN"THEN""GOTO"START;
    "COMMENT" NOW STEP THROUGH ALL POSSIBILITIES;
    "FOR"R1:=1"STEP"1"UNTIL"6"DO"
    "FOR"R2:=1"STEP"1"UNTIL"6"DO"
    "FOR"R3:=1"STEP"1"UNTIL"6"DO"
    "FOR"R4:=1"STEP"1"UNTIL"6"DO""BEGIN"
      PROPOS[1]:=R1;
      PROPOS[2]:=R2;
      PROPOS[3]:=R3;
      PROPOS[4]:=R4;
      "FOR"T:=1"STEP"1"UNTIL"MOVE"DO""BEGIN"
        MARK;
        "IF"BLACK"NE"BOARD[T,5]"OR"WHITE"NE"BOARD[T,6]"THEN""GOTO"NOGOOD;
        "END" OF T LOOP;
      IDSPLA;
      "IF"AGAIN"THEN""GOTO"START;
NOGOOD:
      "END" OF PROPOSAL LOOPS;
    PROPOS[1]:=7;
    IDSPLA;
    "IF"AGAIN"THEN""GOTO"START;
"GOTO"NEWGAME;
NOUGHTS:

"IF"FIRSTGAME=0"THEN""BEGIN""PRINT"'
PLEASE LOAD THE TAPE MARKED DATA INTO THE READER.
DO THIS BY LIFTING UP THE METAL FLAP AND PLACING THE TAPE IN.
THE TAPE SHOULD GO UNDER THE PERSPEX, WITH THE ARROWS ON THE TAPE
POINTING LEFT.
THE SMALL SPROCKET HOLES SHOULD BE AWAY FROM YOU.
PRESS THE BUTTON MARKED READ.@;
READER(1);
  "FOR"Z:=1"STEP"1"UNTIL"9"DO"
  "FOR"X:=1"STEP"1"UNTIL"3"DO"
  "FOR"Y:=1"STEP"1"UNTIL"3"DO" "READ"Z2[Z,X,Y];
FIRSTGAME:=FIRSTGAME+1;

"END";
READER(3);
  COUNT:=0;

  "PRINT"'
CO-ORDINATES ARE:

1,1 1,2 1,3

2,1 2,2 2,3

3,1 3,2 3,3

PLEASE SEPARATE CO-ORDINATES BY A SPACE, AND FINISH WITH A SPACE.
I MAY TAKE RATHER A LONG TIME THINKING.....SORRY


@;
"COMMENT" NOW CLEAR BOARD;

YES: "FOR"X:=1"STEP"1"UNTIL"3"DO"
  "FOR"Y:=1"STEP"1"UNTIL"3"DO" Z1[X,Y]:=0;
  COUNT:=COUNT+1;
  "IF"COUNT>3"THEN""BEGIN"COUNT:=0;
 Z1[1,1]:=-1;
  "END";
  "IF"COUNT=1"THEN"Z1[1,3]:=-1;
  "IF"COUNT=2"THEN"Z1[3,1]:=-1;
  "IF"COUNT=3"THEN"Z1[3,3]:=-1;
  MOVE:=0;
ONE:
  MOVE:=MOVE+1;
  "FOR"X:=1"STEP"1"UNTIL"3"DO""BEGIN"
    "PRINT"''L@@;
    "FOR"Y:=1"STEP"1"UNTIL"3"DO""BEGIN"
      "IF"Z1[X,Y]=0"THEN""PRINT"SAMELINE,' .@;
      "IF"Z1[X,Y]=1"THEN""PRINT"SAMELINE,' X@;
      "IF"Z1[X,Y]=-1"THEN""PRINT"SAMELINE,' O@;
    "END";
  "END";
  "IF"MOVE>4"THEN""BEGIN""PRINT"''L@TYPE YES OR NO FOR ANOTHER GAME: @;
    ADVANCE(3);
    "IF"BUFFER(3,'N@)"THEN""PRINT"'O@;
    "IF"BUFFER(3,'Y@)"THEN""BEGIN""PRINT"'ES@;
      "GOTO"YES;
    "END";
    "PRINT"''L@THANK YOU FOR PLAYING. GOODBYE@;
"GOTO"NEWGAME;
  "END";
  "PRINT"'

TYPE YOUR CO-ORDINATES: @;
  "READ"X,Y;
  "IF"X>3"OR"X<1"OR"Y>3"OR"Y<1"THEN""BEGIN"
    "PRINT"''L@DONT BE SILLY@;
    MOVE:=MOVE-1;
    "GOTO"ONE;
    "END""ELSE"
  "IF"Z1[X,Y]"NE"0"THEN""BEGIN"
    "PRINT"''L@DONT TRY TO CHEAT. WEVE ALREADY USED THIS.'L@@;
    MOVE:=MOVE-1;
    "GOTO"ONE;
  "END";
 Z1[X,Y]:=1;
  "FOR"Z:=1"STEP"1"UNTIL"9"DO""BEGIN"
    FOUND:="FALSE";
    "FOR"R:=1"STEP"1"UNTIL"4"DO"CHARO;
    REVERSE;
    "FOR"R:=5"STEP"1"UNTIL"8"DO"CHARO;
    REVERSE;
    "IF"FOUND"THEN""GOTO"TWO;
    "END";
"GOTO"NEWGAME;

TWO: "IF"Z<4"THEN""BEGIN"
    MOVE:=10;
    "PRINT"''L@BAD LUCK, IVE WON'L@@;
    "GOTO"ONE;
    "END";
  "IF"MOVE=4"THEN""PRINT"''L@WELL DONE. ITS A DRAW@;
  "GOTO"ONE;
"GOTO"NEWGAME;
 "END" OF PROG;


-1  3 -1
 2  2  2
 2  2  2

-1  2  2
 2  3  2
 2  2 -1

-1 -1  3
 2  2  2
 2  2  2

 2  2  2
 1  1  3
 2  2  2

 1  2  2
 2  1  2
 2  2  3

-1  0  3
 2  2  0
 2  2 -1

-1  0  0
 2  2  0
 2  2  3

-1  0  3
 0  0  0
 2  2  1

-1  2  2
 2  2  2
 2  2  3

<! Halt !><! Halt !><! Halt !><! Halt !><! Halt !><! Halt !><! Halt !><! Halt !>